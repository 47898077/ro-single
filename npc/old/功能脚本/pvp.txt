//===== rAthena Script ======================================= 
//= PvP NPCs
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf, Elias (og2)
//===== Current Version: =====================================
//= 2.3
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= PvP NPCs that are found in the Inns in major cities.
//= Includes Yoyo Mode, Nightmare Mode, and Event Mode.
//===== Additional Comments: ================================= 
//= 2.0 Narrators/GateKeepers,etc. Translated from the Official 2011 [Elias]
//= 2.1 Changed PVP Narrator and GateKeeper to Floating NPC [Elias]
//= 2.2 Some cleaning. [Euphy]
//= 2.3 Fixed grammatical errors. [Joseph]
//============================================================

-	script	PVP 讲解员::pvpe	-1,{
	mes "[PVP 讲解员]";
	mes "欢迎，欢迎!";
	mes "我是来为你们介绍 PVP 模式 的";
	mes "我就是讲解员了.";
	next;
	switch(select("什么是 PVP?","PVP 模式 是什么东东?","PVP规则是啥?","储存记忆点.","结束对话.")) {
		case 1:
			mes "[PVP 讲解员]";
			mes "简要来说，PVP就是英文";
			mes "‘Player VS Player’，也就是玩家之间各自为营对战的模式.";
			mes "想要解决个人恩怨的玩家，";
			mes "可以在这里进行PVP.";
			next;
			mes "[PVP 讲解员]";
			mes "告诉那边的那位";
			mes "^3355FF守门人^000000，告诉他";
			mes "你想去试试看，他就会让你进去PVP场地的.";
			next;
			mes "[PVP 讲解员]";
			mes "但是要求的最低等级为 31 级.";
			mes "而且要进入一个PVP场地要支付 500 zeny 的费用.";
			break;
		case 2:
			mes "[PVP 讲解员]";
			mes "如果你符合进入场地的要求,";
			mes "那么就选择一种模式进入吧.";
			mes "溜溜猴模式 或者是 梦魇模式.";
			next;
			mes "[PVP 讲解员]";
			mes "溜溜猴模式木有任何风险的啊嘎嘎嘎.";
			mes "没有任何惩罚的哦！可以自由体验 PVP 呢.";
			mes "如果你只是刚刚开始,想要学习技巧的,";
			mes "就使用溜溜猴模式吧!";
			next;
			mes "[PVP 讲解员]";
			mes "梦魇模式超危险的喇!";
			mes "要小心,小心,再小心哦!";
			mes "你死翘翘了会损失一定的经验值呢!";
			mes "唉,你也会有一定几率掉落身上的装备啊.";
			next;
			mes "[PVP 讲解员]";
			mes "但是你赢了的话,会有很多奖励的!";
			mes "为了避免不必要的损失,也为了避免你一时间的头脑发热,";
			mes "在去那里之前一定,一定要三思...";
			mes "不管怎么样,祝你好运!";
			break;
		case 3:
			mes "[PVP 讲解员]";
			mes "我们提供五个 PVP 地图.";
			mes "想去哪一个,就选择吧!";
			next;
			mes "[PVP 讲解员]";
			mes "每张地图都有人数的进入限制.";
			mes "所以在角落会有参与者/总数.";
			next;
			mes "[PVP 讲解员]";
			mes "在 PVP 模式里也有特别的经验点.";
			mes "这个经验点只在PVP区域中有效,所以没事的.";
			next;
			mes "[PVP 讲解员]";
			mes "刚进入场地的玩家,";
			mes "一般都拥有5个经验点.";
			mes "你赢了一次,就会增加一点经验值.";
			next;
			mes "[PVP 讲解员]";
			mes "一样的,你输了的话...";
			mes "你的经验值也会相应的下降.";
			mes "也不能大意啊!";
			next;
			mes "[PVP 讲解员]";
			mes "当你被打败,而且你的经验点";
			mes "小于等于 0 的时候,";
			mes "你就会从 PVP 场地中被传送出去.";
			mes "这就意味着,你的决斗结束了!";
			next;
			mes "[PVP 讲解员]";
			mes "但是,如果你被打倒,但是经验点";
			mes "大于0,你还是可以获得帮助的.";
			mes "其他的玩家可以帮助你的...";
			mes "明白了吗?";
			next;
			mes "[PVP 讲解员]";
			mes "PVP 场地里的格斗模式和外面的是一样的,";
			mes "基本操作方式都是相同的.";
			next;
			mes "[PVP 讲解员]";
			mes "但是,有一点还请注意...";
			mes "在 PVP 决斗场地区域,是无法储存记忆点的.";
			mes "记好了...这些规则可以帮你取得胜利.";
			break;
		case 4:
			mes "[PVP 讲解员]";
			mes "出生点储存完毕...";
			mes "感谢您的支持!";
			mes "我们会再见的.";
				if(strnpcinfo(4) == "morocc_in") { savepoint "morocc_in",141,139; }
				if(strnpcinfo(4) == "alberta_in") { savepoint "alberta_in",22,148; }
				if(strnpcinfo(4) == "prt_in") { savepoint "prt_in",54,137; }
				if(strnpcinfo(4) == "geffen_in") { savepoint "geffen_in",70,59; }
				if(strnpcinfo(4) == "payon_in01") { savepoint "payon_in01",142,46; }
			break;
		case 5:
			mes "[PVP 讲解员]";
			mes "在与魔物和其他人类的格斗中,";
			mes "这项人类之间的决斗 - PVP -";
			mes "让人们变得更加强大.";
			mes "下次再来吧!我们欢迎勇士!";
			break;
	}
	close;
}

//PVP  Narrator's Duplicates
morocc_in,144,138,4	duplicate(pvpe)	PVP 讲解员#pe1	84
alberta_in,22,146,4	duplicate(pvpe)	PVP 讲解员#pe2	84
prt_in,56,140,4	duplicate(pvpe)	PVP 讲解员#pe3	84
geffen_in,67,63,4	duplicate(pvpe)	PVP 讲解员#pe4	84
payon_in01,142,50,4	duplicate(pvpe)	PVP 讲解员#pe5	84
geffen_in,67,63,4	duplicate(pvpe)	PVP 讲解员#pe6	84

//Gate Keeper (Floating NPC, Locations below)
-	script	守门人::gkut	-1,{
	mes "[守门人]";
	mes "欢迎.";
	mes "我将为你开启 PVP 的大门，";
	mes "如果你有什么关于 PVP 模式和规则的问题，";
	mes "请询问旁边的这位讲解员.";
	next;
	switch(select("^FF5533PvP 梦魇模式^000000","^3355FFPvP 溜溜猴模式^000000","^3355FFPvP 争霸模式^000000","结束对话")) {
		case 1:
			mes "[守门人]";
			mes "抱歉,目前梦魇模式无法使用.";
			mes "请使用溜溜猴模式吧,";
			mes "很抱歉对您带来的不便.";
			break;
		case 2:
			mes "[守门人]";
			mes "进入的费用为 500 Zeny.";
			mes "请问要进入溜溜猴模式的场地吗?";
			next;
			if(select("走吧","不去了")==1) {
				if ((Zeny > 499) && (BaseLevel > 30)) {
					set Zeny, Zeny - 500;
					warp "pvp_y_room",51,23;
				}
				else {
					mes "[守门人]";
					mes "抱歉,但是您并未准备好.";
					mes "请检查您身上是否有 500 zeny的入场费.";
					mes "您的等级也要至少31级！";
				}
				close;
			}
			mes "[守门人]";
			mes "在与魔物和其他人类的格斗中,";
			mes "这项人类之间的决斗 - PVP -";
			mes "让人们变得更加强大.";
			mes "下次再来吧!我们欢迎勇士!";
			close;
		case 3:
			mes "[守门人]";
			mes "欢迎!";
			mes "您是有通行许可或者旁观券吧.";
			next;
			if ((countitem(7028) == 0) && (countitem(7029) == 0)) {
				mes "[守门人]";
				mes "啊，没有啊？抱歉了,";
				mes "这项 PVP 场地只给那些有通行许可,";
				mes "或者拥有旁观券的人开放.";
				mes "没有的话就无法进入了.";
				close;
			}
			mes "[守门人]";
			mes "非常感谢您的支持,祝您玩的愉快!";
			warp "pvp_room",51,23;
			break;
		case 4:
			mes "[守门人]";
			mes "在与魔物和其他人类的格斗中,";
			mes "这项人类之间的决斗 - PVP -";
			mes "让人们变得更加强大.";
			mes "下次再来吧!我们欢迎勇士!";
			break;
	}
	close;
}

//Gate Keeper Duplicates
morocc_in,144,142,4	duplicate(gkut)	守门人#gke1	83
alberta_in,26,146,4	duplicate(gkut)	守门人#gke2	83
prt_in,52,140,4	duplicate(gkut)	守门人#gke3	83
geffen_in,63,63,4	duplicate(gkut)	守门人#gke4	83
payon_in01,140,53,4	duplicate(gkut)	守门人#gke5	83

//Fight square Reception Staff (Locations below)
-	script	FSRS::fsr	-1,{
	if(strnpcinfo(4) == "pvp_y_room") {
		setarray .@Maps$[0],"pvp_y_8-1","pvp_y_8-2","pvp_y_8-3","pvp_y_8-4","pvp_y_8-5";
		setarray .@Name$[0],"普隆德拉","依斯鲁得","斐扬","艾尔贝塔","梦罗克";
		setarray .@Limit[0],128,128,128,128,128;
	} else {
		setarray .@Maps$[0],"pvp_n_8-1","pvp_n_8-2","pvp_n_8-3","pvp_n_8-4","pvp_n_8-5";
		setarray .@Name$[0],"Sandwich","Lock on","Four Room","Under cross","Compass Room";
		setarray .@Limit[0],64,32,32,32,32;
	}
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		set .@menu$, .@menu$+.@Name$[.@i]+" ["+getmapusers(.@Maps$[.@i])+" / "+.@Limit[.@i]+"]:";
	set .@menu$, .@menu$+"结束对话";
	mes "[PVP 竞技场地职员]";
	mes "请选择你想前往的PVP场地！";
	set .@i, select(.@menu$)-1;
	if (.@i == 5) close;
	if (getmapusers(.@Maps$[.@i]) >= .@Limit[.@i]) {
		mes "[PVP 竞技场地职员]";
		mes "抱歉，这个地图已经达到了人数上限了.";
	}
	else warp .@Maps$[.@i],0,0;
	close;

OnInit:
	waitingroom "无限制免费报名",0;
end;
}

//Fight Square Reception Staff
pvp_y_room,86,85,4	duplicate(fsr)	PVP 竞技场地职员#f1	105
pvp_n_room,30,85,4	duplicate(fsr)	PVP 竞技场地职员#f2	105

pvp_n_8-4,33,122,0	warp	Link_1-11	0,2,pvp_n_8-4,0,0
pvp_n_8-4,62,84,0	warp	Link_1-12	1,1,pvp_n_8-4,0,0
pvp_n_8-4,49,71,0	warp	Link_1-21	1,1,pvp_n_8-4,0,0
pvp_n_8-4,56,166,0	warp	Link_1-22	1,1,pvp_n_8-4,0,0
pvp_n_8-4,99,180,0	warp	Link_1-31	2,1,pvp_n_8-4,0,0
pvp_n_8-4,137,84,0	warp	Link_1-32	1,1,pvp_n_8-4,0,0
pvp_n_8-4,150,71,0	warp	Link_1-41	1,1,pvp_n_8-4,0,0
pvp_n_8-4,144,166,0	warp	Link_1-42	1,1,pvp_n_8-4,0,0
pvp_n_8-5,30,30,0	warp	Link_1-99	3,3,pvp_n_8-5,100,100

// Event Contestant Entrance
//============================================================ 
pvp_room,54,85,4	script	场地签到员#1	105,{
	if (countitem(7028) == 0) {
		mes "[PVP 战斗场地签到员]";
		mes "额，你是怎么来这儿的?";
		mes "这里只对参战者开放.";
		mes "想要知道更多的话请详细询问您右方的注册员.";
		close;
	}

	if (.pvp_close_n_room == 1 && .pvp_close_s_room == 1 && .pvp_close_w_room == 1 && .pvp_close_e_room == 1) {
		Set .pvp_close_n_room,0;
		Set .pvp_close_s_room,0;
		Set .pvp_close_w_room,0;
		Set .pvp_close_e_room,0;
	}

	if (select("战斗场地一 :取消") == 1) {
		mes "[PVP 战斗场地签到员]";
		mes "'"+ strcharinfo(0) +"'";
		mes "准备好了吗?!";
		next;
		if (select("Yes!:No!") == 1) {
			mes "[PVP 战斗场地签到员]";
			mes "好的，我这就将你送去.";
			mes "祝你好运!";
			close2;
			delitem 7028,1; //Invite_For_Duel
			while(1) {
				switch(rand(1,4)) {
				case 1:
					if (.pvp_close_n_room == 0) {
						set .pvp_close_n_room,1;
						warp "pvp_2vs2",40,59;
						end;
					}
				case 2:
					if (.pvp_close_s_room == 0) {
						set .pvp_close_s_room,1;
						warp "pvp_2vs2",59,40;
						end;
					}
				case 3:
					if (.pvp_close_w_room == 0) {
						set .pvp_close_w_room,1;
						warp "pvp_2vs2",20,40;
						end;
					}
				case 4:
					if (.pvp_close_e_room == 0) {
						set .pvp_close_e_room,1;
						warp "pvp_2vs2",40,20;
						end;
					}
				}
			}
		}
		mes "[PVP 战斗场地签到员]";
		mes "准备好了就请来找我吧.";
		close;
	}
	close;

OnInit:
	Set .pvp_close_n_room,0;
	Set .pvp_close_s_room,0;
	Set .pvp_close_w_room,0;
	Set .pvp_close_e_room,0;
	waitingroom "参战者专用入口",0;
	end;
}

// Spectator Entrance
//============================================================ 
-	script	观众导览员#dum::PVPSpectator	-1,{
	if (countitem(7029) == 0) {
		mes "[PVP 战斗场地申请员]";
		mes "这里就是观众入口处了.";
		mes "参战者入口归您左方的签到员管理.";
		close;
	}

	if (select("战斗场地一:取消") == 1) {
		mes "[PVP 战斗场地申请员]";
		mes "好的，感谢支持，祝您开心!";
		close2;
		delitem 7029,1; //Admission_For_Duel
		switch(rand(1,4)) {
		case 1: warp "pvp_2vs2",39,7; end;
		case 2: warp "pvp_2vs2",39,73; end;
		case 3: warp "pvp_2vs2",7,39; end;
		case 4: warp "pvp_2vs2",73,39; end;
		}
	}
	close;

OnInit:
	waitingroom "观众专用入口",0;
	end;
}

pvp_room,62,85,4	duplicate(PVPSpectator)	观众导览员#1	105
pvp_room,70,85,4	duplicate(PVPSpectator)	观众导览员#2	105
pvp_room,78,85,4	duplicate(PVPSpectator)	观众导览员#3	105
pvp_room,86,85,4	duplicate(PVPSpectator)	观众导览员#4	105

// Spectator Warps
//============================================================ 
-	script	战斗场地职员#dum::PVPSpecWarp	-1,{
OnTouch:
	mes "[战斗场地职员]";
	mes "请问需要什么帮助吗?";
	if (select("进入中央观众席.:离开战斗场地.") == 1) {
		warp "pvp_2vs2",38,38;
		end;
	}
	warp "pvp_room",84,39;
	end;
}

pvp_2vs2,5,4,0	duplicate(PVPSpecWarp)	战斗场地职员#1	45,1,1
pvp_2vs2,5,74,0	duplicate(PVPSpecWarp)	战斗场地职员#2	45,1,1
pvp_2vs2,74,74,0	duplicate(PVPSpecWarp)	战斗场地职员#3	45,1,1
pvp_2vs2,74,5,0	duplicate(PVPSpecWarp)	战斗场地职员#4	45,1,1

pvp_2vs2,40,40,0	script	战斗场地职员#5	45,1,1,{
	mes "[战斗场地职员]";
	mes "请问需要什么帮助?";
	if (select("去观众席边缘.:离开战斗场地.") == 1)
		switch(rand(1,4)) {
		case 1: warp "pvp_2vs2",39,7; end;
		case 2: warp "pvp_2vs2",39,73; end;
		case 3: warp "pvp_2vs2",7,39; end;
		case 4: warp "pvp_2vs2",73,39; end;
		}
	warp "pvp_c_room",84,39;
	end;
}

// PVP Area Exit warp
//============================================================ 
pvp_room,51,19,0	script	出口#eventpvp	45,4,2,{
OnTouch:
	mes "[战斗场地职员]";
	mes "玩的开心吗?";
	mes "呵呵，请问想去哪里呢?";
	next;
	switch(select("普隆德拉:梦罗克:吉芬:斐扬:艾尔贝塔:取消")) {
	case 1: warp "prontera",107,60; end;
	case 2: warp "morocc",157,96; end;
	case 3: warp "geffen",120,36; end;
	case 4: warp "payon",96,100; end;
	case 5: warp "alberta",41,243; end;
	case 6: close;
	}
}
