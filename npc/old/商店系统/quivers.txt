//===== rAthena Script =======================================
//= Arrow Quiver Event
//===== By: ==================================================
//= Muad_Dib (Prometheus Project); L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= [Official Conversion]
//= Turns arrows into Arrow Quivers.
//= Breakdown of Subroutine "S_BuyQuiver"
//= arg(0): Type of Arrow to be packaged (item ID).
//= arg(1): How many of each 'getarg(0)' arrow per quiver.
//= arg(2): The cost of making a 'getarg(0)' quiver.
//= arg(3): The quiver given by the NPC (item ID).
//===== Additional Comments: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= Converted to rAthena format by Dr.Evil
//= added prize to quest - 500 Zeny
//= Fixed a few spelling errors. [Nexon]
//= 1.1 Added Holy Arrow Quivers [Playtester]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Removed "Holy Arrows" from the list.
//============================================================

payon_in01,5,134,5	script	发明家 钦斯	89,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "[发明家 钦斯]";
		mes "嘿, 你现在的负重太重了";
		mes "你为什么不储存些到卡普拉那? 你把东西去整理一下再来,我们可以谈一谈,好吗?";
		close;
	}
	mes "[发明家 钦斯]";
	mes "我是天才发明家钦斯";
	mes "毫不夸张的说,我绝对是";
	mes "^663300本世纪最伟大的发明家^000000.";
	mes "别用这么怀疑的眼神看着我";
	next;
	mes "[发明家 钦斯]";
	mes "我这一次发明了一个";
	mes "必将载入史册的物品!";
	mes "那就是... ^663300魔法箭袋^000000 !! ";
	mes "弓箭的革命!";
	Emotion e_no1;
	next;
	mes "[发明家 钦斯]";
	mes "我已经对这个神奇的魔法箭袋进行了多年的研究,日夜工作,现在我终于知道了如何浓缩魔法箭袋的秘诀,神奇的魔法箭袋能让你减轻身上的负重,可以携带更多的箭矢!";
	next;
	mes "[发明家 钦斯]";
	mes "你想要尝试一下我的魔法箭袋吗?我毫不怀疑,像你这样的人能欣赏我的天才!";
	next;
	switch(select("箭袋:铁箭袋:钢铁箭袋:神之金属箭袋:火箭袋:银箭袋:风灵箭袋:地灵箭袋:水灵箭袋:影子箭袋:无形箭袋:铁锈箭袋")) {
	case 1: callsub S_BuyQuiver,1750,500,500,12004;
	case 2: callsub S_BuyQuiver,1770,500,500,12005;
	case 3: callsub S_BuyQuiver,1753,500,500,12006;
	case 4: callsub S_BuyQuiver,1765,500,500,12007;
	case 5: callsub S_BuyQuiver,1752,500,500,12008;
	case 6: callsub S_BuyQuiver,1751,500,500,12009;
	case 7: callsub S_BuyQuiver,1755,500,500,12010;
	case 8: callsub S_BuyQuiver,1756,500,500,12011;
	case 9: callsub S_BuyQuiver,1754,500,500,12012;
	case 10: callsub S_BuyQuiver,1767,500,500,12013;
	case 11: callsub S_BuyQuiver,1757,500,500,12014;
	case 12: callsub S_BuyQuiver,1762,500,500,12015;
	}
	//This doesn't seem to be accessable...
	//mes "[发明家 钦斯]";
	//mes "Is there";
	//mes "anything you want?";
	//close;

S_BuyQuiver:
	if (countitem(getarg(0)) > 499) {
		mes "[发明家 钦斯]";
		mes "好极了!";
		mes "你携带了什么箭矢? 我会给你一个箭袋,可以携带 "+getarg(1)+" 个 "+getitemname(getarg(0))+"!不过需要手续费 ^FF3131"+getarg(2)+" Zeny^000000.";
		next;
		switch(select("我想尽可能多的换成箭袋:只换一个箭袋:取消")) {
		case 1:
			set .@arrows,countitem(getarg(0));
			set .@quiver,.@arrows / getarg(1);
			set .@arrows_used,.@quiver * getarg(1);
			set .@arrow_zeny01,.@quiver * getarg(2);
			mes "你一共有";
			mes "箭矢数量: ^3131FF"+.@arrows+"^000000 支";
			mes "如果全部兑换的话,";
			mes "最多可以换箭袋";
			mes "箭袋数量: ^3131FF"+.@quiver+"^000000 个";
			mes "手续费: ^3131FF"+.@arrow_zeny01+" Zeny^000000";
			next;
			mes "[发明家 钦斯]";
			mes "你确定";
			mes "要将身上的所有";
			mes "箭矢换成";
			mes "箭袋吗?";
			next;
			if (select("是的:取消") == 1) {
				if (.@arrow_zeny01 < Zeny) {
					mes "[发明家 钦斯]";
					mes "这是你的箭袋,请收好!";
					mes "另外请记住 ^FF0000你身上的负重量超过最大负重限额达到90%的话,就不能使用箭袋^000000.";
					set zeny,zeny-.@arrow_zeny01;
					delitem getarg(0),.@arrows_used; //Arrow
					getitem getarg(3),.@quiver; //Quiver
					next;
					mes "[发明家 钦斯]";
					mes "所以...";
					mes "只要保持";
					mes "自己的背包";
					mes "不要太满,";
					mes "就应该不会有问题!";
					mes "放心吧.";
					close;
				}
				else {
					mes "[发明家 钦斯]";
					mes "很抱歉,你没有足够的金钱来完成交易!";
					mes "为了发明箭袋,我倾尽了所有......";
					close;
				}
			}
			mes "[发明家 钦斯]";
			mes "你居然改变注意了!";
			mes "就差一步";
			mes "你就能享受这世上最犀利的发明给你带来的便利...";
			close;
		case 2:
			if (Zeny > getarg(2)) {
				mes "[发明家 钦斯]";
				mes "这是你的箭袋,请收好!";
				mes "另外请记住 ^FF0000你身上的负重量超过最大负重限额达到90%的话,就不能使用箭袋^000000.";
				set Zeny, Zeny-getarg(2);
				delitem getarg(0),getarg(1); //Arrow
				getitem getarg(3),1; //Quiver
				next;
				mes "[发明家 钦斯]";
				mes "所以...";
				mes "只要保持";
				mes "自己的背包";
				mes "不要太满,";
				mes "就应该不会有问题!";
				mes "放心吧.";
				close;
			}
			else {
				mes "[发明家 钦斯]";
				mes "你甚至没";
				mes "有" + getarg(2)+" Zeny?";
				mes "很抱歉,我没有想到你会那么...";
				mes "^333333贫困^000000.";
				mes "哎...";
				close;
			}
		case 3:
			mes "[发明家 钦斯]";
			mes "什么...?";
			mes "你看不出来这个发明可以永远改变箭矢的历史?! 未来就是现在!";
			close;
		}
	}
	else {
		mes "[发明家 钦斯]";
		mes "你可以可以最多放500支箭在箭筒里. 这是我用特殊秘方制作的,";
		mes "所以使用箭袋的重量要比直接拿箭矢轻的多.";
		next;
		mes "[发明家 钦斯]";
		mes "这是一个科学的奇迹! 你可以带500支箭和500Zeny 来和我交换一个箭袋,来体验一下吧";
		close;
	}
}
