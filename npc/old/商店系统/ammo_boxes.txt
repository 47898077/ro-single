//===== rAthena Script =======================================
//= Magazine Dealers
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Turns bullets into magazines/packs.
//===== Additional Comments: =================================
//= 1.0 First version. [SinSloth]
//= 1.1 Optimized version - Reduced to only one function [SinSloth]
//= 1.2 Optimized^2, corrected npc's name [ultramage]
//= 1.2a Optimized. Please, ommit extra NPC names [Lupus]
//============================================================

alberta,118,157,3	script	弹药包经销商 凯尼::mdk	83,{

	mes "[凯尼]";	
	mes "我是弹药背包商人 凯尼!";
	if(BaseJob != Job_Gunslinger) {
		mes "我在这里帮助枪手们压缩子弹，压缩子弹或制造弹匣.";
		next;
		mes "[凯尼]";
		mes "你看起来可不像个枪手";
		mes "看来，在你逛完后";
		mes "我必须要请你离开.";
		mes "这里没有你所需要的东西.";
		close;
	}
	mes "如果觉得弹药太重时，";
	mes "记得随时要马上来找我";
	next;
	mes "[凯尼]";
	mes "我所制作的弹药背包是";
	mes "可以让弹药携带起来";
	mes "既轻松又方便，";
	mes "好~ 那么好好逛逛吧~";
	next;
	switch(select("子弹盒 风:子弹盒 暗:子弹盒 毒:子弹盒 水:子弹盒 火:弹药桶:红色弹药桶:银色弹药桶:取消")) {
	case 1: callfunc "Func_Casing",13204,12144; break;
	case 2: callfunc "Func_Casing",13206,12145; break;
	case 3: callfunc "Func_Casing",13205,12146; break;
	case 4: callfunc "Func_Casing",13207,12147; break;
	case 5: callfunc "Func_Casing",13203,12148; break;
	case 6: callfunc "Func_Casing",13200,12149; break;
	case 7: callfunc "Func_Casing",13202,12150; break;
	case 8: callfunc "Func_Casing",13201,12151; break;
	default:
		mes "[凯尼]";
		mes "好吧";
		mes "如果还有什么别的";
		mes "我能帮上忙的话,告诉我.";
		close;
	}
	close;
}

function	script	Func_Casing	{

	mes "[凯尼]";
	mes "请输入你需要的数量.";
	next;
	mes "[凯尼]";
	mes "^0000FF" +getitemname(getarg(1))+ "^E40CAA 制造";
	if(getarg(0) == 13202) 
		mes "需要花费500个血色子弹";
	else
		mes "每个需要花费 ^FF0000" +getitemname(getarg(0))+ "^000000 500个";
	mes "和 500 zeny.";
	next;
	mes "[凯尼]";
	mes "你最大可以交换50个.";
	mes "如果想取消交易请输入0.";
	next;
	input .@amount;
	mes "[凯尼]";
	if(.@amount < 1) {
		mes "好吧,";
		mes "如果有什么其他的";
		mes "我能帮的上忙的,告诉我.";
		close;
	}
	if(.@amount > 50)	{
		mes "你超过了最大限额!";
		mes "下次再试试?";
		close;
	}
	//Weight checking
	if(checkweight(getarg(1), .@amount) != 1)	{
		mes "你超重了.";
		mes "清理一下你的物品再来吧.";
		close;
	}
	
	//Materials checking
	if(countitem(getarg(0)) < .@amount * 500) {
		mes "呵......";
		mes "你带来的材料不够";
		mes "你想交易物品的数量哦";
		mes "请输入正确的交易数量";
		close;
	}
	
	//Zeny checking
	if(Zeny < .@amount * 500) {
		mes "饿哦,看来你带的钱不够.";
		mes "我们收取的费用为 500 zeny";
		mes "检查下你的钱够不够再来吧.";
		close;
	}

	mes "噢非常好!";
	mes "已经确定了数额!";
	if(getarg(1) < 12149)
		mes "我马上给你这些包裹.";
	else
		mes "我马上给你这些盒子.";
	set Zeny, Zeny - .@amount * 500;
	delitem getarg(0), .@amount * 500;
	getitem getarg(1), .@amount;
	close;
}
