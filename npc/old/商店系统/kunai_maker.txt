//===== rAthena Script =======================================
//= Kunai Merchant Kashin
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=     id of the 2 required items plus the amount. Can trade
//=     up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.3a Kagerou/Oboro support (BaseJob -> BaseClass) [Euphy]
//============================================================

que_ng,72,29,3	script	飞刀商人 卡新	83,{
	if(BaseClass == Job_Ninja) {
		mes "[卡新]";
		mes "我是忍者飞刀的兑换者卡新。";
		mes "随便看看吧，有兴趣没？";
		next;
		switch(select("我想兑换 剧毒飞刀 10把:我想兑换 垂冰飞刀 10把:我想兑换 强风飞刀 10把:我想兑换 黑土飞刀 10把:我想兑换 火焰飞刀 10把:结束对话")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[卡新]";
			mes "好勒，有需要再来找我吧。";
			mes "我 - 风之卡新会一直待在这里。";
			mes "萨哟哪啦！";
			close;
		}
	}
	mes "[卡新]";
	mes "我是忍者飞刀的兑换者卡新。";
	mes "如果你有忍者朋友的话，";
	mes "请帮忙转告一下他，";
	mes "在我这里能兑换到好用的飞刀。";
	close;
}

function	script	Kunai_Trade	{
	mes "[卡新]";
	mes "用"+getarg(1)+"个"+getitemname(getarg(0))+"和"+getarg(3)+"个"+getitemname(getarg(2))+"，就可以兑换10把"+getitemname(getarg(4))+"。";
	next;
	mes "[卡新]";
	mes "我一次最多只能给你500把飞刀。";
	mes "如果不想兑换的话，就在订单中输入0。";
	mes "你想要多少把？";
	next;
	input .@amount;
	if(.@amount == 0) {
		mes "[卡新]";
		mes "看来你不想订购飞刀呢！";
		mes "那就请下次再来吧。";
		close;
	}
	if(.@amount < 1 || .@amount > 500) {
		mes "[卡新]";
		mes "啊？对不起，";
		mes "我不能给你那么多，";
		mes "我一次最多只能给你500把。";
		close;
	}
	if(countitem(getarg(0)) < .@amount*getarg(1) || countitem(getarg(2)) < .@amount*getarg(3)) {
		mes "[卡新]";
		mes "哎呀哎呀……";
		mes "你好像没有足够的材料，";
		mes "兑换不到你想要的那么多东西。";
		mes "等准备好了之后再来找我吧。";
		close;
 	}
	if(checkweight(getarg(4), .@amount*10) == 0) {
		mes "[卡新]";
		mes "呃……";
		mes "你的背包好像满了。";
		mes "你最好先清理一下再来找我。";
		close;
	}
	mes "[卡新]";
	mes "噢~很好！";
	mes "物品数量确认完毕！";
	mes "我想你肯定会对这些飞刀很满意的。";
	delitem getarg(0),getarg(1)*.@amount;
	delitem getarg(2),getarg(3)*.@amount;
	getitem getarg(4),10*.@amount;
	close;
}
