//===== rAthena Script ======================================= 
//= Quest NPCs related to Izlude
//===== By: ================================================== 
//= Evera and The rAthena Dev Team
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Edgar's Offer:
//= - [Official Conversion]
//= - Variables in use: MISC_QUEST (Bit 16)
//===== Additional Comments: ================================= 
//= 1.0 Initial release [Evera]
//= 1.1 Updated to 10.3 standard. [L0ne_W0lf]
//============================================================

// Edgar's Offer :: iz_npc
//============================================================
-	script	::Edgar_izlude	-1,{
	if (MISC_QUEST & 16) {
		mes "[爱德嘉]";
		mes "这一次也是为了去艾尔贝塔又来找我的吗?";
		mes "和上一次一样只收250 金币，把你送到艾尔贝塔";
		next;
		mes "[爱德嘉]";
		mes "要搭船吗?";
		next;
		if (select("往艾尔贝塔移动!:结束") == 1) {
			if (Zeny < 250) {
				mes "[爱德嘉]";
				mes "钱不够 !再去多借点钱来";
				close ;
			}
			set zeny,zeny-250;
			warp "alberta",195,164;
			end;
		}
		mes "[爱德嘉]";
		mes "好吧，再见了";
		close;
	}
	mes "[爱德嘉]";
	mes "我们依斯鲁得岛和艾尔贝塔港口之间用港口来做连系，因为贸易交易频繁，我都快要变成那边的人了，";
	mes "特别是跟菲历斯这位朋友交情还不错呢，";
	mes "那家伙特别小气，想要凹他是不可能的";
	next;
	mes "[爱德嘉]";
	mes "可是他本性善良，喜欢帮助别人";
	mes "再加上他手气很海派，稍微在他旁边好好劝说的话，他可是会大力支援你的家伙呢";
	next;
	mes "[爱德嘉]";
	mes "最近他在艾尔贝塔悄悄的，用杰勒比结晶在帮助人们呢";
	mes "若你的经济情况不太好的话，可以试着去找他看看";
	next;
	if (select("请问怎么到艾尔贝塔?:结束对话") == 1) {
		mes "[爱德嘉]";
		mes "嗯?";
		mes "用双脚走也能努力的走到艾尔贝塔的，不过有钱的话，在那里搭船就马上到了";
		next;
		if (select("我知道了:我没钱又不想用走的") == 1) {
			mes "[爱德嘉]";
			mes "你这人真无聊啊..";
			close;
		}
		mes "[爱德嘉]";
		mes "什么?!";
		mes "没有那么多钱又不想用走的，又想要去艾尔贝塔? 真是的...";
		mes "好吧，我也是一位船长啊，多搭乘一位乘客又不是很难的事情，我特别只跟你收250 金币，把你带到艾尔贝塔";
		next;
		if(select("出发艾尔到贝塔!:结束")==1) {
			set MISC_QUEST,MISC_QUEST | 16;
			if (Zeny < 250) {
				mes "[爱德嘉]";
				mes "钱不够 !再去多借点钱来";
				close;
			}
			set zeny,zeny-250;
			warp "alberta",195,164;
			end;
		}
		mes "[爱德嘉]";
		mes "好吧，一路小心";
		close;
	}
	mes "[爱德嘉]";
	mes "好吧，一路小心";
	close;
}
