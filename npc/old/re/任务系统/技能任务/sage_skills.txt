//===== eAthena Script ======================================= 
//= Sage Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.3c
//===== Compatible With: ===================================== 
//= eAthena Revision 3800+
//===== Description: ========================================= 
//= Temp quests for new skills for 2nd classes
//===== Additional Comments: ================================= 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//= 1.3c Fixed some typos [IVBela]
//============================================================


//============================================================
// 贤者技能 - 元素肯贝特 制作 + 元素更换
//============================================================
yuno_in03.gat,176,24,3	script	米秀娜	755,{
	mes "[米秀娜]";
	if(BaseJob!=Job_Sage) goto L_sageno;
	if(getskilllv(1007)) goto L_elemental;

	mes "我能教会你新的技能";
	mes "但是你必须带给我点东西~";
	mes "学技能所要用到的~.";
	next;
	
	mes "[米秀娜]";
	mes "我们需要:";
	mes "10 蝎子尾巴";
	mes "7 硬角";
	mes "12 彩色昆虫外壳";
	mes "10 蜗牛壳";
	mes "4 空白卷轴";
	next;
	
	mes "[米秀娜]";
	mes "检查一下你的物品哦.";
	next;
	
	if(countitem(904)<10 || countitem(947)<7 || countitem(1013)<12 || countitem(946)<10 || countitem(7433)<4) goto L_noitems;//Items: Scorpion_Tail, Horn, Rainbow_Shell, Snail's_Shell, Blank_Scroll,
	delitem 904, 10;//Items: Scorpion_Tail,
	delitem 947, 7;//Items: Horn,
	delitem 1013, 12;//Items: Rainbow_Shell,
	delitem 946, 10;//Items: Snail's_Shell,
	delitem 7433, 4;//Items: Blank_Scroll,
	
	mes "[米秀娜]";
	mes "让我展示下我的才能吧~~";
	skill 1007,1,0;
	next;
	
	mes "[米秀娜]";
	mes "你能在自然中领悟吗？";
	close;

L_noitems:
	mes "[米秀娜]";
	mes "你没带足物品哦~~";
	mes "等你收集完了再来找我吧";
	close;
	
L_sageno:
	mes "祝贺你 你的名字将会";
	mes "留在贤者公会的墙上";
	close;

L_sagefail:
	mes "[米秀娜]";
	mes "你没有足够的物品.";
	mes "等你有了再回来找我";
	mes "那时我会教你使用元素的力量";
	close;
	
L_alreadyhave:
	mes "你是个不错的学生";
	mes "可惜..我没什么能再教你了";
	close;
			
L_elemental:
	if(getskilllv(1008) || getskilllv(1017) || getskilllv(1018) || getskilllv(1019)) goto L_alreadyhave;
	
	mes "我能教会你另一个技能~";
	mes "但是你得带些东西给我做为回报";
	next;
	
	mes "[米秀娜]";
	mes "20 火灵矿石";
	mes "20 水灵矿石";
	mes "20 风灵矿石";
	mes "20 地灵矿石";
	next;
	
	mes "[米秀娜]";
	mes "我只能教会你一个！！";
	mes "选的时候要仔细哦！！";
	next;
	
	menu "元素更换(水)",sage_1, "元素更换(地)",sage_2, "元素更换(火)",sage_3, "元素更换(风)",sage_4;

		sage_1:
		if(countitem(991)<20)goto L_sagefail;//Items: Crystal_Blue,
		delitem 991, 20;		//Items: Crystal_Blue,
		skill 1008,1,0;
		goto L_alreadyhave;

		sage_2:
		if(countitem(993)<20)goto L_sagefail;//Items: Green_Live,
		delitem 993, 20;//Items: Green_Live,
		skill 1017,1,0;
		goto L_alreadyhave;

		sage_3:
		if(countitem(990)<20)goto L_sagefail;//Items: Red_Blood,
		delitem 990, 20;//Items: Red_Blood,
		skill 1018,1,0;
		goto L_alreadyhave;

		sage_4:
		if(countitem(992)<20)goto L_sagefail;//Items: Wind_of_Verdure,
		delitem 992, 20;//Items: Wind_of_Verdure,
		skill 1019,1,0;
		goto L_alreadyhave;
}
