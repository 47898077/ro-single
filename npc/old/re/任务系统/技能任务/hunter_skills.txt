//===== eAthena Script ======================================= 
//= Hunter Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.3d
//===== Compatible With: ===================================== 
//= eAthena Revision 3800+
//===== Description: ========================================= 
//= Temp quests for new skills for 2nd classes
//===== Additional Comments: ================================= 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//= 1.3c Fixed some typos [IVBela]
//= 1.3d Changed NPC Name to the official [Lupus]
//============================================================


//============================================================
// 猎人技能 - 幻影箭
//============================================================
payon_in02.gat,54,13,7	script	亚勒贝斯妥#HS	55,{
	mes "[亚勒贝斯妥]";
	if(BaseJob!=Job_Hunter) goto L_hunterno;
	if(getskilllv(1009)) goto L_alreadyhave;
	if(JobLevel<40) goto L_nojob;

	mes "我告诉你个秘密哦~";
	mes "这个技能是我的家族一代代传下来的";
	mes "我们家族保守这个秘密很久了";
	mes "但是我能透露点给你";
	mes "作为参考~";
	next;
	mes "[亚勒贝斯妥]";
	mes "我需要点东西~";
	mes "5 诅咒红宝石";
	mes "5 哈比的羽毛";
	mes "30 宠物饲料";
	next;
	
	mes "[亚勒贝斯妥]";
	mes "我来检查下你带的东西.";
	next;
	
	if(countitem(724)<5 || countitem(7115)<5 || countitem(537)<30) goto L_noitems;//Items: Cursed_Ruby, Harpy_Feather, Pet_Food,
	delitem 724, 5;//Items: Cursed_Ruby,
	delitem 7115, 5;//Items: Harpy_Feather,
	delitem 537, 30;//Items: Pet_Food,
	
	mes "[亚勒贝斯妥]";
	mes "都带来了,";
	mes "我来教你我引以为傲的技能吧！";
	skill 1009,1,0;
	close;

L_noitems:
	mes "[亚勒贝斯妥]";
	mes "你没带足东西.";
	mes "等你有了再来找我吧";
	close;
		
L_nojob:
	mes "当你成为个真正的猎人的时候";
	mes "再来吧.";
	mes "最起码40级！";
	close;

L_alreadyhave:
	mes "哈~你回来我看我了？";
	close;
	
L_hunterno:
	mes "我能帮你什么忙吗？";
	mes "我想你大概找错人了 我只是个老头";
	close;
}
