
// ---------------------------------
// - 转生任务 脚本 - 
//===================================
// 版本：2.9c
//===================================

valkyrie,48,86,4	script	巴奇利#	811,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[巴奇利]";
		mes "欢迎来到荣誉的巴哈拉，";
		mes "好好的休息再回去吧，";
		mes "戏给战士们光荣!";
		close;
	}
	else if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[巴奇利]";
		mes "欢迎来到荣誉的巴哈拉，";
		mes "现在开始，把你目前的人生划下句点，";
		mes "你会重新获得一次新的生命，";
		mes "献给战士们的光荣!";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() != 0 || checkfalcon() != 0 || checkriding() != 0) {
			mes "[巴奇利]";
			mes "好..你必须要达成英雄的条件，";
			mes "首先，先将等级提升到最大，所谓的名誉就是";
			mes "由身心净空之后才会出来的";
			next;
			mes "[巴奇利]";
			mes "所携带的物品及金币，全部都要净空，";
			mes "之前召唤的动物，或是手推车等...";
			mes "任何东西都不能带走，";
			mes "那么...准备好之后再来吧";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[巴奇利]";
			mes "把身体和心全部净空了";
			mes "很伟大的决心啊，所谓的名誉就是由身心";
			mes "净空之后才会出来的";
			next;
			mes "[巴奇利]";
			mes "好...现在把过去的记忆从你身体里移除，";
			mes "在你的的精神里只帮你留下目前光荣的记忆";
			next;
			if (SkillPoint) {
				mes "[巴奇利]";
				mes "不能有剩余的技能点数";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[巴奇利]";
			mes "好...现在，";
			mes "一，在兀儿德会留下你过去的记忆，";
			mes "二，在蓓儿丹娣只记忆着目前光荣的刹那，";
			mes "三，在诗寇蒂会过着往未来的生活";
			next;
			callfunc "F_ClearJobVar";
			mes "[巴奇利]";
			mes "一";
			next;
			//callfunc("F_SaveQuestSkills"); //now not used
			mes "[巴奇利]";
			mes "二....";
			next;
			mes "[巴奇利]";
			mes "三....";
			set ADVJOB, Class+Job_Novice_High; //memo target 3rd Job ID
			if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			set MISC_QUEST,MISC_QUEST | 1024; //<-reset Skill Reset Event
			skill 142,1,0;
			skill 143,1,0;
			next;
			mes "[巴奇利]";
			mes "好，恭喜你，所有过程都已经结束了，";
			mes "这是，我送给即将走向新生活战士的小礼物";
			getitem 1202,1; //Knife_
			getitem 2302,1; //Cotton_Shirt_
			next;
			mes "[巴奇利]";
			mes "希望记录在过去兀儿德的生活不要白费了，";
			mes "希望记录在现在蓓儿丹娣的荣誉重新再现，";
			mes "希望记录在未来诗寇蒂的新生活里能展现荣誉，";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				warp "izlude",94,103; break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[巴奇利]";
		mes "欢迎来到荣誉的巴哈拉，";
		mes "现在开始，把你目前的人生划下句点，";
		mes "你会重新获得一次新的生命，";
		mes "献给战士们的光荣!";
		next;
		mes "[巴奇利]";
		mes "不过，你不应该出现在这里的";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	玫泰武司 西芙#Library	742,{
	if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 0) {
			mes "[玫泰武司 西芙]";
			mes "欢迎光临，这里是修巴宜且乐比";
			mes "魔法学校";
			mes "的图书馆，来到这里的目的是";
			mes "为了要看'依美乐之书'吧?";
			next;
			mes "[玫泰武司 西芙]";
			mes "依美乐之书的阅览虽然已";
			mes "开放给所有人，有时也会";
			mes "有想要亲自阅读的人，不过";
			mes "由于原本的状态并不太好，";
			mes "所以对想要阅览的人我们会特别的";
			mes "收下乐捐并给予许可";
			next;
			mes "[玫泰武司 西芙]";
			mes "乐捐是1,285,000 金币，";
			mes "是为了书籍的安全保管及";
			mes "古书籍的研究，如果愿意的话";
			mes "现在马上缴款并进场就可以了，";
			mes "请问要乐捐吗?";
			next;
			if (select("要:取消") == 1) {
				if (Zeny > 1284449) {
					set zeny,zeny-1285000;
					set valkyrie_Q,1;
					mes "[玫泰武司 西芙]";
					mes "谢谢你，这些乐捐我们会好好的";
					mes "使用在大学研究方面，";
					mes "你现在可以进去阅览了";
					close;
				}
				mes "[玫泰武司 西芙]";
				mes "你的金币有点不足啊，";
				mes "想要捐献的话，先确认一下";
				mes "口袋里的钱再过来吧";
				close;
			}
			mes "[玫泰武司 西芙]";
			mes "是吗?";
			mes "您不愿意啊?";
			mes "那真是太可惜了!";
			mes "既然如此我也就不勉强了";
			mes "再会!";
			close;
		}
		mes "[玫泰武司 西芙]";
		mes "谢谢你，这些乐捐我们会好好的";
		mes "使用在大学研究方面，";
		mes "你现在可以进去阅览了";
		close;
	}
	mes "[玫泰武司 西芙]";
	mes "欢迎光临，这里是修巴宜且乐比 魔法学校";
	mes "的图书馆，慢慢逛吧，";
	mes "这里有很多书籍";
	close;
}

yuno_in02,93,207,1	script	依美乐之书	111,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[依美乐之书]";
		mes "...进入荣誉神殿的入口";
		mes "已为重生的所有人们而打开，";
		mes "那将有助于迈向英雄之路";
		mes "往世上任何地方的路都会为你开着";
		next;
		mes "[依美乐之书]";
		mes "还有在那地方已为他们";
		mes "准备好了所有事情，听说";
		mes "在现实生活中无法实现的愿望，";
		mes "在那神殿是可以实现的";
		next;
		if (select("停止阅读:继续阅读") == 1) {
			mes "[依美乐之书]";
			mes ".....";
			close;
		}
		mes "[依美乐之书]";
		mes "离天空最近的地方，应该会有";
		mes "通往巴奇利的神殿之路....";
		mes "平凡的他们永远不会知道的";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[依美乐之书]";
			mes "...然而伴随前辈的无数功绩";
			mes "同时可疑的地方是，面对英雄们";
			mes "必然会遇到有关'死亡' ";
			mes "的问题，所有被封上英雄称号的人";
			mes "也都会有最后一刻的时候";
			next;
			mes "[依美乐之书]";
			mes "可是有关任何英雄的歌或一些记录，";
			mes "就算是以口头流传下来的故事，";
			mes "也没有留下那最后的纪录，";
			mes "关于这必须要注意一点的是，";
			mes "那就是记述在 '仙境传说' 里";
			mes "有关巴奇利的内容";
			next;
			mes "[依美乐之书]";
			mes "对于人在巅峰时最棒的那一刻拥有";
			mes "伟大的素质者的人，在最后的圣战上";
			mes "以战士打仗，他的魂会随着";
			mes "巴奇利的领导而走向巴哈拉，";
			mes "他跟一般所有人的最后不同，";
			mes "而是有关英雄的轮回，";
			next;
			mes "[依美乐之书]";
			mes "以真正的英雄重新复活";
			mes "成为照亮新时代的人，";
			mes "跌倒的身体会再爬起来，";
			mes "灵魂唤起新的能力，";
			mes "但是很不幸的依美乐的心脏，";
			mes "在战乱之后从大陆上消失了";
			next;
			mes "[依美乐之书]";
			mes "一点点的发现并不能支持我们";
			mes "做研究，没有验证过的";
			mes "就无法尝试任何事，";
			mes "所以我就留下这纪录，";
			mes "等后人发现这纪录";
			mes "和他的意志及希望，希望能够";
			next;
			mes "[依美乐之书]";
			mes "重生为点亮时代最后一道光";
			mes "的英雄，再次苦苦的等待";
			mes "希望领导巴奇利神殿之";
			mes "英雄们的人出现，还有那以后...";
			mes "........";
			set valkyrie_Q,2;
			close;
		}
		mes "[依美乐之书]";
		mes ".....";
		close;
	}
	mes "[依美乐之书]";
	mes ".....";
	close;
}

yuno_in05,49,43,1	script	依美乐的心脏	111,{
	if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	泰乐柏特#val	124,{
	if (Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[泰乐柏特]";
			mes "恭喜你转生成功，";
			mes "献给战士们光荣!";
			close;
		}
		mes "[泰乐柏特]";
		mes "请不要触摸展示品";
		mes "........";
		close;
	}
	else {
		mes "[泰乐柏特]";
		mes "记住光荣时刻的你";
		mes "请问要去哪里呢?";
		next;
		switch (select("普隆德拉:梦罗克:斐扬:吉芬:艾尔贝塔:伊斯鲁德:艾尔帕兰:克魔岛:朱诺")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6: callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
S_WarpChar:
	mes "[泰乐柏特]";
	mes "祝您平安";
	close2;
	savepoint getarg(0),getarg(1),getarg(2);
	warp getarg(0),getarg(1),getarg(2);
	end;
}

// Warps
//============================================================ 
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75

