//===== eAthena Script =======================================
//= Auction system
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Auction house releated NPCs, and warps
//===== Additional Comments: =================================
//= 1.0 First version, place holders only. [L0ne_W0lf]
//= 1.1 Moved Morroc Auction NPCs to Morroc Ruins. [L0ne_W0lf]
//= 1.2 Warp fixed. Dialog in Ruins/Lighthalzen changed. [Kisuka]
//= 1.3 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//============================================================

// 拍卖场 传送
//============================================================
moc_ruins,78,173,6	script	拍卖场 服务人员#moc	98,{
	callfunc "F_AuctionWarper",1;
}

prontera,218,120,4	script	拍卖场 服务人员#prt	117,{
	callfunc "F_AuctionWarper",2;
}

yuno,129,116,0	script	拍卖场 服务人员#yuno	90,{
	callfunc "F_AuctionWarper",3;
}

lighthalzen,205,169,6	script	拍卖场 服务人员#lhz	833,{
	callfunc "F_AuctionWarper",4;
}

function	script	F_AuctionWarper	{
	mes "[拍卖场 服务人员]";
	mes "请问你要去拍卖场吗?";
	next;
	if (select("是的:不是") == 1) {
		if (getarg(0) == 1 || getarg(0) == 4) {
			mes "[拍卖场 服务人员]"; 
			mes "太好了！那么，"; 
			mes "祝你拍卖愉快~"; 
		}else{
			mes "[拍卖场 服务人员]";
			mes "那么祝你一路顺风~";
		}
		close2;
		switch(getarg(0)) {
		case 1:
			warp "auction_01",179,53; end;
		case 2:
			warp "auction_01",21,43; end;
		case 3:
			warp "auction_02",151,23; end;
		case 4:
			warp "auction_02",43,24; end;
		}
	}
	mes "[拍卖场 服务人员]";
	mes "那么下一次请你再来呦";
	close;
}

// 标示牌
//============================================================
-	script	服务 标示牌#dum::AuctionSign	-1,{
	mes "[服务文章]";
	mes "拍卖移动 服务人员";
	close;
}

moc_ruins,76,176,6	duplicate(AuctionSign)	服务 标示牌#moc	837
prontera,216,120,4	duplicate(AuctionSign)	服务 标示牌#prt	837
yuno,131,116,0	duplicate(AuctionSign)	服务 标示牌#yuno	837
lighthalzen,207,169,6	duplicate(AuctionSign)	服务 标示牌#lhz	837

// 传送点
//============================================================
auction_01,180,49,0	warp	auction_entrance_moc	1,1,moc_ruins,78,171
auction_01,22,37,0	warp	auction_entrance_prt	1,1,prontera,214,120
auction_02,151,17,0	warp	auction_enterance_juno	1,1,yuno,132,119
auction_02,43,17,0	warp	auction_enterance_lhz	1,1,lighthalzen,209,169

// 拍卖场里的NPC
//============================================================
-	script	拍卖管理人#dum::AuctionBroker	-1,{
	mes "[拍卖仲介人]";
	mes "这一次进了许多新货";
	mes "你要看吗?";
	next;
	if (select("是的:不是") == 1) {
		mes "[拍卖仲介人]";
		mes "来来赶快来看..";
		OpenAuction;
		close;
	}
	mes "[拍卖仲介人]";
	mes "那么请你下一次在看吧";
	close;
}

auction_01,182,68,6	duplicate(AuctionBroker)	拍卖管理人#moc1	98
auction_01,182,75,0	duplicate(AuctionBroker)	拍卖管理人#moc2	99
auction_01,177,75,2	duplicate(AuctionBroker)	拍卖管理人#moc3	98
auction_01,177,68,4	duplicate(AuctionBroker)	拍卖管理人#moc4	99
auction_01,21,74,4	duplicate(AuctionBroker)	拍卖管理人#prt1	117
auction_01,27,78,4	duplicate(AuctionBroker)	拍卖管理人#prt2	116
auction_01,16,78,4	duplicate(AuctionBroker)	拍卖管理人#prt3	115
auction_02,158,47,6	duplicate(AuctionBroker)	拍卖管理人#yuno1	90
auction_02,145,47,2	duplicate(AuctionBroker)	拍卖管理人#yuno2	90
auction_02,151,54,0	duplicate(AuctionBroker)	拍卖管理人#yuno3	90
auction_02,152,41,4	duplicate(AuctionBroker)	拍卖管理人#yuno4	90
auction_02,57,46,2	duplicate(AuctionBroker)	拍卖管理人#lhz1	874
auction_02,31,46,6	duplicate(AuctionBroker)	拍卖管理人#lhz2	874
auction_02,43,65,4	duplicate(AuctionBroker)	拍卖管理人#lhz3	833
