//===== rAthena Script ======================================= 
//= Brasilis Town
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Brasilis Town Script
//===== Additional Comments: ================================= 
//= 1.0 First version. Transportation and Basic NPCs.
//= 1.1 Fixed Zeny not being removed for payment. [Kisuka]
//= 1.2 Updated dialog, added Ice Cream Maker. [L0ne_W0lf]
//= 1.3 Moved Alberta NPC to pre-re/re paths. [Euphy]
//============================================================ 

// Brasilis Transportation
//============================================================
/* Pre-Renewal coordinates: alberta,247,115,3 */
alberta,246,82,3	script	布兰妮号 船员#bra2	100,{
	mes "[布兰妮号 船员]";
	mes "嘿, 你有听过一个叫巴西的地方吗?";
	mes "那理有点像沙漠都市梦罗克般炎热、多雨. 是个非常神秘的地方.";
	next;
	mes "[布兰妮号 船员]";
	mes "我们最近发现了一条新的远洋航线，不容易去到那边.";
	mes "往返需 10,000 ! 如何?";
	next;
	switch(select("去巴西!:我要留在这.")) {
	case 1:
		if (Zeny > 9999) {
			mes "[布兰妮号 船员]";
			mes "酷喔~!! 出发~!";
			set zeny,zeny-10000;
			close2;
			warp "brasilis",314,60;
			end;
		}
		else {
			mes "[布兰妮号 船员]";
			mes "我说 10,000 zeny.";
			close;
		}
	case 2:
		mes "[布兰妮号 船员]";
		mes "如果你感到兴趣, 告诉我带你过去.";
		close;
	}
}

brasilis,316,57,3	script	布兰妮号 船员#bra1	100,{
	mes "[布兰妮号 船员]";
	mes "我的船要驶回艾尔贝塔, 你想搭船吗?";
	next;
	switch(select("搭船.:不搭船~.")) {
	case 1:
		mes "[布兰妮号 船员]";
		mes "当然还是自己的家最好.";
		close2;
		if (checkre(0))
			warp "alberta",243,82;
		else
			warp "alberta",244,115;
		end;
	case 2:
		mes "[布兰妮号 船员]";
		mes "好的, 随便你. 当我们回来会在见到你.";
		close;
	}
}

brasilis,155,165,3	script	告示牌#bra1	858,{
	mes ":: 美术博物馆 ::";
	close;
}

brasilis,195,231,3	script	告示牌#bra2	858,{
	mes ":: 韦拉斯纪念碑 ::";
	close;
}

brasilis,240,247,3	script	告示牌#bra3	858,{
	mes ":: 市集 ::";
	mes " ";
	mes "- 药水与武器 -";
	close;
}

brasilis,303,309,3	script	告示牌#bra4	858,{
	mes ":: 丛林缆车 ::";
	mes "";
	mes "- 心脏弱的玩家禁入 -";
	close;
}

brasilis,278,137,3	script	告示牌#bra5	858,{
	mes ":: 巴西　大饭店 ::";
	close;
}

brasilis,137,77,5	script	霜淇淋宝贝	85,{
	mes "[霜淇淋宝贝]";
	mes "来哟~来哟~";
	mes "冰淇淋是炎热夏天最佳消暑良伴~";
	mes "只要 ^3355FF100 Zeny^000000~";
	mes "霜淇淋~";
	mes "霜淇淋来了!";
	next;
	switch(select("给我一个!:关于霜淇淋？:取消交易.")) {
	case 1:
		mes "[霜淇淋宝贝]";
		mes "清凉美味的霜淇淋由于太受欢迎，.";
		mes "所以每人只能限购 5个-";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[霜淇淋宝贝]";
				mes "不买?";
				mes "为了下一位排队购买霜淇淋的人, 请让位好吗.";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[霜淇淋宝贝]";
				mes "喔喔.";
				mes "你有点贪心喔.";
				mes "吃超过5个的话，到了晚上你的肠胃就会跟你作对吧?.";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[霜淇淋宝贝]";
			mes "！！ 您的金币似乎不足?.";
			mes "每个是 ^3355FF100 Zeny^000000~ !";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[霜淇淋宝贝]";
			mes "你携带太多物品.";
			mes "请减少一些携带品在购买.";
			close;
		}
		set zeny,zeny-.@icecream_hap;
		getitem 536,.@input; //Ice_Cream
		close;
	case 2:
		mes "[霜淇淋宝贝]";
		mes "'霜淇淋...";
		mes "等等,";
		mes "你不知道什么是霜淇淋?";
		mes "你该不会是...";
		mes "生活在天龙国吧?";
		next;
		mes "[霜淇淋宝贝]";
		mes "所谓'霜淇淋'就是...冷冻的冰品.";
		mes "因热量高营养丰富，适当的吃才有益健康.";
		close;
	case 3:
		mes "[霜淇淋宝贝]";
		mes "不要错过品尝美味的冰淇淋喔~!";
		close;
	}
}
