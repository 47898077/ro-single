//===== rAthena Script ======================================= 
//= Gym Pass Cash Item NPC
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: ================================= 
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================ 

payon,173,141,4	script	重力圣人#GymPass::GymPassMan	899,{
	mes "[重力圣人]";
	mes "你好，我是重力圣人。";
	mes "现在人们的体力已经大不如前了啊，即使你是靠体力劳动维持生计的人。";
	mes "你的身体也不像你想的那么强壮哦!";
	next;
	mes "[重力圣人]";
	mes "跟我一起锻炼吧！";
	mes "我保证你可以拿更多的东西！";
	mes "每次交给我^FF0000一张健身房会员券^000000";
	mes "我们就可以开始锻炼啦！";
	next;
	mes "[重力圣人]";
	mes "但是别太激动哦！";
	mes "虽然你可以多次的来锻炼，";
	mes "但是如果锻炼强度太大，";
	mes "你的身体可是会吃不消的哟";
	mes "明白啦?";
	next;
	mes "[重力圣人]";
	mes "在我这里，";
	mes "你可以接受一系列严格的训练，";
	mes "这些锻炼可以帮你增加负重，";
	mes "一共可以增加^FF000010次哦^000000.";
	mes "准备好了吗?";
	next;
	switch(select("我准备好啦:我还没有准备好:呃，我的负重能力出问题了.")) {
	case 1:
		if (gympassmemory < 10) {
			set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - .@add_carry;
			if (countitem(7776) > 0) {
				mes "[重力圣人]";
				mes "哦，我看到你带健身券来了";
				mes "好的，跟着我做！";
				mes "你会发现身体充满能量,";
				mes "试着去体会能量燃烧的";
				mes "感觉吧！预备？开始！";
				next;
				specialeffect2 EF_EARTHSPIKE;
				next;
				specialeffect2 EF_DEVIL;
				next;
				specialeffect2 EF_COIN;
				next;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[重力圣人]";
				mes "好啦，现在你有力气去扛更多的东西啦！";
				mes "让我看看，如果我们继续做下去，";
				mes "你的负重能力还能增长^FF0000 " + .@remain_carry + "^000000 次！";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,.@add_carry;
				skill "ALL_INCCARRY",.@add_carry,4;
				close;
			}
			else {
				mes "[重力圣人]";
				mes "不记得我告诉过你的?";
				mes "如果你想增强体质，";
				mes "记得带^FF0000健身房会员券^000000过来！";
				mes "跟着我锻炼，可以让你更加健美哦~";
				close;
			}
		}
		else {
			mes "[重力圣人]";
			mes "我想你的身体已经";
			mes "得到了充足的锻炼！";
			mes "如果再继续增加强度，";
			mes "对你的身体相当危险！";
			mes "好吧，别这样啦";
			mes "我告诉过你最大锻炼次数是10次的！";
			close;
		}
	case 2:
		mes "[重力圣人]";
		mes "哦~那太糟糕了！";
		mes "如果你改主意了，可以回来找我！";
		mes "如果你的朋友需要锻炼身体增加负重，";
		mes "你可以推荐他们来找我哦！";
		close;
	case 3:
		if (gympassmemory > 0) {
			mes "[重力圣人]";
			mes "发生什么事情了?你一定是锻炼强度太大导致肌肉拉伤了吧！";
			mes "你很幸运哦，肌肉是有记忆功能的，而且不会花太久时间就能恢复正常.";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			mes "[重力圣人]";
			mes "现在感觉怎么样?";
			mes "你的肌肉机能应该恢复正常啦！";
			mes "不要再超负荷锻炼啦！";
			mes "千万要记住哦！";
			skill "ALL_INCCARRY",gympassmemory,4;
			close;
		}
		else {
			mes "[重力圣人]";
			mes "呃...";
			mes "放心，我们之前并没有";
			mes "进行剧烈运动影响到";
			mes "你的负重能力~";
			mes "我保证你一切正常！";
			close;
		}
	}
}

prontera,160,136,3	duplicate(GymPassMan)	重力圣人#GymPass_prt	899
