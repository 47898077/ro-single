//===== rAthena Script =======================================
//= Rare Diamond Merchant
//===== By: ==================================================
//= Z3R0
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= [Official Conversion]
//= Exchanges 17 Carat Diamond
//===== Additional Comments: =================================
//= 1.0 First / Optimized Version
//= 1.1 Adjusted for Missing ; on Line #271 (#15425)
//= 1.2 Removed Comments per Request (#15426)
//= 1.3 Changed Emotion # to Const Definition (#15427)
//= 1.4 Removed Double Space on Line #148 (#15428)
//= 1.5 Added This Version History (#15429)
//============================================================

-	script	RareDiamondMerchant	58,{

	set .@npc$, "[稀有钻石商人]";

	mes .@npc$;
	mes "先生和女士们! ! !";
	mes "你听说过传闻 , 但你没有一次真正看到";
	mes "世界上最稀有的钻石!";
	mes "^FF82FF'世界之星钻石'^000000 目前正以便宜的价格出售!!";
	mes "如果你对钻石感兴趣!";
	next;
	
	mes .@npc$;
	mes "你可以将钻石做为礼物送给你所爱的人.";
	mes "对方收下 ^FF82FF'世界之星钻石'^000000";
	mes "你在对方心中将更添珍贵.";
	emotion e_loud;
	next;
	
	mes .@npc$;
	mes "什么？你没有情人?";
	mes "你仍然可以拥有它.";
	mes "迷人光彩的 ^FF82FF'世界之星钻石'^000000 ";
	mes "会在你的生活中带来爱.";
	next;
	
	mes .@npc$;
	mes "^FF82FF'世界之星钻石'^000000!";
	mes "被称为迷人的钻石...";
	next;

	set .@menu$, "^FF82FF'世界之星钻石'^000000?";
	if (countitem(6024))
		set .@menu$, .@menu$ + ":世界之星钻石换取金币...";

	switch(select(.@menu$)) {
	case 1:
		mes .@npc$;
		mes "是的!";
		mes "呵呵, 你现在看到的是,";
		mes "最高质量的^FF82FF'世界之星钻石'^000000.";
		mes "无论是它的大小、质量、色泽与光透度";
		mes "从梦罗克所挖掘出来的钻石简直无以言喻!";
		emotion e_omg;
		next;
		
		mes .@npc$;
		mes "有位　Kachua　声称在克魔岛，";
		mes "也有见到非常相似的钻石!";
		next;
		
		if (select("结束对话:多少钱?") == 1) {
			mes .@npc$;
			mes "这是相当便宜的!";
			mes "如果你有兴趣，随时都能告诉我!";
			close;
		}
		
		mes .@npc$;
		mes "当然了! 最重要的是价格!";
		mes "诚如我刚才所说, 没有任何其他的钻石像他一样,";
		mes "连续多年被评鉴为.";
		mes "^FF82FF'世界之星钻石'^000000 .";
		next;
		
		mes .@npc$;
		mes "一个需 5亿 zeny!";
		mes "还有 1,000,000z 的服务费.";
		mes "与钻石的质量相比";
		mes "这是很小的代价.";
		next;
		
		if (select("^828282太贵了.^000000:我买它.") == 1) {
			mes .@npc$;
			mes "竟然说";
			mes "^FF82FF'世界之星钻石'^000000 太昂贵, ";
			mes "你让我无言.";
			close;
		}
		
		mes .@npc$;
		mes "!!从看见你的那刻起";
		mes "我就知道，你是这个珍贵钻石的拥有者.";
		mes "很高兴我没有错";
		mes "我相信这钻石非常适合您";
		mes "我需要你完成合约.";
		next;
		
		while(1) {
			if (select("^B9062F我会在合约上签字.^000000:^828282没关系, 我改变主意了.^000000") == 2) {
				mes .@npc$;
				mes "我能理解, 这太糟糕了..";
				close;
			}
			
			mes .@npc$;
			mes "你的大名是?";
			next;
			
			mes "[" + strcharinfo(0) + "]";
			mes "我的名子是 " + strcharinfo(0) + ".";
			next;

			mes .@npc$;
			mes "那么好吧, 请在这里签名.";
			next;
			
			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "Okay, good.";
				mes "只剩下交易付款.";
				next;
				
				if (Zeny >= 501000000) {
					mes .@npc$;
					mes ". . . . . .";
					next;
					
					mes .@npc$;
					mes "计算总价格..";
					mes "501,000,000 zeny.";
					mes "确认金额.";
					emotion e_loud;
					set Zeny, Zeny - 501000000;
					getitem 6024, 1;
					next;

					mes .@npc$;
					mes "无论何时你能用钻石换取金币.";
					mes "然而,将会有 1,000,000z 服务费.";
					mes "感谢您的买卖.";
					emotion e_thx;
					close;
				}
				else {
					mes .@npc$;
					mes "Hm?";
					mes "很抱歉, 你没有足够的资金.";
					mes "包含服务费，共计 501,000,000z 是必须的.";
					mes "请确认你的资金并再试一次.";
					emotion e_hmm;
					close;
				}
			}
			else {
				mes .@npc$;
				mes "这真的是你的签名吗?";
				mes "将签署再次确认?";
				next;
			}
		}
	case 2:
		mes .@npc$;
		mes "AAH. 你想用 ^FF82FF'世界之星钻石'^000000换取金币?";
		emotion e_gasp;
		next;
		if (select("是的:不要") == 2) {
			mes .@npc$;
			mes "那么，什么是你的愿望呢?..";
			mes ". . . . . ....";
			emotion e_dots;
			close;
		}

		mes .@npc$;
		mes "明白. 收到你的签名后, ";
		mes "您必须提供您的 ^FF82FF'世界之星钻石'^000000.";
		mes "也别忘记有 1,000,000z 服务费!";
		next;
		
		mes .@npc$;
		mes "你的名子是?";
		next;
		
		mes "[" + strcharinfo(0) + "]";
		mes "我的名子是 " + strcharinfo(0) + ".";
		next;
		
		mes .@npc$;
		mes "那么好吧，请在这里签字.";
		next;
		
		while (1) {
			if (select("签署:不签") == 2) {
				mes .@npc$;
				mes "交易不能没有你的签名.";
				close;
			}
			
			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "已收到您的签名.";
				mes "我将收取 ^FF82FF'世界之星钻石'^000000..";
				mes "您将收到 ^0000FF499,000,000z^000000 .";
				next;
				
				mes .@npc$;
				mes "感谢您的买卖.";
				mes "每当你需要 ^FF82FF'世界之星钻石'^000000,";
				mes "请随时回来.";
				emotion e_thx;
				delitem 6024, 1;
				set Zeny, Zeny + 499000000;
				close;
			}
			else {
				mes .@npc$;
				mes "这真的是你的签名吗?";
				mes "将在次确认?";
				next;
			}
		}
	}
}

prontera,165,89,6	duplicate(RareDiamondMerchant)	稀有钻石商人#1	58
geffen,106,63,6	duplicate(RareDiamondMerchant)	稀有钻石商人#2	58
morocc,146,100,6	duplicate(RareDiamondMerchant)	稀有钻石商人#3	58
alberta,129,60,6	duplicate(RareDiamondMerchant)	稀有钻石商人#4	58
lighthalzen,163,65,6	duplicate(RareDiamondMerchant)	稀有钻石商人#5	58
rachel,106,142,6	duplicate(RareDiamondMerchant)	稀有钻石商人#6	58
payon,180,130,6	duplicate(RareDiamondMerchant)	稀有钻石商人#7	58
