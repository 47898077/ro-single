// ---------------------------------
// - 弓箭手 转职脚本 - 
//===================================
// 版本：1.7
//===================================

payon_in02,64,71,4	script	工会服务人员#arch	85,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy) {
			mes "[弓箭手 工会员]";
			mes "您已经转生了，不需要什么转职考试了哦";
			next;
			if (getskilllv(1) < 9) {
				mes "[弓箭手 工会员]";
				mes "您的'基本技能'等级还不够喔!";
				mes "再确认一次转职条件，请努力争取到基本技能等级９吧";
				close;
			}
			mes "[弓箭手 工会员]";
			mes "请好好的把弓箭手的精神发扬光大吧";
			next;
			skill 143,0,0;
			jobchange Job_Archer_high;
			skill 147,1,0;
			skill 148,1,0;
			mes "[弓箭手 工会员]";
			mes "恭喜您转职成功!";
			mes "从今往后也请为了弓箭手工会的复兴努力奋斗吧!";
			close;
		}
		else {
			mes "[弓箭手 工会员]";
			mes "嗯... 第一次看到弓箭手吗?";
			mes "继续走你的路吧!";
			mes "我是有水准的...";
			mes "啊啊，不是这个...?";
			next;
			mes "[弓箭手 工会员]";
			mes "嘿嘿，刚刚讲的算没有听到好吗?";
			mes "是你听错了，是你心情在做怪";
			close;
		}
	}
	mes "[弓箭手 工会员]";
	mes "你好";
	mes "这里是弓箭手工会，有什么可以帮您的吗??";
	next;
	switch(select("转职成为弓箭手:转职条件是什么?:没什么事情")) {
	case 1:
		if (BaseJob == Job_Archer) {
			mes "[弓箭手 工会员]";
			mes "阁下已经是弓箭手了";
			close;
		}
		else if (BaseJob != Job_Archer && BaseJob != Job_Novice) {
			mes "[弓箭手 工会员]";
			mes "看样子你已经不是初学者了...";
			mes "你已经选择了其他职业，无法转职为弓箭手";
			close;
		}
		if (job_archer_q == 0) {
			mes "[弓箭手 工会员]";
			mes "你是为转职弓箭手而来吗?";
			mes "这样的话，在这里填一下转职申请书吧";
			next;
			if (select("填写弓箭手转职申请书:放弃") == 1) {
				set job_archer_q,1;
				mes "[弓箭手 工会员]";
				mes "在申请书上签名...";
				mes "嗯，这样一来就OK了";
				next;
				mes "[弓箭手 工会员]";
				mes "接下来只要满足了弓箭手转职的条件的话就可以顺利转职了";
				mes "是不是已经满足了条件了呢，现在可以让我确认一下吗";
				next;
				if (select("好的:不好") == 1) {
					mes "[弓箭手 工会员]";
					mes "好的，马上为您开始确认";
					next;
				}
				else {
					mes "[弓箭手 工会员]";
					mes "明白了";
					mes "如果对弓箭手转职条件还有什么疑问的话，请随时来问我";
					close;
				}
			}
			mes "[弓箭手 工会员]";
			mes "啊...再见了";
			close;
		}
		mes "[弓箭手 工会员]";
		mes "姓名是... " +strcharinfo(0)+ "吧?";
		next;
		if (getskilllv(1) < 9) {
			mes "[弓箭手 工会员]";
			mes "您的'基本技能'等级还不够喔!";
			mes "再确认一次转职条件，请努力争取到基本技能等级９吧";
			close;
		}
		if (job_archer_q == 1) {
			set .@archer_item1,countitem(1066) * 5;
			set .@archer_item2,countitem(1067) * 3;
			set .@archer_item3,countitem(1068) * 2;
			set .@archer_item4,countitem(1019);
			set .@total_archer,.@archer_item1 + .@archer_item2 + .@archer_item3 + .@archer_item4;
			set .@total_archer2,(((.@archer_item2 + .@archer_item3) * 2) + .@archer_item4);
			mes "[弓箭手 工会员]";
			mes "初学者技能等级已经足够了...";
			mes "那么开始检验制作弓所必须的树枝数量吧";
			next;
			if (countitem(1066) == 0 && countitem(1067) == 0 && countitem(1068) == 0 && countitem(1019) == 0) {
				mes "[弓箭手 工会员]";
				mes "....";
				mes "并没有要检验的树枝啊!!";
				close;
			}
			mes "[弓箭手 工会员]";
			if (countitem(1066) != 0) mes " 天地树枝 : " + countitem(1066) + "个，" + .@archer_item1 + "分";
			if (countitem(1067) != 0) mes " 智慧树枝 : " + countitem(1067) + "个，" + .@archer_item2 + "分";
			if (countitem(1068) != 0) mes " 世界树枝 : " + countitem(1068) + "个，" + .@archer_item3 + "分";
			if (countitem(1019) != 0) mes " 玛哪树枝 : " + countitem(1019) + "个，" + .@archer_item4 + "分";
			if (.@total_archer < 25) {
				mes "总分: ^FF0000" + .@total_archer + "^000000 / 40";
				next;
				mes "[弓箭手 工会员]";
				mes "25分以下?";
				mes "如果不到25分以上的话不能让你合格哦!";
				mes "请再努力一些吧!";
				close;
			}
			else {
				mes "总分: ^0000FF" + .@total_archer + "^000000 / 40";
				next;
				mes "[弓箭手 工会员]";
				if (.@total_archer > 40) {
					mes "哇!40分以上!";
					mes "好厉害啊!合格了!";
				}
				else if (.@total_archer > 30) {
					mes "30分以上!合格了!";
					mes "真是辛苦您了!";
				}
				else if (.@total_archer > 25) {
					mes "25分以上!嗯~";
					mes "刚好合格了";
				}
			}
			next;
			mes "[弓箭手 工会员]";
			mes "现在就把树枝转交给'弓'的制作负责人";
			if (countitem(1066) != 0) delitem 1066,countitem(1066); //Tree_Of_Archer_1
			if (countitem(1067) != 0) delitem 1067,countitem(1067); //Tree_Of_Archer_2
			if (countitem(1068) != 0) delitem 1068,countitem(1068); //Tree_Of_Archer_3
			if (countitem(1019) != 0) delitem 1019,countitem(1019); //Wooden_Block
		}
		next;
		callfunc "Job_Change",Job_Archer;
		callfunc "F_ClearJobVar";
		mes "[弓箭手 工会员]";
		mes "这么一来弓箭手转职条件就都齐了";
		mes "马上就为您转职";
		next;
		mes "[弓箭手 工会员]";
		mes "恭喜您弓箭手转职成功!";
		mes "那么，从今往后也请为了弓箭手工会的复兴努力奋斗吧!";
		mes "啊，制作负责人的礼物送到了请收下吧";
		getitem 1702,1; //Bow_
		getitem 1750,.@total_archer2; //Arrow
		next;
		mes "[弓箭手 工会员]";
		mes "弓箭手是离不开弓与箭的!";
		mes "那么，请您好好努力吧";
		mes "我将拭目以待";
		close;
	case 2:
		mes "[弓箭手 工会员]";
		mes "那么现在就来说明一下弓箭手的转职条件吧";
		if (BaseJob != Job_Novice) {
			if (BaseJob == Job_Archer) {
				next;
				mes "[弓箭手 工会员]";
				mes "但是，你已经是弓箭手了呢...";
			}
			else {
				next;
				mes "[弓箭手 工会员]";
				mes "但是，你已经是其他职业了呢...";
			}
			mes "...那么，好吧";
		}
		next;
		mes "[弓箭手 工会员]";
		mes "首先，请将初学者职业等级提升到９以上";
		mes "弓箭手是需要高度集中力的职业，做事糊涂的家伙是绝对不能胜任的";
		next;
		mes "[弓箭手 工会员]";
		mes "要完成弓箭手转职考试，需要收集制作弓箭所必须的^ff0000树枝^000000";
		mes "可以收集到的树枝共分为４个等级，将收集到的树枝带回来后把数量合计后打分";
		next;
		mes "[弓箭手 工会员]";
		mes "达到40分满分中的^0000ff25分以上^000000的话，你就可以顺利转职成弓箭手了";
		mes "树枝这种东西，可以从魔物'树精'身上取得";
		mes "树精虽然不是很厉害的魔物，但是轻敌可是大忌哦";
		close;
	case 3:
		close;
	}
}

