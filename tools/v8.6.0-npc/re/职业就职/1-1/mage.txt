// ---------------------------------
// - 魔法师 转职脚本 - 
//===================================
// 版本：1.7a
//===================================

geffen_in,164,124,4	script	魔法工会服务人员#gef1	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[魔法工会服务人员]";
				mes "哇，好久没看到你了";
				mes "我还以为你已经挂了呢";
				next;
				mes "[魔法工会服务人员]";
				mes "你应该是获得重生了吧，";
				mes "不管怎样，祝你一切平安";
				next;
				if (getskilllv(1) < 9) {
					mes "[魔法工会服务人员]";
					mes "....都准备好了吗?";
					mes "....什么还没准备好啊，'基本技能'等级还不够";
					mes "等提高了'基本技能'等级后再来吧";
					close;
				}
				skill 143,0,0;
				jobchange Job_Mage_High;
				skill 157,1,0;
				mes "[魔法工会服务人员]";
				mes "恭喜您转职成功!";
				mes "这辈子也请为了魔法工会的复兴努力奋斗吧!";
				close;
			}
			else {
				mes "[魔法工会服务人员]";
				mes ".....";
				close;
			}
		}
		else {
			mes "[魔法工会服务人员]";
			mes "你对魔法有兴趣吗? 就算是也办法";
			mes "毕竟你不属于这里，";
			mes "你应该到你该去的地方";
			close;
		}
	}
	mes "[魔法工会服务人员]";
	mes "你好，有什么事情吗?";
	next;
	switch(select("转职成为魔法师:询问有关转职成为魔法师所需要的条件:取消")) {
	case 1:
		mes "[魔法工会服务人员]";
		if (BaseJob == Job_Mage) {
			mes "啊...你不已经是魔法师了吗?";
			mes "难道你不知道吗?";
			mes "难不成要能空手变出火焰时，你才知道自己是魔法师吗?";
			mes "多实践才是王道啊，你说是不是?";
			close;
		}
		if (BaseJob != Job_Novice) {
			mes "哟...你已经不能转职为魔法师了";
			mes "你不是已经有其他职业了吗?";
			close;
		}
		if (job_magician_q == 0) {
			mes "你想成为魔法师?";
			if (sex)
				mes "像你这么可爱的小帅哥";
			else {
				mes "像你这么可爱的正妹";
			}
			mes "如果能成为魔法工会的一员，一定会很受欢迎的~";
			next;
			mes "[魔法工会服务人员]";
			mes "说归说，你想转职成为魔法师吗?";
			mes "那么，麻烦你在魔法师转职申请书上签个名";
			next;
			if (select("申请转职成为魔法师:取消") == 1) {
				mes "[魔法工会服务人员]";
				mes "对，在那里签名....";
				mes "噢，写得真好看―";
				mes "嗯，好了，好了";
				mes "名字叫...." +strcharinfo(0)+ "吧";
				next;
				mes "[魔法工会服务人员]";
				mes "你的转职考试项目是....";
				mes "做出魔法师转职考试要求的";
				switch(rand(3)) {
				case 1:
					mes "^3355FF混合液１号^000000";
					set job_magician_q,1;
					break;
				case 2:
					mes "^3355FF混合液２号^000000";
					set job_magician_q,2;
					break;
				case 3:
					mes "^3355FF混合液３号^000000";
					set job_magician_q,3;
					break;
				default:
					mes "^3355FF混合液４号^000000";
					set job_magician_q,4;
				}
				next;
				getitem 1092,1; //空试管
				mes "[魔法工会服务人员]";
				mes "做混合液所需的所有材料，";
				mes "工会里的书籍上都有写，请你自己去找，";
				mes "书放在你右边中央的书柜";
				next;
				mes "[魔法工会服务人员]";
				mes "你看得到中间的那个机器吧?";
				mes "要调制混合液的话，请利用那个机器";
				mes "那么，加油吧~";
				close;
			}
			mes "[魔法工会服务人员]";
			mes "什么啊!你不是说过想当成魔法师?";
			mes "呼呼!";
			close;
		}
		mes "....都准备好了吗?";
		if (getskilllv(1) < 9) {
			mes "....什么还没准备好啊，";
			mes "初学者的技能等级还不够";
			mes "等提高了初学者技能等级后再来吧";
			close;
		}
		mes strcharinfo(0)+ "的转职考试项目是....";
		mes "做出魔法师转职考试要求的";
		if (job_magician_q == 1)
			mes "混合液１号";
		else if (job_magician_q == 2)
			mes "混合液２号";
		else if (job_magician_q == 3)
			mes "混合液３号";
		else
			mes "混合液４号";
		next;
		mes "[魔法工会服务人员]";
		mes "那么让我检查一下";
		mes "你制作的混合液吧";
		next;
		mes "[魔法工会服务人员]";
		if (countitem(1071) == 0 && countitem(1085) == 0 && countitem(1086) == 0 && countitem(1087) == 0 && countitem(1090) == 0) {
			mes "之前的混合液溶液的试管在哪里呢....?";
			mes "这样无法评价你是否能成为魔法师的";
			close;
		}
		else {
			if ((job_magician_q == 1 && countitem(1071) == 0) || (job_magician_q == 2 && countitem(1085) == 0) || (job_magician_q == 3 && countitem(1086) == 0) || (job_magician_q == 4 && countitem(1087) == 0)) {
				mes "....是不是搞错了";
				mes "这不是你转职考试所要求的";
				mes "混合液";
				mes "请你重新制作了....";
				next;
				mes "[魔法工会服务人员]";
				mes "你的转职考试项目是....";
				mes "做出魔法师转职考试要求的";
				if (job_magician_q == 1)
					mes "混合液１号";
				else if (job_magician_q == 2)
					mes "混合液２号";
				else if (job_magician_q == 3)
					mes "混合液３号";
				else
					mes "混合液４号";

				if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
				else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
				else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
				else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
				else delitem 1090,1; //Mage_Test_Etc
				close;
			}
		}
		if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
		else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
		else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
		else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
		else delitem 1090,1; //Mage_Test_Etc
		mes "....嗯，相信你已经尽力了";
		mes "虽然做的不是很完美，";
		mes "但是这个程度应该可以了";
		mes "........";
		next;
		mes "[魔法工会服务人员]";
		mes "很好!你已经通过魔法师转职考试，";
		mes "我就依你的意愿将你转职成为魔法师吧!";
		mes "你成为了我们的兄弟姊妹~";
		mes "你就是魔法师!";
		mes "恭喜你成为我们魔法工会的一员!";
		next;
		callfunc "Job_Change",Job_Mage;
		callfunc "F_ClearJobVar";
		set Zeny,Zeny+50;
		mes "[魔法工会服务人员]";
		mes "欢迎你来到魔法工会!";
		mes "哎哟，只是打个招呼嘛，";
		mes "你也不用脸红的―";
		mes "怎么不说句话呢―";
		next;
		mes "[魔法工会服务人员]";
		mes "总之，恭喜你成为我们魔法工会的一员!";
		mes "以后还请多多指教~";
		close;
	case 2:
		mes "[魔法工会服务人员]";
		mes "你想成为魔法师?";
		mes "如果你能成为魔法工会的一员，";
		mes "一定会很受欢迎的~";
		next;
		mes "[魔法工会服务人员]";
		mes "首先，初学者的技能要达到９以上";
		mes "其次，必须要做出魔法师转职要求的混合液";
		next;
		mes "[魔法工会服务人员]";
		mes "你要做什么样的混合液，";
		mes "要等你填写完魔法师转职申请书后，";
		mes "我才能告诉你";
		next;
		mes "[魔法工会服务人员]";
		mes "等你认为自己已经符合这两项转职条件后，";
		mes "再来向我询问其他的事项吧";
		close;
	case 3:
		mes "[魔法工会服务人员]";
		mes "应该没事了吧，恩?";
		close;
	}
}

geffen_in,164,112,4	script	物品 调和机	111,{
	mes "[物品 调和机]";
	mes "魔法师转职考试专用物品调和机";
	mes "本器材是吉芬魔法协会的所有物";
	mes "请善加利用";
	next;
	if (select("使用物品调和机:取消") == 1) {
		mes "[物品 调和机]";
		mes "请选择要调和的水溶液";
		next;
		switch(select("斐扬水溶液:梦罗克水溶液:不使用水溶液")) {
		case 1:
			if (countitem(1089) == 0) {
				mes "[物品 调和机]";
				mes "错误，您身上没有该物品";
				close;
			}
			set .@mixitem2,1;
			break;
		case 2:
			if (countitem(1088) == 0) {
				mes "[物品 调和机]";
				mes "错误，您身上没有该水溶液";
				close;
			}
			set .@mixitem2,2;
			break;
		case 3:
			set .@mixitem2,0;
			break;
		}
		while (1) {
			if (.@progress == 2) {
				mes "[物品 调和机]";
				if (.@mixitem1_1 != 0) mes "杰勒比结晶: " + .@mixitem1_1 + "个";
				if (.@mixitem1_2 != 0) mes "毛: " + .@mixitem1_2 + "个";
				if (.@mixitem1_3 != 0) mes "牛奶: " + .@mixitem1_3 + "个";
				if (.@mixitem2 == 0) mes "水溶液: 无";
				if (.@mixitem2 == 1) mes "水溶液: 斐扬水溶液.";
				if (.@mixitem2 == 2) mes "水溶液: 梦罗克水溶液";
				next;
				mes "[物品 调和机]";
				mes "现在要调和物品吗?";
				mes "还是要继续设定物品呢?";
				next;
				switch(select("调和物品:继续设定物品:重新设定")) {
				case 1:
					mes "[物品 调和机]";
					mes "请你将设定好的物品放入调和桶中";
					mes "一旦将物品放进去后，就不能再加减数量";
					next;
					mes "[物品 调和机]";
					mes "请再仔细确认，";
					mes "确认完成后请按'调和开始'按钮，";
					mes "如果要停止请按'结束'按钮";
					next;
					if (select("调和开始:结束") == 1) {
						mes "[物品 调和机]";
						mes "将设定好的物品放入调和机";
						mes "请稍候";
						next;
						mes "[物品 调和机]";
						if (countitem(909) < .@mixitem1_1) {
							mes "'杰勒比结晶'的数量错误，请重新确认";
							close;
						}
						else if (countitem(914) < .@mixitem1_2) {
							mes "'毛'的数量错误，请重新确认";
							close;
						}
						else if (countitem(519) < .@mixitem1_3) {
							mes "'牛奶'的数量错误，请重新确认";
							close;
						}
						else if (.@mixitem2 == 1 || .@mixitem2 == 2) {
							if (countitem(1089) == 0 && countitem(1088) == 0) {
								mes "无法找到水溶液，请重新确认";
								close;
							}
						}
						if (.@mixitem1_1 != 0) delitem 909,.@mixitem1_1; //Jellopy
						if (.@mixitem1_2 != 0) delitem 914,.@mixitem1_2; //Fluff
						if (.@mixitem1_3 != 0) delitem 519,.@mixitem1_3; //Milk
						if (.@mixitem2 == 1) delitem 1089,1; //Payon_Potion
						if (.@mixitem2 == 2) delitem 1088,1; //Morocc_Potion
						mes "将设定好的物品放入调和箱子完毕";
						mes "关闭调和箱子";
						set .@progress,3;
						next;
					}
					break;
				case 2:
					set .@continue,0;
					next;
					break;
				case 3:
					set .@mixitem1_1,0;
					set .@mixitem1_2,0;
					set .@mixitem1_3,0;
					set .@progress,0;
					set .@continue,0;
					mes "[物品 调和机]";
					mes "Reset Complete.";
					mes "Initiate again?";
					next;
					if (select("Yes.:No.") == 1)
						break;
					mes "[物品 调和机]";
					mes "谢谢你的使用";
					close;
				}
				if (.@progress == 3) break;
			}
			else if (.@progress == 1) {
				mes "[物品 调和机]";
				mes "找不到东西";
				next;
			}
			mes "[物品 调和机]";
			mes "请选择要调和的物品：";
			while(1) {
				switch(select("杰勒比结晶:毛:牛奶:查看所设定的物品明细")) {
				case 1:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[物品 调和机]";
							mes "物品数量输入超出范围";
							mes "请设置10000个以下的非负值";
							next;
						}
						else {
							if (countitem(909) > 0) set .@mixitem1_1,.@mixitem1_1 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 2:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[物品 调和机]";
							mes "物品数量输入超出范围";
							mes "请设置10000个以下的非负值";
							next;
						}
						else {
							if (countitem(914) > 0) set .@mixitem1_2,.@mixitem1_2 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 3:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[物品 调和机]";
							mes "物品数量输入超出范围";
							mes "请设置10000个以下的非负值";
							next;
						}
						else {
						if (countitem(519) > 0) set .@mixitem1_3,.@mixitem1_3 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;

				case 4:
					if (.@progress != 2)
						set .@progress,1;
					set .@continue,1;
					next;
				}
				if (.@continue) break;
			}
		}
		mes "[物品 调和机]";
		mes "请将调和的魔法粉末号码写下来";
		next;
		while(1) {
			input .@input;
			if (.@input < 1000 || .@input > 9999) {
				mes "[物品 调和机]";
				if (.@input == 0) {
					mes "确定不使用魔法粉末吗?";
					next;
					if (select("是的:不") == 1) {
						break;
					}
				}
				else {
					mes "超出输入范围";
					mes "请重新输入";
					next;
				}
			}
			else {
				mes "[物品 调和机]";
				mes "号码 #" + .@input + " 魔法粉末";
				mes "对吗?";
				next;
				if (select("是的:不是") == 1) {
					if (.@input == 8472)
						set .@magic_powder,1;
					else if (.@input == 3735)
						set .@magic_powder,2;
					else if (.@input == 2750)
						set .@magic_powder,3;
					else if (.@input == 5429)
						set .@magic_powder,4;
					else
						set .@magic_powder,5;
				}
				break;
			}
		}
		mes "[物品 调和机]";
		mes "最后，请选择催化石";
		next;
		switch(select("黄色魔力宝石:红色魔力宝石:蓝色魔力宝石:透明的宝石:跳过")) {
		case 1: set .@mixitem3,1; break;
		case 2: set .@mixitem3,2; break;
		case 3: set .@mixitem3,3; break;
		case 4: set .@mixitem3,4; break;
		case 5: break;
		}
		mes "[物品 调和机]";
		mes "已经准备好了，现在可以开始调和了";
		mes "请稍候";
		next;
		mes "[物品 调和机]";
		mes "- 每日一句 -";
		switch(rand(1,5)) {
		case 1:
			mes "“被最信任的人背叛”";
			mes "意思就是说太过容易轻信别人，受伤的往往会是自己";
			break;
		case 2:
			mes "“冤家路窄”";
			mes "意思就是说与人结怨的话无可避免的还是得跟那个人碰面，最后容易为自己招来横祸";
			break;
		case 3:
			mes "“远亲不如近邻”";
			mes "意思就是说当紧急事情发生的时候在远处的亲人朋友帮不上你的忙，倒是街头巷尾的人可以给你一些帮助";
			break;
		case 4:
			mes "意思是说常说谎话的话，到最后可能会落得大家都不相信你的地步";
			break;
		default:
			mes "意思就是说你要先主动对别人好，别人才会也对你好";
		}
		next;
		if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 1 && .@mixitem2 == 1 && .@mixitem3 == 1 && .@magic_powder == 1) {
			mes "[物品 调和机]";
			mes "魔法师转职考试要求的混合液1号";
			getitem 1071,1; //Mage_Test_1
			next;
		}
		else if (.@mixitem1_1 == 3 && .@mixitem1_2 == 1 && .@mixitem1_3 == 1 && .@mixitem2 == 0 && .@mixitem3 == 2 && .@magic_powder == 2) {
			mes "[物品 调和机]";
			mes "魔法师转职考试要求的混合液2号";
			getitem 1085,1; //Mage_Test_2
			next;
		}
		else if (.@mixitem1_1 == 6 && .@mixitem1_2 == 1 && .@mixitem1_3 == 0 && .@mixitem2 == 1 && .@mixitem3 == 3 && .@magic_powder == 3) {
			mes "[物品 调和机]";
			mes "魔法师转职考试要求的混合液3号";
			getitem 1086,1; //Mage_Test_3
			next;
		}
		else if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 0 && .@mixitem2 == 2 && .@mixitem3 == 4 && .@magic_powder == 4) {
			mes "[物品 调和机]";
			mes "魔法师转职考试要求的混合液4号";
			getitem 1087,1; //Mage_Test_4
			next;
		}
		else {
			mes "[物品 调和机]";
			mes "制作混合液的过程中发生了某些问题";
			getitem 1090,1; //Mage_Test_Etc
			next;
		}
		mes "[物品 调和机]";
		mes "调和完成了";
		mes "调和机作业终止";
		mes "感谢你的使用";
		close;
	}
	close;
}

pay_arche,122,100,0	script	魔法工会服务人员#pay1	88,{
	mes "[魔法工会服务人员]";
	mes "要购买水溶液吗?";
	mes "那么需要50 金币和至少１个空试管";
	next;
	if (select("购买水溶液:放弃") == 1) {
		mes "[魔法工会服务人员]";
		if (Zeny < 50) {
			mes "很可惜，您的金钱不足";
			close;
		}
		if (countitem(1092) == 0) {
			mes "空着手的话物品要怎么携带呢?";
			mes "带一个空试管装来吧";
			close;
		}
		delitem 1092,1; //空试管
		set zeny,zeny-50;
		getitem 1089,1; //Payon_Potion
	}
	close;
}

moc_ruins,91,150,0	script	魔法工会服务人员#moc1	93,{
	mes "[魔法工会服务人员]";
	mes "要购买水溶液吗?";
	mes "那么需要50 金币和至少１个空试管";
	next;
	if (select("购买水溶液:放弃") == 1) {
		mes "[魔法工会服务人员]";
		if (zENY < 50) {
			mes "很可惜，您的金钱不足";
			close;
		}
		if (countitem(1092) == 0) {
			mes "空着手的话物品要怎么携带呢?";
			mes "带一个空试管装来吧";
			close;
		}
		delitem 1092,1; //空试管
		set zeny,zeny-50;
		getitem 1088,1; //Morocc_Potion
	}
	CLOSE;
}

geffen_in.gat,177,112,0	script	书架#geffen	111,{
	mes "[物品调和书籍]";
	mes "本书是吉芬魔法协会的所有物，";
	mes "请善加利用";
	next;
	switch (select("混合液1号","混合液2号","混合液3号","混合液4号","停止")) {
	case 1:
		mes "[魔法师转职考试要求的混合液1号]";
		mes "* 材料明细 *";
		mes " - 杰勒比结晶 2 个";
		mes " - 毛 3 个";
		mes " - 牛奶 1 个";
		next;
		mes "[魔法师转职考试要求的混合液1号]";
		mes "* 溶剂 *";
		mes "斐扬水溶液";
		mes "所在地：斐扬弓箭手村庄的小泉";
		next;
		mes "[魔法师转职考试要求的混合液1号]";
		mes "* 魔法粉末 *";
		mes " - 8472";
		next;
		mes "[魔法师转职考试要求的混合液1号]";
		mes "* 催化物 *";
		mes " - 黄色魔力矿石";
		mes "(内藏在物品调和机中)";
		close;
	case 2:
		mes "[魔法师转职考试要求的混合液2号]";
		mes "* 材料明细 *";
		mes " - 杰勒比结晶 3 个";
		mes " - 毛 1 个";
		mes " - 牛奶 1 个";
		next;
		mes "[魔法师转职考试要求的混合液2号]";
		mes "* 溶剂 *";
		mes " - 不需要";
		next;
		mes "[魔法师转职考试要求的混合液2号]";
		mes "* 魔法粉末 *";
		mes " - 3735";
		next;
		mes "[魔法师转职考试要求的混合液2号]";
		mes "* 催化物 *";
		mes " - 红色魔力矿石";
		mes "(内藏在物品调和机中)";
		close;
	case 3:
		mes "[魔法师转职考试要求的混合液3号]";
		mes "* 材料明细 *";
		mes " - 杰勒比结晶 6 个";
		mes " - 毛 1 个";
		next;
		mes "[魔法师转职考试要求的混合液3号]";
		mes "* 溶剂 *";
		mes "斐扬水溶液";
		mes "所在地：斐扬弓箭手村庄的小泉";
		next;
		mes "[魔法师转职考试要求的混合液3号]";
		mes "* 魔法粉末 *";
		mes " - 2750";
		next;
		mes "[魔法师转职考试要求的混合液3号]";
		mes "* 催化物 *";
		mes " - 蓝色魔力矿石";
		mes "(内藏在物品调和机中)";
		close;
	case 4:
		mes "[魔法师转职考试要求的混合液4号]";
		mes "* 材料明细 *";
		mes " - 杰勒比结晶 2 个";
		mes " - 毛 3 个";
		next;
		mes "[魔法师转职考试要求的混合液4号]";
		mes "* 溶剂 *";
		mes "梦罗克水溶液";
		mes "所在地：梦罗克金字塔入口附近的小泉";
		next;
		mes "[魔法师转职考试要求的混合液4号]";
		mes "* 魔法粉末 *";
		mes " - 5429";
		next;
		mes "[魔法师转职考试要求的混合液4号]";
		mes "* 催化物 *";
		mes " - 透明的宝石";
		mes "(内藏在物品调和机中)";
		close;
	case 5:
		close;
	}
}