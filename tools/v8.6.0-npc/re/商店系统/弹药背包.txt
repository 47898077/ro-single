//===== eAthena Script =======================================
//= Magazine Dealer 凯尼
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= eAthena 1.0+
//===== Description: =========================================
//= Turns bullets/spheres into packs/casings.
//===== Additional Comments: =================================
//= 1.0 First version. [SinSloth]
//= 1.1 Optimized version - Reduced to only one function [SinSloth]
//= 1.2 Optimized^2, corrected npc's name [ultramage]
//= 1.2a Optimized. Please, ommit extra NPC names [Lupus]
//============================================================

-	script	弹药包经销商 凯尼	-1,{

	mes "[凯尼]";	
	mes "我是弹药包经销商, 凯尼!";
	if(BaseJob != Job_Gunslinger) {
		mes "在这里我给每位枪手包装他们的";
		mes "子弹和榴弹.";
		next;
		mes "[凯尼]";
		mes "可是我怎么看, 你似乎都不像是";
		mes "一位枪手啊. 恐怕我得在你四处";
		mes "乱转悠前叫你离开这里了.";
		close;
	}
	mes "如果你的弹药携带起来很繁重的";
	mes "话, 就来找我吧!";
	next;
	mes "[凯尼]";
	mes "我能为你包装子弹盒和子弹桶, ";
	mes "从而让你携带起来更轻便!";
	mes "来! 看一看!";
	next;
	switch(select("子弹盒 风:子弹盒 暗:子弹盒 毒:子弹盒 水:子弹盒 火:弹药桶:红色弹药桶:银色弹药桶:取消")) {
	case 1: callfunc "Func_Casing",13204,12144; break;
	case 2: callfunc "Func_Casing",13206,12145; break;
	case 3: callfunc "Func_Casing",13205,12146; break;
	case 4: callfunc "Func_Casing",13207,12147; break;
	case 5: callfunc "Func_Casing",13203,12148; break;
	case 6: callfunc "Func_Casing",13200,12149; break;
	case 7: callfunc "Func_Casing",13202,12150; break;
	case 8: callfunc "Func_Casing",13201,12151; break;
	default:
		mes "[凯尼]";
		mes "好吧. 如果你还有什么其它事情";
		mes "而需要我来帮助你的话, 请要告";
		mes "诉我.";
		close;
	}
	close;
}

function	script	Func_Casing	{

	mes "[凯尼]";
	mes "请输入你需要的数量.";
	next;
	mes "[凯尼]";
	mes "包装每包" +getitemname(getarg(1))+ " ";
	if(getarg(0) == 13202) 
		mes "需要500发红色子弹";
	else
		mes "需要消耗500 " +getitemname(getarg(0))+ "";
	mes "还有手续费500 Zeny.";
	next;
	mes "[凯尼]";
	mes "你可以最多跟我交易50 包.";
	mes "如果你想要取消的话请输入0.";
	next;
	input .@amount;
	mes "[凯尼]";
	if(.@amount < 1) {
		mes "好吧. 如果你还有什么其它事情";
		mes "而需要我来帮助你的话, 请要告";
		mes "诉我.";
		close;
	}
	if(.@amount > 50)	{
		mes "你输入的超出的限制范围!";
		mes "再试试吧?";
		close;
	}
	//负重检查
	if(checkweight(getarg(1), .@amount) != 1)	{
		mes "你超重了.";
		mes "请清理下你的物品.";
		close;
	}
	
	//材料检查
	if(countitem(getarg(0)) < .@amount * 500) {
		mes "呃......";
		mes "你没有足够的材料来包装你想要";
		mes "的那么多数量的物品.";
		mes "请带来足够数量的物品.";
		close;
	}
	
	//金钱检查
	if(Zeny < .@amount * 500) {
		mes "呃... 你没有足够的金钱.";
		mes "这费用为500 Zeny";
		mes "检查下你的金钱再来吧.";
		close;
	}

	mes "非常好啊!";
	mes "确认数量!";
	if(getarg(1) < 12149)
		mes "我将立刻给你子弹盒.";
	else
		mes "我将立刻给你子弹桶.";
	set Zeny, Zeny - .@amount * 500;
	delitem getarg(0), .@amount * 500;
	getitem getarg(1), .@amount;
	close;
}

que_ng,187,149,3	duplicate(弹药包经销商 凯尼)	弹药包经销商 凯尼#que	83
izlude,171,127,3	duplicate(弹药包经销商 凯尼)	弹药包经销商 凯尼#iz	83
alberta,118,157,3	duplicate(弹药包经销商 凯尼)	弹药包经销商 凯尼#alb	83

