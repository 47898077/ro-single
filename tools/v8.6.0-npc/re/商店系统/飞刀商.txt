//===== eAthena Script =======================================
//= Kunai "Trader" @ que_ng
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= eAthena 1.0
//===== Description: =========================================
//= NPC that trades you a few shurikens + ninja stones for 
//= elemental kunais.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=	  id of the 2 required items plus the amount. Can trade
//= 	  up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//============================================================

-	script	飞刀商人 卡新	-1,{

	mes "[卡新]";
	if(BaseJob != Job_Ninja) {
		mes "我是替影子们提供";
		mes "兑换物品服务的，";
		mes "叫做 风之卡新";
		next;
		mes "[卡新]";
		mes "我看你不像是个忍者，";
		mes "你就随便看看后再走吧";
		close;
	}
	mes "你好，我是 卡新!";
	mes "如果你有缺少飞刀的话，";
	mes "欢迎你向我购买!";
	next;
	mes "[卡新]";
	mes "请问你要买哪一项呢?";
	next;

	switch(select("巨毒飞刀:垂冰飞刀:强风飞刀:黑土飞刀:火焰飞刀:取消")) {
	//用法: callfunc "Kunai_Trade",itemreqid1,itemreqct1,itemreqid2,itemreqct2,itemidtrade;
	case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
	case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
	case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
	case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
	case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
	default:
		mes "[卡新]";
		mes "嗯~ 好吧~";
		mes "随时再来~";
		mes "我, 风之卡新, 永远在这里.";
		close;
	}
	close;
}

function	script	Kunai_Trade	{
	
	mes "[卡新]";
	mes "如果你给我"+getarg(1)+" 支"+getitemname(getarg(0))+" ";
	mes "和"+getarg(3)+" 颗"+getitemname(getarg(2))+", 我会给你一包含";
	mes "10 支的"+getitemname(getarg(4))+".";
	next;
	mes "[卡新]";
	mes "你一次最多能交易500 包.";
	mes "如果你不想交易的话, 请输入0.";
	next;
	input .@amount;

	mes "[卡新]";
	if(.@amount < 1) {
		mes "嗯~ 好吧~";
		mes "随时再来~";
		mes "我, 风之卡新, 永远在这里.";
		close;
	}
	if(.@amount > 500) {
		mes "你输入的数字超额了!";
		mes "下次请输入有效的数字~!";
		close;
	}
	if(countitem(getarg(0)) < .@amount*getarg(1) || countitem(getarg(2)) < .@amount*getarg(3)) {
		mes "嗯... 情况不太妙~";
		mes "你没有足够的材料来交易你想要";
		mes "的那么多的苦无.";
		mes "如果你真需要, 就要带够材料. ";
		close;
 	}
	if(checkweight(getarg(4), .@amount*10) == 0) {
		mes "你的背包太满了不能放这么多货";
		mes "物. 等你为这些货品腾出地方在";
		mes "来找我吧.";
		close;
	}
	mes "很好~ 非常好!";
	mes "确认数量!";
	mes "这是你的货物.";
	delitem getarg(0),getarg(1)*.@amount;
	delitem getarg(2),getarg(3)*.@amount;
	getitem getarg(4),10*.@amount;
	close;
}

que_ng,72,29,3	duplicate(飞刀商人 卡新)	飞刀商人 卡新#que	83
prontera,178,240,3	duplicate(飞刀商人 卡新)	飞刀商人 卡新#prt	83
