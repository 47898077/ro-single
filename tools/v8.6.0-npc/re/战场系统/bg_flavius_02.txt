// ==============================================================================
// 战场系统 - 霍厄斯战场 2
// ==============================================================================
//翻译组：乐土工作室 http://promised-land.cn
//翻译员：『PLTeam』Soul
//翻译进度：100%
//脚本美化：『PLTeam』Soul
//===============================================================================

// 登记处NPC事项
// *********************************************************************

bat_room,57,97,4	script	霍厄斯战场-02登记处::Fl2R_Guillaume	418,{
	end;
OnInit:
	waitingroom "战区 10 玩家",10,"Flavius_BG2::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG2_id1, waitingroom2bg("bat_b02",390,10,"Flavius_BG2::OnGuillaumeQuit","");
	end;
}

bat_room,57,74,0	script	霍厄斯战场-02登记处::Fl2R_Croix	414,{
	end;
OnInit:
	waitingroom "战区 10 玩家",10,"Flavius_BG2::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG2_id2, waitingroom2bg("bat_b02",10,290,"Flavius_BG2::OnCroixQuit","");
	end;
}

// 战场启动
// *********************************************************************

-	script	Flavius_BG2	-1,{
	end;

OnInit:
	disablenpc "吉永军司令官#fl2";
	disablenpc "克勒亚军司令官#fl2";
	disablenpc "战场治疗师#fl21";
	disablenpc "战场治疗师#fl22";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@FlaviusBG2 == 0 )
		donpcevent "Flavius_BG2::OnReadyCheck";
	end;

OnReadyCheck:
	if( $@FlaviusBG2 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Fl2R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"Fl2R_Croix");
	
	if( .@Guillaume < 10 || .@Croix < 10 )
	{
		mapannounce "bat_room","霍厄斯战场-02 [80-99] G: " + .@Guillaume + "/10, C: " + .@Croix + "/10",1,0x00FF00;
		end;
	}

	// BG 变量
	set $@FlaviusBG2, 1;
	set $@FlaviusBG2_Victory, 0;
	set .Guillaume_Score, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	// NPC 准备
	donpcevent "#gfl2_respawn::OnBGStart";
	donpcevent "#cfl2_respawn::OnBGStart";
	enablenpc "战场治疗师#fl21";
	enablenpc "战场治疗师#fl22";
	disablenpc "吉永军司令官#fl2";
	disablenpc "克勒亚军司令官#fl2";
	// 建筑及传送点
	donpcevent "Fl2R_Guillaume::OnEnterBG";
	donpcevent "Fl2R_Croix::OnEnterBG";
	announce "霍厄斯战场-02 [80-99]已经开始!",0,0x00FF00;
	initnpctimer;
	// 比赛开始!!

OnRoundStart:
	sleep 2000;
	if( $@FlaviusBG2 != 1 ) end;

	areapercentheal "bat_b02",382,2,397,17,100,100;
	areapercentheal "bat_b02",2,282,17,297,100,100;
	bg_warp $@FlaviusBG2_id1,"bat_b02",311,224;
	bg_warp $@FlaviusBG2_id2,"bat_b02",87,75;

	sleep 2000;
	if( $@FlaviusBG2 != 1 ) end;

	set .Match, .Match + 1;
	// 水晶生成
	set .Guillaume_Crystal, bg_monster($@FlaviusBG2_id1,"bat_b02",328,150,"吉永军水晶",1914,"Flavius_BG2::OnGuillaumeBreak");
	setwall "bat_b02",327,149,2,6,0,"gui_wall2-1";
	setwall "bat_b02",329,149,2,0,0,"gui_wall2-2";
	setwall "bat_b02",329,151,2,2,0,"gui_wall2-3";
	setwall "bat_b02",327,151,2,4,0,"gui_wall2-4";

	set .Croix_Crystal, bg_monster($@FlaviusBG2_id2,"bat_b02",62,150,"克勒亚军水晶",1915,"Flavius_BG2::OnCroixBreak");
	setwall "bat_b02",61,149,2,6,0,"cro_wall2-1";
	setwall "bat_b02",63,149,2,0,0,"cro_wall2-2";
	setwall "bat_b02",63,151,2,2,0,"cro_wall2-3";
	setwall "bat_b02",61,151,2,4,0,"cro_wall2-4";

	// 监护人生成
	bg_monster $@FlaviusBG2_id1,"bat_b02",328,160,"吉永军 守护者",1949,"Flavius_BG2::OnGuiGuardian";
	bg_monster $@FlaviusBG2_id1,"bat_b02",328,140,"吉永军 守护者",1950,"Flavius_BG2::OnGuiGuardian";
	set .Guillaume_Guardian, 2;
	bg_monster $@FlaviusBG2_id2,"bat_b02",62,160,"克勒亚军 守护者",1949,"Flavius_BG2::OnCroGuardian";
	bg_monster $@FlaviusBG2_id2,"bat_b02",61,140,"克勒亚军 守护者",1950,"Flavius_BG2::OnCroGuardian";
	set .Croix_Guardian, 2;
	// Announces
	mapannounce "bat_b02"," 霍厄斯战场-02 [ " + .Match + " ] 局已经开始!!",1,0x00FF00;
	end;

OnRoundStop:
	// 移除魔物
	killmonster "bat_b02","Flavius_BG2::OnGuiGuardian";
	killmonster "bat_b02","Flavius_BG2::OnCroGuardian";
	delwall "gui_wall2-1";
	delwall "gui_wall2-2";
	delwall "gui_wall2-3";
	delwall "gui_wall2-4";
	killmonster "bat_b02","Flavius_BG2::OnGuillaumeBreak";
	delwall "cro_wall2-1";
	delwall "cro_wall2-2";
	delwall "cro_wall2-3";
	delwall "cro_wall2-4";
	killmonster "bat_b02","Flavius_BG2::OnCroixBreak";
	end;

OnGuiGuardian:
	if( set(.Guillaume_Guardian, .Guillaume_Guardian - 1) <= 0 )
	{
		delwall "gui_wall2-1";
		delwall "gui_wall2-2";
		delwall "gui_wall2-3";
		delwall "gui_wall2-4";
		mapannounce "bat_b02","吉永军的水晶失去保护!",1,0x0000FF;
	}
	end;

OnCroGuardian:
	if( set(.Croix_Guardian, .Croix_Guardian - 1) <= 0 )
	{
		delwall "cro_wall2-1";
		delwall "cro_wall2-2";
		delwall "cro_wall2-3";
		delwall "cro_wall2-4";
		mapannounce "bat_b02","克勒亚军的水晶失去保护!",1,0xFF0000;
	}
	end;

OnGuillaumeBreak:
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Guillaume_Loss, .Guillaume_Loss + 1;
	if( set(.Croix_Score, .Croix_Score + 1) < 2 )
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","吉永军的水晶被摧毁!",1,0x0000FF;
		donpcevent "Flavius_BG2::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","克勒亚军在 霍厄斯战场-02 战斗中获胜!",1,0xFF0000;
		set $@FlaviusBG2_Victory, 2;
		donpcevent "Flavius_BG2::OnMatchEnd";
	}
	end;

OnCroixBreak:
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Croix_Loss, .Croix_Loss + 1;
	if( set(.Guillaume_Score, .Guillaume_Score + 1) < 2 )
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","克勒亚军的水晶被摧毁!",1,0xFF0000;
		donpcevent "Flavius_BG2::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","吉永军在 霍厄斯战场-02 战斗中获胜!",1,0x0000FF;
		set $@FlaviusBG2_Victory, 1;
		donpcevent "Flavius_BG2::OnMatchEnd";
	}
	end;

OnTimer2400000:
	mapannounce "bat_b02"," 霍厄斯战场-02 战斗将在5分钟后结束",1,0x00FF00;
	end;
OnTimer2640000:
	mapannounce "bat_b02"," 霍厄斯战场-02 战斗将在1分钟后结束",1,0x00FF00;
	end;

OnTimer2700000:
	if( .Croix_Score > .Guillaume_Score )
	{
		mapannounce "bat_b02","克勒亚军在 霍厄斯战场-02 战斗中获胜!",1,0xFF0000;
		set $@FlaviusBG2_Victory, 2;
	}
	else if( .Croix_Score < .Guillaume_Score )
	{
		mapannounce "bat_b02","吉永军在 霍厄斯战场-02 战斗中获胜!",1,0x0000FF;
		set $@FlaviusBG2_Victory, 1;
	}
	else
	{
		mapannounce "bat_b02"," 霍厄斯战场-02 战斗已经结束. 这场战斗宣告平局",1,0x00FF00;
		set $@FlaviusBG2_Victory, 3;
	}

OnMatchEnd:
	set $@FlaviusBG2, 2;
	stopnpctimer;
	donpcevent "#gfl2_respawn::OnBGStop";
	donpcevent "#cfl2_respawn::OnBGStop";
	disablenpc "战场治疗师#fl21";
	disablenpc "战场治疗师#fl22";
	enablenpc "吉永军司令官#fl2";
	enablenpc "克勒亚军司令官#fl2";
	sleep 2000;
	bg_warp $@FlaviusBG2_id1,"bat_b02",390,10;
	bg_warp $@FlaviusBG2_id2,"bat_b02",10,290;
	sleep 3000;
	mapannounce "bat_b02"," 霍厄斯战场-02 将在1分钟后关闭!",1,0x00FF00;
	initnpctimer;
	end;

OnTimer30000:
	if( $@FlaviusBG2 == 2 )
		mapannounce "bat_b02"," 霍厄斯战场-02 将在30秒后关闭!",1,0x00FF00;
	end;
OnTimer50000:
	if( $@FlaviusBG2 == 2 )
		mapannounce "bat_b02"," 霍厄斯战场-02 将在10秒后关闭!",1,0x00FF00;
	end;

OnTimer60000:
	if( $@FlaviusBG2 != 2 )
		end;
OnReset:
	stopnpctimer;
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Guillaume_Score, 0;
	set .Guillaume_Crystal, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Crystal, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	set $@FlaviusBG2_Victory, 0;
	// NPC事项
	disablenpc "吉永军司令官#fl2";
	disablenpc "克勒亚军司令官#fl2";
	disablenpc "战场治疗师#fl21";
	disablenpc "战场治疗师#fl22";

	if( $@FlaviusBG2_id1 ) { bg_destroy $@FlaviusBG2_id1; set $@FlaviusBG2_id1, 0; }
	if( $@FlaviusBG2_id2 ) { bg_destroy $@FlaviusBG2_id2; set $@FlaviusBG2_id2, 0; }
	sleep 1000;
	mapwarp "bat_b02","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_b02",0,3; // Just in case someone else
	sleep 2000;
	bg_updatescore "bat_b02",0,0;
	set $@FlaviusBG2, 0;
	donpcevent "Flavius_BG2::OnReadyCheck";
	end;
}

// 战场奖励
// *********************************************************************

bat_b02,389,13,5	script	吉永军司令官#fl2	419,{
	if( $@FlaviusBG2_Victory )
	{
		if( $@FlaviusBG2_Victory == Bat_Team )
		{ // 胜利者
			mes "[阿历斯 罗斯]";
			mes "神圣的吉永军!!";
			mes "让我们享受胜利的荣耀吧!";
			mes "" + strcharinfo(0) + ", 恭喜您成为胜利者";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 失败者
			mes "[阿历斯 罗斯]";
			mes "你虽然失败了，但你已经在这次战斗中尽力了.";
			mes "这是吉永 马乐罗对你奋战的奖励!";
			mes "把这次的失败当作教训吧.";
			close2;
			set .@Reward, 3;
		}

		setquest 2070;
		getitem 7829, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_b02,9,293,5	script	克勒亚军司令官#fl2	415,{
	if( $@FlaviusBG2_Victory )
	{
		if( $@FlaviusBG2_Victory == Bat_Team )
		{ // 胜利者
			mes "[斯曼德理]";
			mes "神圣的克勒亚军!!";
			mes "让我们享受胜利的荣耀吧!";
			mes "" + strcharinfo(0) + ", 恭喜您成为胜利者";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 失败者
			mes "[斯曼德理]";
			mes "噢, " + strcharinfo(0) + ". 不要太难过.";
			mes "虽然我们没有战胜，但是已尽我们所能了.";
			mes "这是克勒亚军赠予的皇家礼物，请别忘记这次战争，我们会在下次赢回来的.";
			close2;
			set .@Reward, 3;
		}

		setquest 2070;
		getitem 7829, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// 战场治疗师
// *********************************************************************

bat_b02,391,13,5	script	战场治疗师#fl22	95,{
	mes "[战场治疗师]";
	mes "只需要闭上眼，然后深呼吸.";
	mes "这样可以减轻痛楚.";
	specialeffect2 312;
	close;
}

bat_b02,11,293,5	script	战场治疗师#fl21	95,{
	mes "[战场治疗师]";
	mes "只需要闭上眼，然后深呼吸.";
	mes "这样可以减轻痛楚.";
	specialeffect2 312;
	close;
}

// 战场重生
// *********************************************************************

bat_b02,390,10,0	script	#gfl2_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",382,2,397,17,100,100;
	areawarp "bat_b02",382,2,397,17,"bat_b02",311,224;
	initnpctimer;
	end;
}

bat_b02,10,290,0	script	#cfl2_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",2,282,17,297,100,100;
	areawarp "bat_b02",2,282,17,297,"bat_b02",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bat_b02,304,231,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat32	973
bat_b02,319,231,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat33	973
bat_b02,304,218,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat34	973
bat_b02,319,218,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat35	973
bat_b02,304,231,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat36	973
bat_b02,304,231,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat37	973
bat_b02,335,142,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat38	973
bat_b02,335,157,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat39	973
bat_b02,390,16,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat40	973
bat_b02,292,163,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat41	973
bat_b02,292,136,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat42	973
bat_b02,241,185,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat43	973
bat_b02,247,179,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat44	973

bat_b02,96,81,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat30	974
bat_b02,96,68,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat31	974
bat_b02,79,81,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat32	974
bat_b02,79,68,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat33	974
bat_b02,96,81,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat34	974
bat_b02,96,81,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat35	974
bat_b02,59,164,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat36	974
bat_b02,59,137,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat37	974
bat_b02,10,296,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat38	974
bat_b02,110,162,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat39	974
bat_b02,110,137,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat40	974
bat_b02,152,120,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat41	974
bat_b02,158,114,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat42	974

// MapFlags
// *********************************************************************

bat_b02	mapflag	battleground
bat_b02	mapflag	nomemo
bat_b02	mapflag	nosave	SavePoint
bat_b02	mapflag	noteleport
bat_b02	mapflag	nowarp
bat_b02	mapflag	nowarpto
bat_b02	mapflag	noreturn
bat_b02	mapflag	nobranch
bat_b02	mapflag	nopenalty
