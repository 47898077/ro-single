//===========================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ------------------------------------------
// - 艾尔贝塔 导游 脚本 -
// ------------------------------------------
// 版本 1.1
//===========================================

// North --------------------------------------------------------
alberta,23,238,4	script	导游#alberta	105,{
	mes "[艾尔贝塔导游]";
	mes "欢迎光临 港口之都艾尔贝塔.";
	mes "我们为了来到艾尔贝塔的旅客 安排了导游为您们介绍这个城市 当您们有问题的时候 随时可以询问他们.";
	mes "第一次来到这里的旅客 先按主选单 选择注意事项 了解必须注意的事情.";
	while(1) {
		next;
		switch (select("开始由导游说明城市:删掉微型地图上全部的记号:听取注意事项:取消")) {
		case 1:
			if(@compass&0x20 == 0) {
				mes "[艾尔贝塔导游]";
				mes "要在微型地图上留下记号吗?";
				next;
				if(select("要:不要")==1)
					set @compass,@compass|0x20;
			}
			mes "[艾尔贝塔导游]";
			mes "请选择";
			while(1) {
				next;
				switch (select("^FF0000商人协会^000000","武器商店:道具商店:旅馆:取消")) {
				case 1:
					mes "[艾尔贝塔导游]";
					mes "专门办理商人转职业务的商人协会.";
					if(@compass&0x20)
						viewpoint 1,33,41,1,0xFF0000;
					continue;
				case 2:
					mes "[艾尔贝塔导游]";
					mes "武器商店位于微型地图中心的南边.";
					if(@compass&0x20)
						viewpoint 1,117,37,2,0xFF00FF;
					continue;
				case 3:
					mes "[艾尔贝塔导游]";
					mes "道具商店位于微型地图上的中心处.";
					if(@compass&0x20)
						viewpoint 1,98,154,3,0xFF00FF;
					continue;
				case 4:
					mes "[艾尔贝塔导游]";
					mes "旅馆位于微型地图中心的北边.";
					if(@compass&0x20)
						viewpoint 1,65,233,4,0xFF00FF;
					continue;
				case 5:
					mes "[艾尔贝塔导游]";
					mes "需要删掉记号请选取 '删掉微型地图上全部的记号' .";
				}
				break;
			}
			continue;
		case 2:
			mes "[艾尔贝塔导游]";
			mes "删除完毕";
			viewpoint 2,33,41,1,0xFF0000;
			viewpoint 2,117,37,2,0xFF00FF;
			viewpoint 2,98,154,3,0xFF00FF;
			viewpoint 2,65,233,4,0xFF00FF;
			set @compass,@compass&~0x20;
			continue;
		case 3:
			mes "[艾尔贝塔导游]";
			mes "我们废除了原来较差的指引方式采用最现代化的。无论您在那里都可以借由微型地图知道您的位置.";
			mes "您们可以直接确认卢恩米德加尔特王国魔法和科学技术组合后的广域测位系统的功能.";
			next;
			mes "[艾尔贝塔导游]";
			mes "在流览之前必须先参考右上方的微型地图.";
			mes "如果看不见微型地图的话 请按'ctrl+tab'或者选取基本情报视窗 内的'map'.";
			mes "微型地图出现之后, 在地图上按  '-' 几下可以看见所有城市.";
			continue;
		case 4:
			mes "[艾尔贝塔导游]";
			mes "再见";
			close;
		}
	}
}

alberta,120,60,3	duplicate(导游#alberta)	导游	105