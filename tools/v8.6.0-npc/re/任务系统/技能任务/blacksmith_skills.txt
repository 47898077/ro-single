//===== eAthena Script ======================================= 
//= Blacksmith Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.3a
//===== Compatible With: ===================================== 
//= eAthena Revision 3800+
//===== Description: ========================================= 
//= Temp quests for new skills for 2nd classes
//===== Additional Comments: ================================= 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//= 1.3c Inverted the weight check for Greed [DracoRPG]
//============================================================


//============================================================
// 铁匠技能 - 贪婪 + 诡计的商术
//============================================================
//== GOODDAY - GREED SKILL ===================================
geffen.gat,172,52,7	script	固特伊	826,{
	mes "[固特伊]";
	if(BaseJob!=Job_Blacksmith) goto L_nosmith;
	if(getskilllv(1013)) goto L_alreadyhave;
	if(Weight < MaxWeight-5000) goto L_notstrong; // You must carry so much that you cannot add 500 more (weight is x10!)


	mes "你看起来非常强壮啊！";
	mes "我想我能教你个技能~";
	mes "就像巫师移动东西不用手一样";
	skill 1013,1,0;
	close;

L_notstrong:
	mes "等你再强壮些回来找我吧";
	mes "当你的负重超过500";
	close;

L_nosmith:
	mes "吉芬是个不错的城市啊.";
	mes "难道你不这样认为吗?";
	next;
	emotion e_omg;
	mes "[固特伊]";
	mes "什么？";
	mes "你不这样想？!";
	close;

L_alreadyhave:
	mes "我已经把我知道的传授给你了";
	mes "所以你不必再回来找我了";
	close;
}

//== AKI - 诡计的商术 ==================================
geffen.gat,178,72,7	script	亚齐	726,{
	mes "[亚齐]";
	if(BaseJob!=Job_Blacksmith) goto L_nosmith;
	if(getskilllv(1012)) goto L_alreadyhave;
	if(getskilllv(1013)<1) goto L_nogreed;
	if(JobLevel<30) goto L_nojob;
	mes "我们需要点东西";
	mes "帮我带来我就教你:";
	mes "2 钢铁";
	mes "8 煤矿";
	mes "2 钢铁锤子";
	mes "1 帝特立米药水";
	mes "500 Zeny";
	next;
	
	mes "[亚齐]";
	mes "让我检查下你的物品.";
	next;
	
	if(countitem(999)<2 || countitem(1003)<8 || countitem(613)<2 || countitem(971)<1 || zeny<500) goto L_noitems;//Items: Steel, Coal, Iron_Hammer, Detrimindexta,
	delitem 999, 2;//Items: Steel,
	delitem 1003, 8;//Items: Coal,
	delitem 613, 2;//Items: Iron_Hammer,
	delitem 971, 1;//Items: Detrimindexta,
	set zeny, zeny-500;
	
	mes "[亚齐]";
	mes "很好，你把东西都带来了";
	mes "我们开始学习吧~";
	skill 1012,1,0;
	next;
	
	specialeffect 183;
	mes "哇！你学的真快！";
	mes "好好使用你的新技能!";
	close;

L_noitems:
	mes "[亚齐]";
	mes "你没有足够的东西哦.";
	mes "等你有了再来找我吧";
	close;
	
L_nosmith:
	mes "不要整天呆在这无聊的地方.";
	mes "但是~他却喜欢这个无聊的地方~";
	emotion e_laugh;
	close;

L_nogreed:
	mes "如果你还想学点什么";
	mes "那你就站在这跟天气说话吧~";
	close;

L_alreadyhave:
	mes "我想你已经学过了";
	mes "吉芬那你也许还能再学会个技能~";
	close;
	
L_nojob:
	mes "等你成为名铁匠再来吧";
	mes "最起码30级的铁匠啊！";
	close;
}
