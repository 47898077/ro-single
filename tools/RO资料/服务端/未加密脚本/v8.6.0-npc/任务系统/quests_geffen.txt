//===== rAthena Script ======================================= 
//= Quest NPCs related to Geffen
//===== By: ================================================== 
//= kobra_k88 & Samuray22
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena 7.15 +
//===== Description: ========================================= 
//= Welding Mask and Headset Quests.
//===== Additional Comments: ================================= 
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Updated "Welding Mask" Quest to the Official one. [Samuray22]
//= 1.3a replaced item "names" with item id. Added missing [Lupus]
//= 1.4 Updated welding mask and headset quests. [L0ne_W0lf]
//============================================================ 

// Blacksmith: 'Welding Mask' Quest
//============================================================
geffen_in,144,166,4	script	铁匠	63,{
	mes "[铁匠]";
	mes "你没有听说过我？";
	mes "为什么，我是这里的资深铁匠。";
	mes "我在这热与血腥的地狱之火奋斗了30年，";
	mes "我有无法弯曲的钢铁意志！";
	next;
	mes "[铁匠]";
	mes "但是...";
	mes "我的力量随着我年龄的老去而消失着。";
	mes "所以我请来了一些年轻的，热血的，";
	mes "上进的铁匠，希望他们掌握我的技术...";
	next;
	switch(select("这是什么？:...")) {
	case 1:
		mes "[铁匠]";
		mes "我们铁匠经常在极度高温的环境下打铁，这些炎热的气温和融化的钢铁不是一般人可以忍耐的。";
		next;
		mes "[铁匠]";
		mes "为了我们的艺术，我们的美丽，我们的脸部时常处于危险中...";
		next;
		mes "[铁匠]";
		mes "但随着一个非凡的技术诞生，这些问题再也没有需要担忧！";
		mes "看，这是就是我发明的^3355FF钢铁面罩^000000！！";
		next;
		mes "[铁匠]";
		mes "它由可以覆盖脸部的厚金属板构成。";
		mes "事实上，这是所有人都想要的道具！";
		mes "如果我不是自己要用，给一些正在学习打铁的朋友也不失为一个好主意。";
		next;
		mes "[铁匠]";
		mes "这样如何？如果你给我^2244FF50个钢铁^000000和^4422FF2000金币^000000，我会马上为你做一个~！！";
		next;
		switch(select("嗯...不错的样子，好吧:这是怎么工作的能再说一遍么？")) {
		case 1:
			if ((countitem(999) > 49) && (Zeny > 1999)){
				mes "[铁匠]";
				mes "啊，我发现你很欣赏铁匠的工作！ 很好，我马上做给你！！";
				next;
				delitem 999,50;  //Steel
				set Zeny,Zeny-2000;
				getitem 2292,1; // Welding_Mask
				mes "[铁匠]";
				mes "哈哈哈~！好好用它哦！";
				close;
			}
			else {
				mes "[铁匠]";
				mes "嗯...很抱歉~";
				mes "你没有我需要的道具。";
				next;
				mes "[铁匠]";
				mes "正常的铁匠会把这当成是不尊重的信号，但我并没有觉得你对铁匠有不敬的感觉。如果你还想我做面具的话就别忘了我需要的东西。";
				close;
			}
		case 2:
			mes "[铁匠]";
			mes "^3355FF钢铁面具^000000用覆盖你脸部的厚铁板来保护你的脸。本发明是我在铁锤和铁砧中无休止地工作30年的结果。";
			close;
		}
	case 2:
		mes "[铁匠]";
		mes "啊...!";
		mes "我相信你第一次遇到我这么好的铁匠，";
		mes "所以敬畏得说不出话。";
		mes "当你准备好的时候请找个时间告诉我。";
		next;
		mes "^3355FF这家伙似乎有点自满~^000000";
		close;
	}
}

// Eric: 'Headset' Quest
//============================================================
geffen_in,30,71,4	script	郑何意嘉	83,{
	mes "[郑何意嘉]";
	mes "你要不要听听我爷爷说的故事呢?";
	next;
	switch (select("继续对话:询问材料:制作看看:取消")) {
	case 1:
		mes "[郑何意嘉]";
		mes "前几年刚过世的爷爷";
		mes "在我的生命中是个相当重要的人物";
		mes "比起我那忙碌的父亲";
		mes "我可以感觉到爷爷依旧在我身边守候着我";
		next;
		mes "[郑何意嘉]";
		mes "爷爷的书房更是最令我感到有趣的地方";
		mes "就像是' 世外桃源 '一般";
		mes "每当我想念我爷爷的时候";
		mes "我就会到书房去找些书来看";
		next;
		mes "[郑何意嘉]";
		mes "在那里";
		mes "我可以透过书籍学习到许多的知识";
		mes "因为爷爷对于炼金术和音乐都相当的喜爱";
		mes "所以书房里头几乎满满的都是相关的书籍";
		next;
		mes "[郑何意嘉]";
		mes "直到有一天 ..";
		mes "我像往常一样进到爷爷的书房里";
		mes "在那里我偶然发现了一张相当神奇的设计图";
		next;
		mes "[郑何意嘉]";
		mes "在设计图中有一样东西，如果将它像耳罩一样戴上";
		mes "如果将它像耳罩一样戴，透过一条细线的牵引";
		mes "将可以听到像留声机中一样的声音";
		mes "这就是设计图的内容";
		next;
		mes "[郑何意嘉]";
		mes "我 ..一定要完成爷爷的遗愿";
		mes "做出这样东西来";
		mes "所以我到处去将这设计图给其他炼金者看";
		mes "请求他们帮忙，但是所有的人都说不知道这是什么东西";
		next;
		mes "[郑何意嘉]";
		mes "所以我才会想要亲自动手来做做看";
		mes "经过我多年的苦心研究..";
		mes "我已经可以完全理解那图上的结构了";
		mes "不过 .. 我还没能收集到所需要的材料";
		next;
		mes "[郑何意嘉]";
		mes "如果现在手边有材料的话";
		mes "我就可以马上依照设计图动手制作了";
		mes "如果 .. 你感兴趣的话";
		mes "是不是可以帮我找到所需要的材料呢?";
		next;
		mes "[郑何意嘉]";
		mes "我只是想要完成爷爷的遗愿";
		mes "做出设计图上的东西而已";
		mes "完成后如果你要的话，给你也没关系";
		close;
	case 2:
		mes "[郑何意嘉]";
		mes "想完成爷爷的遗愿，我需要这些东西：";
		mes "^3355FF钢铁^000000 ^FF555540^000000 片";
		mes "^3355FF神之金属^000000 ^FF55551^000000 个";
		mes "^3355FF酒精^000000 ^FF55551^000000 瓶";
		mes "^3355FF煤矿^000000 ^FF55551^000000 块";
		close;
	case 3:
		if ((countitem(999) > 39) && (countitem(984) > 0) && (countitem(970) > 0) && (countitem(1003) > 0)) {
			delitem 999,40; //Steel
			delitem 984,1; //Oridecon
			delitem 970,1; //Alchol
			delitem 1003,1; //Coal
			mes "滴答..滴答..滴答..";
			next;
			getitem 5001,1; //Headset
			mes "[郑何意嘉]";
			mes "真是太感谢你了";
			mes "托你的福我才能完成爷爷的遗愿";
			mes "如果爷爷能看到的话一定会很开心的";
			next;
			mes "[郑何意嘉]";
			mes "诚如我之前说过的，完成品请你带走吧！";
			mes "既然已经完成了，我就感到很高兴了";
			close;
		}
		else {
			mes "[郑何意嘉]";
			mes "谢谢你的好意 .. 但是材料似乎不足";
			mes "请你备齐了再来吧！";
			close;
		}
	case 4:
		mes "[郑何意嘉]";
		mes "不想听吗？";
		mes "好吧...或许你有你自己的想法~";
		mes "请慢走";
		close;
	}
}
