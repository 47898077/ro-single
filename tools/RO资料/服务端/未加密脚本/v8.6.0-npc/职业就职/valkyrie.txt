//===== rAthena Script ======================================= 
//= Valkyrie Realm
//===== By: ==================================================
//= Nana, fixes by Poki
//= finished, optimized and tested by Lupus
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 3.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Quest to become a Tranceded Class. (Advanced/Rebirthed)
//===== Additional Comments: ================================= 
//= 1.1 Made all into functions,additional checks,quest skills,now Valhallana can return you back. She also makes reborn process only when you got no Zeny/items (as in kRO) [Lupus]
//= 1.2 now alternative classes can use their 1-1 job NPC to become Advanced 1st Class [Lupus]
//= 1.4 added Baby Class support [Lupus]
//= 1.5 Fixed loads of gramatical errors. [Mass Zero]
//= 1.6 Added a func which prevent advanced classes passing 2nd Job Quests again. It also guides adv classes where to go. [Lupus]
//= 1.7 On reborn reset some Job Quest Vars and Reset Girls Counter [Lupus]
//= 1.8 Added Taekwondo classes check [Lupus]
//= 1.9 Added support for Job NPC Fase pics, sorrected one dialogue stuff [Lupus]
//= 2.0 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 2.1 Changed all numbers into constants. [Vicious]
//= 2.2 Now ALL quests skills are saved/restored [Lupus]
//= 2.3 Removed Duplicates [Silent]
//= 2.4 Fixed typos, optimized [Lupus]
//= 2.5 Fixed a stuck bug [Lupus]
//= 2.6a Adding proper dialogues [Lupus]
//= 2.7 Rescripted to Aegis 10.3 standards. [L0ne_W0;f]
//=     Removed all functions as they are no longer required.
//=     Correct job level requirements for 1-1 -> 2-x.
//= 2.7a Added Function "F_BlockHigh" back in until rewriteing of the 2-x [L0ne_W0lf]
//=     scripts are comepleted.
//= 2.7b Removed function "F_BlockHigh" as it's no longer used. [L0ne_W0lf]
//= 2.8 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.9 Corrected Valkyrie case spicifying where to warp after rebirthing. [L0ne_W0lf]
//= 2.9b Fixed Verdandi typo, fixed resetting vars [Lupus]
//= 3.0 Added quest log commands. [L0ne_W0lf]
//= 3.1 Added Izlude Renewal coordinates.
//============================================================ 

valkyrie,48,86,4	script	巴基力#	811,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[巴基力]";
		mes "欢迎";
		mes "来到瓦尔哈拉,";
		mes "荣耀的殿堂.";
		next;
		mes "[巴基力]";
		mes "请保持";
		mes "身体的放松";
		mes "当你来到这里.";
		mes "荣耀的勇士!";
		close;
	}
	else if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[巴基力]";
		mes "欢迎";
		mes "来到瓦尔哈拉,";
		mes "荣耀的殿堂.";
		next;
		mes "[巴基力]";
		mes "你将要结束现有的生命";
		mes "然后开始新的人生";
		mes "荣耀属于勇士们.";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() != 0 || checkfalcon() != 0 || checkriding() != 0) {
			mes "[巴基力]";
			mes "在开始之前,还有一些事情需要做.";
			mes "首先,必须清洗你的身体和灵魂.";
			mes "放弃所有的私欲.";
			mes "迎接荣耀的到来...";
			next;
			mes "[巴基力]";
			mes "在通往重生的道路上, 你不能携带任何东西. 你的物品, 金钱, 宠物, 坐骑必须全部丢弃.";
			next;
			mes "[巴基力]";
			mes "当你准备好了";
			mes "再回来找我,";
			mes "勇敢的冒险家.";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[巴基力]";
			mes "看上去你已经准备好了";
			mes "释放自己所有的心理与意念";
			mes ""+ strcharinfo(0) +".";
			next;
			mes "[巴基力]";
			mes "对于你这样的冒险家, 你的态度让人钦佩. 当你放弃所有个人的私欲时, 荣耀之日即会归来.";
			next;
			if (SkillPoint) {
				mes "[巴基力]";
				mes "嗯... 我感觉到";
				mes "对于目前的生活你有一些挥之不去的依恋,";
				mes "或有一些未竟的事业.";
				mes "你要知道的是,";
				mes "你现在的生命即将消失.";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[巴基力]";
			mes "现在, 让我把它们全部移除";
			mes "你现在的记忆...";
			mes "然而, ";
			mes "您将能够记住此生最光辉的时刻";
			next;
			mes "[巴基力]";
			mes "第一,";
			mes "我会请求";
			mes "女神抹去你所有的记忆.";
			next;
			mes "[巴基力]";
			mes "第二,";
			mes "我会请求";
			mes "维尔丹尼";
			mes "您将能够记住此生最光辉的时刻.";
			next;
			mes "[巴基力]";
			mes "第三,";
			mes "我会请求";
			mes "诗寇蒂";
			mes "为你重铸新生.";
			next;
			mes "[巴基力]";
			mes "1...";
			callfunc "F_ClearJobVar";
			next;
			mes "[巴基力]";
			mes "1...";
			mes "2......";
			next;
			mes "[巴基力]";
			mes "1...";
			mes "2......";
			mes "3.";
			set ADVJOB, Class+Job_Novice_High; //memo the target 3rd Job ID
			if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			set MISC_QUEST,MISC_QUEST | 1024; //<-reset Skill Reset Event
			skill 142,1,0;
			skill 143,1,0;
			completequest 1000;
			next;
			mes "[巴基力]";
			mes "祝贺你.";
			mes "你获得了新生";
			mes "进入一个全新的生活.";
			mes "请收下这些微薄的礼物";
			mes "准备开始您的新冒险.";
			getitem 1202,1; //Knife_
			getitem 2302,1; //Cotton_Shirt_
			next;
			mes "[巴基力]";
			mes "我期待女神能够给你赐福. 我希望维尔丹尼能够让你永远记得最荣耀的时刻.";
			next;
			mes "[巴基力]";
			mes "我祈祷诗寇蒂女神所重铸的新生命, 会比你的前生更显尊贵.";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				if (checkre(0))
					warp "izlude",129,97;
				else
					warp "izlude",94,103;
				break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[巴基力]";
		mes "欢迎";
		mes "来到瓦尔哈拉,";
		mes "荣耀的殿堂.";
		next;
		mes "[巴基力]";
		mes "非常不幸, 你未被邀请. 请尽快离开这里. 尊贵的勇士!";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	梅特赫斯 塞菲#Library	742,{
	if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 0) {
			mes "[梅特赫斯 塞菲]";
			mes "欢迎来到斯克维切比利魔法学院的图书馆.";
			mes "我知道你是来这里";
			mes "查阅'伊米尔之书'的.";
			next;
			mes "[梅特赫斯 塞菲]";
			mes "非常不幸, 由于年代久远这本书的原稿已经破旧不堪了. 我们现在只提供给公众副本来查阅.";
			next;
			mes "[梅特赫斯 塞菲]";
			mes "还有, 为了保护和修缮'伊米尔之书'的原稿, 我们决定向需要阅读副本的读者收取捐助费用.";
			next;
			mes "[梅特赫斯 塞菲]";
			mes "建议捐款金额是";
			mes "1,285,000 zeny.";
			next;
			if (select("- 捐赠.:- 取消.") == 1) {
				if (Zeny >= 1285000) {
					set zeny,zeny-1285000;
					set valkyrie_Q,1;
					mes "[梅特赫斯 塞菲]";
					mes "非常感谢, 您的捐赠将被用于慈善事业. ";
					mes "您可以继续阅读了.";
					close;
				}
				mes "[梅特赫斯 塞菲]";
				mes "非常不幸, 看上去您没有带够足够的钱. 请准备好了再来吧.";
				close;
			}
			mes "[梅特赫斯 塞菲]";
			mes "慢慢看, ";
			mes "享受这次行程吧.";
			close;
		}
		mes "[梅特赫斯 塞菲]";
		mes "再次感谢您慷慨的捐赠. 您闲暇时可以随时来取阅 '伊米尔之书' 的副本.";
		close;
	}
	mes "[梅特赫斯 塞菲]";
	mes "欢迎来到斯克维切比利魔法学院的图书馆.";
	mes "在这里, 我们有无数的典籍. 请花费些时间四处看看.";
	close;
}

yuno_in02,93,207,1	script	伊米尔之书	111,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[伊米尔之书]";
		mes "...荣誉殿堂的入口为所有憧憬重生的人开放. 这能够帮助那些英雄们做出自己的决定, 同时能够带领他们到达世界上的任何一个地方.";
		next;
		mes "[伊米尔之书]";
		mes "在荣耀殿堂, 为英雄们准备好了一切. 传闻现实生活中无法完成的心愿, 在荣耀殿堂中都可以实现.";
		next;
		if (select("- 停止阅读.:- 继续阅读.") == 1) {
			mes "[伊米尔之书]";
			mes ".....";
			close;
		}
		mes "[伊米尔之书]";
		mes "有一个被遗忘的路径, 连接到荣誉殿堂, 接近天堂的地方. 普通人永远不会发现这个地方...";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[伊米尔之书]";
			mes "...因此, 古代英雄总是在痛苦之中";
			mes "他们知道自己是凡人,";
			mes "最终无法逃过死亡的困扰.";
			next;
			mes "[伊米尔之书]";
			mes "这里没有文档,";
			mes "歌声, 或者传说 均描述了他们死后的一些信息 . 不过, 我最近发现了一个旧的卷轴.";
			mes "关于巴基力...";
			next;
			mes "[伊米尔之书]";
			mes "巴基力...";
			mes "传说中的";
			mes "守护天使.";
			mes "仙境传说中的天使.";
			next;
			mes "[伊米尔之书]";
			mes "拥有伟大力量的冒险家";
			mes "将在巴基力的领导下,";
			mes "到达瓦尔哈拉, 荣誉殿堂.";
			mes "在那里他们将被赐予新的生命.";
			next;
			mes "[伊米尔之书]";
			mes "转生, 他们会重生.";
			mes "那些伟大的英雄, 将光明带给世界.";
			mes "他们的肉体将被修补,";
			mes "使他们重新充满活力...";
			next;
			mes "[伊米尔之书]";
			mes "他们的灵魂将从伊米尔之心获得的能力. 然而, 伊米尔之心在卢恩米德加兹之战后被完全摧毁, 并散落在世界各地.";
			next;
			mes "[伊米尔之书]";
			mes "经过了很长很长一段时间，";
			mes "我已经找到了少量的伊米尔之心的碎片.";
			mes "但是我无法确认巴基力和瓦尔哈拉";
			mes "是不是真的存在.";
			next;
			mes "[伊米尔之书]";
			mes "因此, 我出发去寻找这个被人们寄托未来希望的地方.";
			mes "可以确认, 巴基力和瓦尔哈拉是实际存在的...";
			next;
			set valkyrie_Q,2;
			if(checkquest(1000) == -1) {
				setquest 1000;
			}
			mes "[伊米尔之书]";
			mes "让英雄们的生命获得重生";
			mes "因此他们能够保护这个世界";
			mes "远离灾难. 不过...";
			close;
		}
		mes "[伊米尔之书]";
		mes "...";
		close;
	}
	mes "[伊米尔之书]";
	mes "...";
	close;
}

yuno_in05,49,43,1	script	伊米尔之心	111,{
	if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	传送器	124,{
	if (Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[传送器]";
			mes "恭喜你.";
			mes "荣耀的勇士!";
			close;
		}
		mes "[传送器]";
		mes "请不要触摸";
		mes "任何物品";
		mes "..........";
		close;
	}
	else {
		mes "[传送器]";
		mes "荣耀一,";
		mes "你想去哪个地方?";
		next;
		switch(select("- 普隆德拉:- 梦罗克:- 斐扬:- 吉芬:- 阿尔贝塔:- 伊斯鲁德:- 阿尔帕兰:- 克魔岛:- 朱诺")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6:
			if (checkre(0))
				callsub S_Warpchar,"izlude",129,97;
			else
				callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
S_WarpChar:
	mes "[传送器]";
	mes "旅途愉快.";
	close2;
	savepoint getarg(0),getarg(1),getarg(2);
	warp getarg(0),getarg(1),getarg(2);
	end;
}

// Warps
//============================================================ 
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75

