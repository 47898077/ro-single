//===========================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ------------------------------------------
// - 菲音斯 导游 脚本 -
// ------------------------------------------
// 版本 1.1
//===========================================

veins,210,345,5	script	菲音斯 导游#1::ve_guide	934,{
	mes "[菲音斯 导游]";
	mes "欢迎冒险者来到峡谷村落 菲音斯";
	mes "第一次接受我们介绍的旅客们，先按选单";
	mes "'听取注意事项'，了解必须注意的事情";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("开始由导游说明城市:删掉小地图上全部的记号:听取注意事项:取消")) {
		case 1:
			mes "[菲音斯 导游]";
			mes "好的，现在就为你导游城市";
			if (.@compass_check == 0) {
				mes "要在小地图上留下记号吗?";
				next;
				if (select("好的:不要") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("瑟斯隆尼勒神殿:旅馆:武器店:道具商店:飞空艇:酒店:地理研究协会:取消")) {
				case 1:
					mes "[菲音斯 导游]";
					mes "我们的神殿位在北边";
					mes "平常有许多信奉菲依雅女神的信徒";
					mes "会在那里";
					if (.@compass_check == 1) viewpoint 1,196,258,1,0xFF0000;
					break;
				case 2:
					mes "[菲音斯 导游]";
					mes "你可以在旅馆里休息";
					mes "舒缓跋山涉水的疲惫";
					if (.@compass_check == 1) viewpoint 1,128,266,2,0xFF00FF;
					break;
				case 3:
					mes "[菲音斯 导游]";
					mes "是的，有时候你必须把自己从危险之中解救出来";
					mes "购买一把好武器绝对是必要的";
					mes "菲音斯的武器店位于西边";
					if (.@compass_check == 1) viewpoint 1,150,175,3,0x99FFFF;
					break;
				case 4:
					mes "[菲音斯 导游]";
					mes "你背包内的补给品还够用吗?";
					mes "如果有任何必须品需要购买的话，请到道具商店";
					if (.@compass_check == 1) viewpoint 1,230,161,4,0x0000FF;
					break;
				case 5:
					mes "[菲音斯 导游]";
					mes "请注意，";
					mes "菲音斯的飞空艇只用来载运货物";
					if (.@compass_check == 1) viewpoint 1,273,285,5,0x00FF00;
					break;
				case 6:
					mes "[菲音斯 导游]";
					mes "如果你想广交朋友，";
					mes "建议你到酒店里去看看";
					if (.@compass_check == 1) viewpoint 1,150,217,6,0x00FF00;
					break;
				case 7:
					mes "[菲音斯 导游]";
					mes "你对地理研究有兴趣吗?";
					mes "也许你可以过去看看，";
					mes "地理研究协会在武器店的二楼";
					if (.@compass_check == 1) viewpoint 1,150,175,7,0x00FF00;
					break;
				case 8:
					mes "[菲音斯 导游]";
					mes "需要删除记号请选择“删掉小地图上全部的记号”";
					set .@loop2,0;
					break;
				}
			}
			break;

		case 2:
			viewpoint 2,196,258,1,0xFF0000;
			viewpoint 2,128,266,2,0xFF00FF;
			viewpoint 2,150,175,3,0x99FFFF;
			viewpoint 2,230,161,4,0x0000FF;
			viewpoint 2,273,285,5,0x00FF00;
			viewpoint 2,150,217,6,0x00FF00;
			viewpoint 2,150,175,7,0x00FF00;
			mes "[菲音斯 导游]";
			mes "已删掉小地图上所有标志了";
			mes "如果想要重新在地图上做标志的话，";
			mes "请随时来找我吧";
			break;
		case 3:
			mes "[菲音斯 导游]";
			mes "我们废除了原来较差的指引方式，采用最现代化的";
			mes "无论您在哪里都可以籍由小地图知道您的位置";
			next;
			mes "[菲音斯 导游]";
			mes "在浏览之前，必须先参考右上方的小地图";
			mes "如果看不见小地图的话，请按“Ctrl + Tab”或者选取基本情报视窗内的“map”";
			mes "微型地图出现之后，在地图上按“-”几下可以看见所有城市";
			break;
		case 4:
			mes "[菲音斯 导游]";
			mes "那么，祝你有个愉快的旅行";
			close2;
			set .@loop1,0;
			break;
		}
	}
	close;
}

veins,189,101,5	duplicate(ve_guide)	菲音斯 导游#2	934