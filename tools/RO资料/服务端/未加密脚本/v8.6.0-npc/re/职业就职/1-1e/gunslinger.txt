// ---------------------------------
// - 神枪手 转职脚本 - 
//===================================
// 版本：1.8 部分中文化 by 黑色小猫
//===================================

que_ng.gat,152,167,3	script	米勒 大师	901,{

	if (Class == Job_Novice) {
		if (GUNS_Q >= 1) {
			mes "[米勒 大师]";
			mes "喔~ 你回来了?";
			next;
			if (GUNS_Q == 10005) {
				if (SkillPoint > 0) {
					mes "[米勒 大师]";
					mes "嗯~ 很抱歉，你仍有技能点数尚未用完! 你需要先将点数用完才能转职成为枪手!";
					close;
				} else if (JobLevel < 10) {
					mes "[米勒 大师]";
					mes "嗯~ 很抱歉，你的职业等级不足! 你需要升级职业等级至10级才能转职!";
					close;
				}
				mes "[米勒 大师]";
				mes "我很高兴知道你完成了聪明的黄牛角的任务，让我确定你配的上成为一名...";
				next;
				mes "[米勒 大师]";
				mes "枪手!!";
				callfunc "Job_Change",Job_Gunslinger;
				callfunc "F_ClearJobVar";
				next;
				mes "[米勒 大师]";
				mes "喔! 差点忘了，因为我很欣赏你，所以这送给你！";
				if (rand(1,3) < 3) {
					getitem 13100,1;
				} else {
					getitem 13150,1;
				}
				close;
			}
			mes "[米勒 大师]";
			mes "但是你还没完成我告诉你的任务! 需要我在告诉你一次吗?";
			switch (select ("是的，请再告诉我一次!:不，谢了!")) {
				case 1:
					next;
					mes "[米勒 大师]";
					mes "你要先去斐扬，在南方出口的城墙边找到聪明的黄牛角，他会委托你一些事情! 去吧!";
					close;
				case 2:
					next;
					mes "[米勒 大师]";
					mes "快去吧! 你想要成为一名枪手的，不是吗?";
					close;
			}
		}
		mes "[米勒 大师]";
		mes "嗨! ^0000CC"+strcharinfo(0)+"^000000，我要你帮我完成一件事，你愿意吗?";
		switch (select ("好的!:不要!")) {
			case 1:
				next;
				mes "[米勒 大师]";
				mes "很好，我要你去斐扬为聪明的黄牛角做一件事!";
				next;
				mes "[米勒 大师]";
				mes "他将会在南方出口的城墙边等你，到时见!";
				set GUNS_Q,1;
				close;
			case 2:
				next;
				mes "[米勒 大师]";
				mes "这真是太糟糕了! 等你准备好再回来吧!";
				close;
		} 

	}
	mes "[米勒 大师]";
	mes "嗨，小子! 最近过的好吗?";
	close;
}

payon.gat,184,65,3	script	聪明的 黄牛角	866,{

	if (GUNS_Q == 1) {
		mes "[黄牛角]";
		mes "喔! 你是 ^0000CC"+strcharinfo(0)+"^000000，是吗? 艾音布罗克的米勒告诉我你会来帮我收集一些东西!";
		next;
		mes "[黄牛角]";
		mes "我要煮一份特制的汤给我妻子，但是就如你所见，我没有时间去收集材料，因为我的妻子生病了!";
		next;
		mes "[黄牛角]";
		mes "所以，可以请你帮忙吗? 拜托你?";
		switch (select ("好的，可以!:抱歉! 现在不行!")) {
			case 1:
				set GUNS_Q,callfunc("F_RandMes",4,1019,1066,1067,1068);
				goto L_LIST;

			case 2:
				next;
				mes "[黄牛角]";
				mes "喔~ 我该怎么办才好!";
				close;
		}

	} else if (GUNS_Q > 1 && GUNS_Q < 10000) {
		mes "[黄牛角]";
		mes "喔! 你回来了! 你找到所有材料了吗?";
		next;
		switch (select("是的，我找到了!:我忘记要找的东西了!")) {
			case 1:
				next;
				if (countitem(949) < 3) {
					mes "[黄牛角]";
					mes "抱歉，你缺少3个柔毛!";
					close;
				} else if (countitem(912) < 3) {
					mes "[黄牛角]";
					mes "抱歉，你缺少3个刹勒空结晶!";
					close;
				} else if (countitem(1013) < 3) {
					mes "[黄牛角]";
					mes "抱歉，你缺少3个彩色昆虫外壳!";
					close;
				} else if (countitem(GUNS_Q) < 1) {
					mes "[黄牛角]";
					mes "抱歉，你没有智慧树枝!";
					close;
				} else if (countitem(511) < 3) {
					mes "[黄牛角]";
					mes "抱歉，你缺少3个绿色药草!";
					close;
				} else if (countitem(935) < 10) {
					mes "[黄牛角]";
					mes "抱歉，你缺少10个坚硬外壳!";
					close;
				}
				mes "[黄牛角]";
				mes "太好了! 你找到全部的材料了! 真是太感谢你了!";
				delitem 949,3;
				delitem 912,3;
				delitem 1013,3;
				delitem GUNS_Q,1; //here we keep Trunk ID
				delitem 935,10;
				delitem 511,3;
				next;
				mes "[黄牛角]";
				mes "喔! 你真是帮了个大忙! 我将会向艾音布罗克的米勒推荐你的就职申请!";
				set GUNS_Q,10003;
				close;
			case 2:
			L_LIST:
				next;
				mes "[黄牛角]";
				mes "好的，我将会再告诉你一次! 请记住!";
				mes "^33CCFF 3个柔毛^000000";
				mes "^009933 3个刹勒空结晶^000000";
				mes "^660066 3个彩色昆虫外壳^000000";
				mes "^009900 3个绿色药草^000000";
				mes "^663300 1 "+getitemname(GUNS_Q)+"^000000";
				mes "^FF6600 10个坚硬外壳^000000";
				next;
				mes "[黄牛角]";
				mes "请快点! 我不知道我妻子还能活多久，喔~ 亲爱的...";
				close;
		}
	} else if (GUNS_Q == 10003 || GUNS_Q == 10004) {
		if (countitem(519) >= 1 && GUNS_Q == 10004) {
			mes "[黄牛角]";
			mes "就是它! 我忘记牛奶了! 太好了，非常谢谢你! 你的牛奶可以给我吗?";
			switch(select("当然! 拿去吧!:抱歉，不行!")) {
				case 1:
					next;
					mes "[黄牛角]";
					mes "太谢谢你了! 去找米勒吧! 他会答应你的就职! 再次谢谢你! 再见!";
					delitem 519,1;
					set GUNS_Q,10005;
					close;
				case 2:
					next;
					mes "[黄牛角]";
					mes "喔，拜托请你给我! 我真的非常需要它! 拜托!";
					close;
			}
		}
		mes "[黄牛角]";
		mes "我还是要继续等，我相信一定少了什么东西，感谢你的帮忙!";
		set GUNS_Q,10004;
		close;
	} else if (Class == Job_Gunslinger || GUNS_Q == 10005) {
		mes "[黄牛角]";
		mes "嗨! ^0000CC"+strcharinfo(0)+"^000000! 你最近好吗? 我妻子要我谢谢你的关心!";
		close;
	} 
	mes "[黄牛角]";
	mes "嗨! 你最近好吗? 一定过的很好吧! 是吧!";
	close;
}