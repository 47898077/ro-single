// ==============================================================================
// 战场系统 - 霍厄斯战场 1
// ==============================================================================
//翻译组：乐土工作室 http://promised-land.cn
//翻译员：『PLTeam』Soul
//翻译进度：100%
//脚本美化：『PLTeam』Soul
//===============================================================================

// 登记处NPC事项
// *********************************************************************

bat_room,253,97,4	script	霍厄斯战场-01登记处::Fl1R_Guillaume	418,{
	end;
OnInit:
	waitingroom "战区 10 玩家",10,"Flavius_BG1::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id1, waitingroom2bg("bat_b01",390,10,"Flavius_BG1::OnGuillaumeQuit","");
	end;
}

bat_room,253,74,0	script	霍厄斯战场-01::Fl1R_Croix	414,{
	end;
OnInit:
	waitingroom "战区 10 玩家",10,"Flavius_BG1::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id2, waitingroom2bg("bat_b01",10,290,"Flavius_BG1::OnCroixQuit","");
	end;
}

// 战场启动
// *********************************************************************

-	script	Flavius_BG1	-1,{
	end;

OnInit:
	disablenpc "吉永军司令官#fl1";
	disablenpc "克勒亚军司令官#fl1";
	disablenpc "战场治疗师#fl11";
	disablenpc "战场治疗师#fl12";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@FlaviusBG1 == 0 )
		donpcevent "Flavius_BG1::OnReadyCheck";
	end;

OnReadyCheck:
	if( $@FlaviusBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Fl1R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"Fl1R_Croix");
	
	if( .@Guillaume < 10 || .@Croix < 10 )
	{
		mapannounce "bat_room","霍厄斯战场-01 [80-99] G: " + .@Guillaume + "/10, C: " + .@Croix + "/10",1,0x00FF00;
		end;
	}

	// BG 变量
	set $@FlaviusBG1, 1;
	set $@FlaviusBG1_Victory, 0;
	set .Guillaume_Score, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	// NPC 准备
	donpcevent "#gfl1_respawn::OnBGStart";
	donpcevent "#cfl1_respawn::OnBGStart";
	enablenpc "战场治疗师#fl11";
	enablenpc "战场治疗师#fl12";
	disablenpc "吉永军司令官#fl1";
	disablenpc "克勒亚军司令官#fl1";
	// 建筑及传送点
	donpcevent "Fl1R_Guillaume::OnEnterBG";
	donpcevent "Fl1R_Croix::OnEnterBG";
	announce "霍厄斯战场-01 [80-99] 已经开始!",0,0x00FF00;
	initnpctimer;
	// 比赛开始!!

OnRoundStart:
	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	areapercentheal "bat_b01",382,2,397,17,100,100;
	areapercentheal "bat_b01",2,282,17,297,100,100;
	bg_warp $@FlaviusBG1_id1,"bat_b01",311,224;
	bg_warp $@FlaviusBG1_id2,"bat_b01",87,75;

	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	set .Match, .Match + 1;
	// 水晶生成
	set .Guillaume_Crystal, bg_monster($@FlaviusBG1_id1,"bat_b01",328,150,"吉永军水晶",1914,"Flavius_BG1::OnGuillaumeBreak");
	setwall "bat_b01",327,149,2,6,0,"gui_wall1-1";
	setwall "bat_b01",329,149,2,0,0,"gui_wall1-2";
	setwall "bat_b01",329,151,2,2,0,"gui_wall1-3";
	setwall "bat_b01",327,151,2,4,0,"gui_wall1-4";

	set .Croix_Crystal, bg_monster($@FlaviusBG1_id2,"bat_b01",62,150,"克勒亚军水晶",1915,"Flavius_BG1::OnCroixBreak");
	setwall "bat_b01",61,149,2,6,0,"cro_wall1-1";
	setwall "bat_b01",63,149,2,0,0,"cro_wall1-2";
	setwall "bat_b01",63,151,2,2,0,"cro_wall1-3";
	setwall "bat_b01",61,151,2,4,0,"cro_wall1-4";

	// 监护人生成
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,160,"吉永军 守护者",1949,"Flavius_BG1::OnGuiGuardian";
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,140,"吉永军 守护者",1950,"Flavius_BG1::OnGuiGuardian";
	set .Guillaume_Guardian, 2;
	bg_monster $@FlaviusBG1_id2,"bat_b01",62,160,"克勒亚军 守护者",1949,"Flavius_BG1::OnCroGuardian";
	bg_monster $@FlaviusBG1_id2,"bat_b01",61,140,"克勒亚军 守护者",1950,"Flavius_BG1::OnCroGuardian";
	set .Croix_Guardian, 2;
	// 公告
	mapannounce "bat_b01"," 霍厄斯战场-01 [ " + .Match + " ] 局已经开始!!",1,0x00FF00;
	end;

OnRoundStop:
	// 移除魔物
	killmonster "bat_b01","Flavius_BG1::OnGuiGuardian";
	killmonster "bat_b01","Flavius_BG1::OnCroGuardian";
	delwall "gui_wall1-1";
	delwall "gui_wall1-2";
	delwall "gui_wall1-3";
	delwall "gui_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnGuillaumeBreak";
	delwall "cro_wall1-1";
	delwall "cro_wall1-2";
	delwall "cro_wall1-3";
	delwall "cro_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnCroixBreak";
	end;

OnGuiGuardian:
	if( set(.Guillaume_Guardian, .Guillaume_Guardian - 1) <= 0 )
	{
		delwall "gui_wall1-1";
		delwall "gui_wall1-2";
		delwall "gui_wall1-3";
		delwall "gui_wall1-4";
		mapannounce "bat_b01","吉永军的水晶失去保护!",1,0x0000FF;
	}
	end;

OnCroGuardian:
	if( set(.Croix_Guardian, .Croix_Guardian - 1) <= 0 )
	{
		delwall "cro_wall1-1";
		delwall "cro_wall1-2";
		delwall "cro_wall1-3";
		delwall "cro_wall1-4";
		mapannounce "bat_b01","克勒亚军的水晶失去保护!",1,0xFF0000;
	}
	end;

OnGuillaumeBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Loss, .Guillaume_Loss + 1;
	if( set(.Croix_Score, .Croix_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","吉永军的水晶被摧毁!",1,0x0000FF;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","克勒亚军在 霍厄斯战场-01 战斗中获胜!",1,0xFF0000;
		set $@FlaviusBG1_Victory, 2;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnCroixBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Croix_Loss, .Croix_Loss + 1;
	if( set(.Guillaume_Score, .Guillaume_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","克勒亚军的水晶被摧毁!",1,0xFF0000;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","吉永军在 霍厄斯战场-01 战斗中获胜!",1,0x0000FF;
		set $@FlaviusBG1_Victory, 1;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnTimer2400000:
	mapannounce "bat_b01"," 霍厄斯战场-01 战斗将在5分钟后结束",1,0x00FF00;
	end;
OnTimer2640000:
	mapannounce "bat_b01"," 霍厄斯战场-01 战斗将在1分钟后结束",1,0x00FF00;
	end;

OnTimer2700000:
	if( .Croix_Score > .Guillaume_Score )
	{
		mapannounce "bat_b01","克勒亚军在 霍厄斯战场-01 战斗中获胜!",1,0xFF0000;
		set $@FlaviusBG1_Victory, 2;
	}
	else if( .Croix_Score < .Guillaume_Score )
	{
		mapannounce "bat_b01","吉永军在 霍厄斯战场-01 战斗中获胜!",1,0x0000FF;
		set $@FlaviusBG1_Victory, 1;
	}
	else
	{
		mapannounce "bat_b01"," 霍厄斯战场-01 战斗已经结束. 这场战斗宣告平局",1,0x00FF00;
		set $@FlaviusBG1_Victory, 3;
	}

OnMatchEnd:
	set $@FlaviusBG1, 2;
	stopnpctimer;
	donpcevent "#gfl1_respawn::OnBGStop";
	donpcevent "#cfl1_respawn::OnBGStop";
	disablenpc "战场治疗师#fl11";
	disablenpc "战场治疗师#fl12";
	enablenpc "吉永军司令官#fl1";
	enablenpc "克勒亚军司令官#fl1";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",390,10;
	bg_warp $@FlaviusBG1_id2,"bat_b01",10,290;
	sleep 3000;
	mapannounce "bat_b01"," 霍厄斯战场-01 将在1分钟后关闭!",1,0x00FF00;
	initnpctimer;
	end;

OnTimer30000:
	if( $@FlaviusBG1 == 2 )
		mapannounce "bat_b01"," 霍厄斯战场-01 将在30秒后关闭!",1,0x00FF00;
	end;
OnTimer50000:
	if( $@FlaviusBG1 == 2 )
		mapannounce "bat_b01"," 霍厄斯战场-01 将在10秒后关闭!",1,0x00FF00;
	end;

OnTimer60000:
	if( $@FlaviusBG1 != 2 )
		end;
OnReset:
	stopnpctimer;
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Score, 0;
	set .Guillaume_Crystal, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Crystal, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	set $@FlaviusBG1_Victory, 0;
	// NPC事项
	disablenpc "吉永军司令官#fl1";
	disablenpc "克勒亚军司令官#fl1";
	disablenpc "战场治疗师#fl11";
	disablenpc "战场治疗师#fl12";

	if( $@FlaviusBG1_id1 ) { bg_destroy $@FlaviusBG1_id1; set $@FlaviusBG1_id1, 0; }
	if( $@FlaviusBG1_id2 ) { bg_destroy $@FlaviusBG1_id2; set $@FlaviusBG1_id2, 0; }
	sleep 1000;
	mapwarp "bat_b01","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_b01",0,3; // Just in case someone else
	sleep 2000;
	bg_updatescore "bat_b01",0,0;
	set $@FlaviusBG1, 0;
	donpcevent "Flavius_BG1::OnReadyCheck";
	end;
}

// 战场奖励
// *********************************************************************

bat_b01,389,13,5	script	吉永军司令官#fl1	419,{
	if( $@FlaviusBG1_Victory )
	{
		if( $@FlaviusBG1_Victory == Bat_Team )
		{ // 胜利者
			mes "[阿历斯 罗斯]";
			mes "神圣的吉永军!!";
			mes "让我们享受胜利的荣耀吧!";
			mes "" + strcharinfo(0) + ", 恭喜您成为胜利者";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 失败者
			mes "[阿历斯 罗斯]";
			mes "你虽然失败了，但你已经在这次战斗中尽力了.";
			mes "这是吉永 马乐罗对你奋战的奖励!";
			mes "把这次的失败当作教训吧.";
			close2;
			set .@Reward, 3;
		}

		setquest 2070;
		getitem 7829, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_b01,9,293,5	script	克勒亚军司令官#fl1	415,{
	if( $@FlaviusBG1_Victory )
	{
		if( $@FlaviusBG1_Victory == Bat_Team )
		{ // 胜利者
			mes "[斯曼德理]";
			mes "神圣的克勒亚军!!";
			mes "让我们享受胜利的荣耀吧!";
			mes "" + strcharinfo(0) + ", 恭喜您成为胜利者";
			close2;
			set .@Reward, 9;
		}
		else
		{ // 失败者
			mes "[斯曼德理]";
			mes "噢, " + strcharinfo(0) + ". 不要太难过.";
			mes "虽然我们没有战胜，但是已尽我们所能了.";
			mes "这是克勒亚军赠予的皇家礼物，请别忘记这次战争，我们会在下次赢回来的.";
			close2;
			set .@Reward, 3;
		}

		setquest 2070;
		getitem 7829, .@Reward;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// 战场治疗师
// *********************************************************************

bat_b01,391,13,5	script	战场治疗师#fl12	95,{
	mes "[战场治疗师]";
	mes "只需要闭上眼，然后深呼吸.";
	mes "这样可以减轻痛楚.";
	specialeffect2 312;
	close;
}

bat_b01,11,293,5	script	战场治疗师#fl11	95,{
	mes "[战场治疗师]";
	mes "只需要闭上眼，然后深呼吸.";
	mes "这样可以减轻痛楚.";
	specialeffect2 312;
	close;
}

// 战场重生
// *********************************************************************

bat_b01,390,10,0	script	#gfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",382,2,397,17,100,100;
	areawarp "bat_b01",382,2,397,17,"bat_b01",311,224;
	initnpctimer;
	end;
}

bat_b01,10,290,0	script	#cfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",2,282,17,297,100,100;
	areawarp "bat_b01",2,282,17,297,"bat_b01",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bat_b01,304,231,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat1	973
bat_b01,319,231,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat2	973
bat_b01,304,218,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat3	973
bat_b01,319,218,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat4	973
bat_b01,304,231,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat5	973
bat_b01,304,231,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat6	973
bat_b01,335,142,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat7	973
bat_b01,335,157,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat8	973
bat_b01,390,16,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat9	973
bat_b01,292,163,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat19	973
bat_b01,292,136,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat20	973
bat_b01,241,185,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat21	973
bat_b01,247,179,1	duplicate(Guillaume camp#bat)	吉永军阵营#bat22	973

bat_b01,96,81,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat1	974
bat_b01,96,68,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat2	974
bat_b01,79,81,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat3	974
bat_b01,79,68,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat4	974
bat_b01,96,81,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat5	974
bat_b01,96,81,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat6	974
bat_b01,59,164,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat7	974
bat_b01,59,137,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat8	974
bat_b01,10,296,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat9	974
bat_b01,110,162,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat18	974
bat_b01,110,137,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat19	974
bat_b01,152,120,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat20	974
bat_b01,158,114,1	duplicate(Croix camp#bat)	克勒亚军阵营#bat21	974

// MapFlags
// *********************************************************************

bat_b01	mapflag	battleground	2
bat_b01	mapflag	nomemo
bat_b01	mapflag	nosave	SavePoint
bat_b01	mapflag	noteleport
bat_b01	mapflag	nowarp
bat_b01	mapflag	nowarpto
bat_b01	mapflag	noreturn
bat_b01	mapflag	nobranch
bat_b01	mapflag	nopenalty
