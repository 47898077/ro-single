
// ---------------------------------
// - 服事任务技能 脚本 - 
//===================================
// 版本: 1.5
//===================================

prt_church,173,23,4	script	主任 服事#qsk_al	79,{
	mes "[服事 克利福特]";
	mes "啊啊，兄弟姐妹们! 想要拯救";
	mes "可怜的小绵羊";
	mes "是否觉得心有余力呢?";
	mes "我想伴随你，为你服务";
	next;
	switch(select("有关^3355FF'服事隐藏的能力'^000000 ..:移交^3355FF' 神圣之光 '^000000:结束对话")) {
	case 1:
		mes "[服事 克利福特]";
		mes "像我们这样的圣职者";
		mes "要独自发现自己的";
		mes "隐藏能力之外，";
		mes "必须要透过资深圣职者们";
		mes "移交后所得到的技能";
		mes "才能彻底的发挥使用";
		next;
		mes "[服事 克利福特]";
		mes "为了防止邪恶的势力逃脱";
		mes "可以拯救他们";
		mes "无限期的痛苦";
		mes "可以使用有一个叫做^3355FF神圣之光^000000 技能";
		mes "想把这技能移交给你的话";
		mes "要使用特别的力量";
		next;
		mes "[服事 克利福特]";
		mes "所需要几个媒介物是";
		mes "^FF3355白宝石1个^000000";
		mes "^FF3355水灵矿石1个^000000";
		mes "^FF3355幸运珠链1个^000000";
		mes "等等";
		next;
		mes "[服事 克利福特]";
			mes "但是为了移交这能力时";
			mes "需要某种程度的天份才可以";
			mes "^FF3355职业等级 30 以上^000000";
			mes "才能做移交的动作";
		next;
		mes "[服事 克利福特]";
		mes "以祭司来说";
		mes "已经是认定有能力的职位";
		mes "所以不受职业等级的影响";
		mes "可以直接'移交'传授";
		close;
	case 2:
		if (getskilllv(156) == 1) {
			mes "[服事 克利福特]";
			mes "亲爱的兄弟姐妹，你已经成功移交";
			mes "'神圣之光了'";
			mes "请好好使用这个技能吧";
			close;
		}
		else {
			if (countitem(727) > 0 && countitem(991) > 0 && countitem(2608) > 0) {
				if ((JobLevel > 29 && BaseJob == Job_Acolyte) || BaseJob == Job_Monk || BaseJob == Job_Priest) {
					mes "[服事 克利福特]";
					mes "对信任感，抱着忠诚态度的你";
					mes "给你有使用神圣之光";
					mes "的资格";
					mes "对你以后即将要走的路";
					mes "将与幸福同在 . . . . .";
					next;
					delitem 727,1; //白宝石
					delitem 991,1; //水灵矿石
					delitem 2608,1; //幸运珠链
					skill 156,1,0;
					mes "[服事 克利福特]";
					mes "来 ，以后你就";
					mes "尽情的使用神圣之光吧 ，希望你把这能力";
					mes "用在善行吧. . . . .";
					close;
				}
			}
			mes "[服事 克利福特]";
			mes "噢噢，兄弟姐妹们";
			mes "你还不够资格移交";
			mes "神圣之光啊..";
			next;
			mes "[服事 克利福特]";
			mes "请仔细听我说";
			mes "有关服事隐藏能力";
			mes "的说明";
			mes "你就会了解，自己不足的地方";
			mes "该怎么去补充了";
			close;
		}
	case 3:
		mes "[服事 克利福特]";
		mes ". . . . .";
		mes "我了解你的意思了";
		mes "请你慢慢的思考之后";
		mes "好好的实践吧";
		mes "祝你前途无量啊 . . . . .";
		close;
	}
}
