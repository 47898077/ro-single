//===========================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -----------------------------------------
// - 药钵 购买 - 改写自JA
//===========================================

alde_alche,24,188,3	script	组合 贩卖员	740,{
	if (checkweight(1201,1) == 0) {
		mes "- 请等等! -";
		mes "- 你身上携带了太多物品了 -";
		mes "- 先把东西多余的东西 -";
		mes "- 寄放在卡普拉仓库之后 -";
		mes "- 再回来吧! -";
		close;
	}
	mes "[葛伯 纱普]";
	mes "欢迎来到炼金术师组织，";
	mes "需要什么服务吗?";
	next;
	switch (select("购买制药物品:购买制作说明书:取消")) {
	case 1:
		mes "[葛伯 纱普]";
		mes "请问需要什么物品";
		next;
		if(select("药钵 - 8z:停止交易")==2)
			break;
		mes "[葛伯 纱普]";
		mes "请问需要几个?";
		mes "如想要停止交易请写 '0' ";
		while(1) {
			next;
			input .@num;
			if(.@num <= 0) {
				mes "[葛伯 纱普]";
				mes "那，下次再说吧";
				close;
			}
			if(.@num > 2000) {
				mes "[葛伯 纱普]";
				mes "请选于2000个以下";
				continue;
			}
			break;
		}
		if(Zeny < .@num*8) {
			mes "[葛伯 纱普]";
			mes "金额不足噢";
			mes "请确认一下所携带的金额";
			close;
		}
		if(checkweight(7134,.@num)==0) {
			mes "看样子都不能带走啊，";
			mes "请确认一下所携带的物品量";
			close;
		}
		set Zeny,Zeny-.@num*8;
		getitem 7134,.@num;
		break;
	case 2:
		mes "[葛伯 纱普]";
		mes "请问需要哪些物品?";
		mes "一般的制作说明书是100,000金币，";
		mes "比较特殊的几种制作说明书除外";
		next;
		switch (select("一般药水制作说明书:酒精制作说明书:火烟瓶制作说明书:盐酸制作说明书",
				"植物瓶制作说明书:刺锤瓶制作说明书:护贝药制作说明书:纤细药水制作说明书:取消交易")) {
			default:
				set .@price,100000;
				break;
			case 8:
				set .@price,240000;
				break;
			case 9:
				mes "[葛伯 纱普]";
				mes "那，下次再说吧";
				close;
		}
		if(Zeny < .@price) {
			mes "[葛伯 纱普]";
			mes "金额不足噢";
			mes "请确认一下所携带的金额";
			close;
		}
		set Zeny,Zeny-.@price;
		setarray .@itemid,7144,7127,7128,7129,7130,7131,7132,7133;
		getitem .@itemid[@menu-1],1;
		mes "[葛伯 纱普]";
		mes "感谢你的购买，";
		mes "愿你做出实用的药";
		close;
	case 3:
		break;
	}
	mes "[葛伯 纱普]";
	mes "那，下次再说吧";
	close;
}

alde_alche,31,186,2	script	制造书销售员#Mechanic	883,{
	if (checkweight(1201,1) == 0) {
		mes "等一下!";
		mes "当前你带了太多东西了!";
		close;
	}
	mes "[制造书销售员]";
	mes "你好, 我这里出售基因学者使用";
	mes "的^0000FF特殊制造书^000000.";
	next;
	mes "[制造书销售员]";
	mes "基因学者必须拥有制造书, 才能";
	mes "发动技能.";
	next;
	mes "[制造书销售员]";
	mes "拥有的书的种类越多, 可以使用";
	mes "的技能种类当然就越多了.";
	next;
	mes "[制造书销售员]";
	mes "由于这里出售的是特殊的书, 因";
	mes "此^FF0000不卖其它物品^000000.";
	mes "敬请谅解.";
	next;
	mes "[制造书销售员]";
	mes "你想看看制造书吗?";
	next;
	if(select("是的:不") == 2) {
		mes "[制造书销售员]";
		mes "欢迎下次光临. 再见.";
		close;
	}
	mes "[制造书销售员]";
	mes "好的, 我把制造书清单给你看看.";
	while(1) {
		next;
		switch(select("苹果炸弹制造书:菠萝炸弹制造书:椰果炸弹制造书:甜瓜炸弹制造书:香蕉炸弹制造书:植物基因栽培法:高级药水制造手册:组合烹饪料理书:体力爆发研究书:活力药水制造法:结束购买.")) {
		case 1:
			callsub S_SellManual,6279,100000; // 苹果炸弹制造书
			break;
		case 2:
			callsub S_SellManual,6280,100000; // 菠萝炸弹制造书
			break;
		case 3:
			callsub S_SellManual,6281,100000; // 椰果炸弹制造书
			break;
		case 4:
			callsub S_SellManual,6282,100000; // 甜瓜炸弹制造书
			break;
		case 5:
			callsub S_SellManual,6283,100000; // 香蕉炸弹制造书
			break;
		case 6:
			callsub S_SellManual,6284,100000; // 植物基因栽培法
			break;
		case 7:
			callsub S_SellManual,6285,100000; // 高级药水制造手册
			break;
		case 8:
			callsub S_SellManual,11022,100000; // 组合烹饪料理书
			break;
		case 9:
			callsub S_SellManual,11023,100000; // 体力爆发研究书
			break;
		case 10:
			callsub S_SellManual,11024,100000; // 活力药水制造法
			break;
		case 11:
			mes "[制造书销售员]";
			mes "欢迎下次光临. 谢谢, 再见.";
			close;
		}
		close;

S_SellManual:
		mes "[制造书销售员]";
		mes "^0000FF["+getitemname(getarg(0))+"]^000000的价格是"+getarg(1)+"z.";
		mes "请告诉我您想买几本.";
		next;
		input .@input,0,2001;
		if (.@input < 1 || .@input > 2000) {
			mes "[制造书销售员]";
			mes "想看看其它书吗?";
			continue;
		}
		set .@price,.@input*getarg(1);
		mes "[制造书销售员]";
		mes "购买"+.@input+"^0000FF["+getitemname(getarg(0))+"]^000000.";
		mes "是否继续?";
		next;
		if(select("是的:不") == 2) {
			mes "[制造书销售员]";
			mes "购买制造书的时候, 应该要慎重一些.";
			close;
		}
		if (Zeny < .@price) {
			mes "[制造书销售员]";
			mes "不好意思, 你的钱好像不够.";
			close;
		}
		set zeny,zeny-.@price;
		getitem getarg(0),.@input;
		mes "[制造书销售员]";
		mes "谢谢您的支持.";
		mes "欢迎下次再来.";
		close;
	}
}
