
// -----------------------------------------
// - 染料制作 脚本 -
//========================
//= 版本：1.3c
//========================

prt_in,223,42,4	script	染色商人	58,{

	mes "[染色商人]";
	mes "阿...";
	mes "有强烈的太阳这种天气作这种天气作做染料最好不过了.";
	next;
	switch(select("对话:做染料:取消")) {
	case 1:
		mes "[染色商人]";
		mes "我对您没什么话讲...";
		mes "不过您想听我的过去的话我可以讲给您听.";
		mes "可能有一点闷 您要听吗?";
		next;
		if (select("听:不听") == 1) {
			mes "[染色商人]";
			mes "我出生懂事之前我爸爸是一直做染料.";
			mes "花很长的时间, 我爸爸会做染料.";
			mes "还有...";
			mes "我妈妈生病去世的时候我爸爸没停下来做染料的工作....";
			mes "有时候会讨厌有这样的爸爸...";
			next;
			mes "[染色商人]";
			mes "我不喜欢爸爸.. 自然不喜欢做染料的工作所以年轻的 时候我离家出走做别的事情.";
			mes "不过... 我现在已经回家继承家业. 做染料的工作... 可能骗不了自己身上的血统... 这份工作... 已经做了15年了.";
			next;
			mes "[染色商人]";
			mes "最近才开始了解我爸爸的感受....";
			mes "我爸爸为什么放弃一切投入所有的力量在做染料的工作...";
			next;
			mes "[染色商人]";
			mes "那是因为他想要做出梦幻之色....";
			next;
			mes "[染色商人]";
			mes "哎... 怎么知道?... 现在的我就是这样... 前几年当我可 以听见颜色在说话的时候, 我开始了解我爸爸的感受.";
			mes "等着瞧吧. 虽然我爸爸没有成功不过我一定会做出梦幻之色.";
			close;
		}
		mes "[染色商人]";
		mes "啊哈哈.. 说的也是..";
		mes "谁会听染料工的话. 哈哈哈....";
		close;

	case 2:
		mes "[染色商人]";
		mes "好吧! 如果您要的话. 我一定会做出您想要的颜色.";
		mes "不过您要什么颜色的染料? 颜色不一样价钱也不一样.";
		mes "您想知道为什么吗? 因为有的颜色容易做出来, 有的颜色不容易.";
		mes "千万不要觉得收费很贵. 这个染料里面有我的灵魂 .";
		next;
		switch(select("红色染料:黄色染料:蓝色染料:绿色染料:赤色染料:紫色染料:白色染料:黑色染料:取消")) {
		case 1: callsub S_MakeDye,1,1; break;
		case 2: callsub S_MakeDye,2,1; break;
		case 3: callsub S_MakeDye,3,2; break;
		case 4: callsub S_MakeDye,4,3; break;
		case 5: callsub S_MakeDye,5,4; break;
		case 6: callsub S_MakeDye,6,4; break;
		case 7: callsub S_MakeDye,7,4; break;
		case 8: callsub S_MakeDye,8,4; break;
		case 9:
			mes "[染色商人]";
			mes "这这这!! 这是什么?";
			mes "到现在才要改变主意 真是无奈...";
			close;
		}

	case 3:
		mes "[染色商人]";
		mes "这不是我吹牛 我有做染料药剂的技术.";
		mes "如果需要找我先告诉我 可以给您优惠.";
		close;
	}

S_MakeDye:
	mes "[染色商人]";
	switch(getarg(0)) {
	case 1:
		mes "嗯... 要做红色染料需要30个红色的药草还有中和剂和空瓶子.";
		mes "收费是3000金币.";
		mes "嗯! 您应该准备好了... 可以开始工作了吗?";
		setarray .@item[0], 507, 973, 713;
		setarray .@count[0], 30,   1,   1;
		set .@cost,3000;
		set .@dyestuff,975;
		break;
	case 2:
		mes "嗯... 要做黄色染料需要30个黄色的药草还有中和剂和 空瓶子.";
		mes "收费是3000金币.";
		mes "嗯! 您应该准备好了... 可以开始工作了吗?";
		setarray .@item[0], 508, 973, 713;
		setarray .@count[0], 30,   1,   1;
		set .@cost,3000;
		set .@dyestuff,976;
		break;
	case 3:
		mes "嗯... 要做蓝色染料需要20个蓝色的药草还有中和剂和空瓶子.";
		mes "收费是3500金币.";
		mes "嗯! 您应该准备好了... 可以开始工作了吗?";
		setarray .@item[0], 510, 973, 713;
		setarray .@count[0], 20,   1,   1;
		set .@cost,3500;
		set .@dyestuff,978;
		break;
	case 4:
		mes "嗯... 要做绿色染料需要蓝色药草 5个，绿色药草20个, 黄色药草20个，中和剂, 还有中和剂和空瓶子.";
		mes "有很多的材料不要搞乱 收费是5000金币.";
		mes "嗯! 您应该准备好了... 可以开始工作了吗?";
		setarray .@item[0], 510, 511, 508, 974, 973, 713;
		setarray .@count[0],  5,  20,  20,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,979;
		break;
	case 5:
		mes "嗯... 要做桔色染料需要20个红色的药草, 20个黄色的药草, 中和剂, 还有混合剂和空瓶子.";
		mes "收费是5000金币.";
		mes "嗯! 您应该准备好了... 可以开始工作了吗?";
		setarray .@item[0], 507, 508, 974, 973, 713;
		setarray .@count[0], 20,  20,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,980;
		break;
	case 6:
		mes "嗯... 要做紫色染料需要10个蓝色的药草，30个红色的药草, 解剂和, 还有和混合剂空瓶子.";
		mes "收费是5000金币.";
		mes "嗯! 您应该准备好了... 可以开始工作了吗?";
		setarray .@item[0], 510, 507, 974, 973, 713;
		setarray .@count[0], 10,  30,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,981;
		break;
	case 7:
		mes "嗯... 要做白色染料需要30个白色的药草还有中和剂和空瓶子.";
		mes "收费是3000金币.";
		mes "嗯! 您应该准备好了... 可以开始工作了吗?";
		setarray .@item[0], 509, 973, 713;
		setarray .@count[0], 30,   1,   1; 
		set .@cost,3000;
		set .@dyestuff,982;
		break;
	case 8:
		mes "嗯... 要做黑色染料需要各30个黄色绿色红色的药草, 蓝色药草, 中和剂, 还有混合剂和空瓶子.";
		mes "这个比较难 所以要收7000金币.";
		mes "嗯! 您应该准备好了... 可以开始工作了吗?";
		setarray .@item[0], 507, 508, 511, 510, 974, 973, 713;
		setarray .@count[0], 30,  30,  30,   5,   1,   1,   1;
		set .@cost,7000;
		set .@dyestuff,983;
		break;
	}
	next;
	if (select("做染料:取消") == 1) {
		set .@size, getarraysize(.@item);
		for( set .@i,0 ; .@i < .@size ; set .@i,.@i+1 )
			if( countitem(.@item[.@i]) < .@count[.@i] )
				break;
		if( .@i < .@size || Zeny < .@cost ) {
			mes "[染色商人]";
			mes " 不够...";
			mes "用这种材料做不出来让人满 意的颜色. 您要再去收集材料噢.";
			close;
		}
		for (set .@i, 0; .@i < .@size ; set .@i, .@i+1)
			delitem .@item[.@i], .@count[.@i];
		set zeny,zeny-.@cost;
		getitem .@dyestuff,1;

		mes "[染色商人]";
		mes "完成了! 这么完美的色泽唯有出自我手";
		mes "仔细看看... 这种颜色是多么的诱人啊!";
		next;
		mes "[染色商人]";
		mes "有需要制作其它染料的话, 再来找我吧";
		close;
	}
	mes "[染色商人]";
	mes "这这这!! 这是什么?";
	mes "到现在才要改变主意 真是无奈...";
	return;
}