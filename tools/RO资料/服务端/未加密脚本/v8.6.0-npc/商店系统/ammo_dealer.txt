//===== rAthena Script =======================================
//= Bullet Dealers
//===== By ===================================================
//= Playtester, Paradox924X
//===== Version ==============================================
//= 1.3
//===== Compatible With ======================================
//= rAthena SVN
//===== Description ==========================================
//= Bullet trader.
//===== Comments =============================================
//= 1.0 First version [Playtester]
//= 1.1 Converted from Aegis [Paradox924X]
//= 1.2 More optimized conversion [Legionaire]
//= 1.2a Removed .GATs [Lupus]
//= 1.3 Fixed [Playtester] Optimized. Got rid of @vars [Lupus]
//============================================================

alberta,118,160,3	script	弹药经销商 托尼#alb::bdt	86,{

	mes "[汤尼]";
	if (BaseJob == Job_Gunslinger) {
		mes "我就是神枪手汤尼!";
		mes "无论什么时候只要你的子弹用光了,";
		mes "都可以来找我!!!";
		next;
		mes "[汤尼]";
		mes "那么现在~! 兄弟您需要什么 ~";
		mes "尽情的挑选吧，不要忘记付钱~!";
		next;
		switch(select("毒物子弹:火焰子弹:雷击子弹:暗盲子弹:冰冻子弹:取消")) {
		case 1: callfunc "Bullet_Trade",937,10,13205; break;
		case 2: callfunc "Bullet_Trade",7097,2,13203; break;
		case 3: callfunc "Bullet_Trade",7053,3,13204; break;
		case 4: callfunc "Bullet_Trade",1024,5,13206; break;
		case 5: callfunc "Bullet_Trade",7054,2,13207; break;
		default:
			mes "[汤尼]";
			mes "恩恩~很好 ~";
			mes "欢迎随时来找我~";
			mes "我,子弹之王汤尼,";
			mes "一直在这里满足您的需求~!!!";
			close;
		}
	}
	mes "我是专门负责买卖交换";
	mes "枪手用品的商人.";
	mes "你可以叫我神枪手汤尼.";
	next;
	mes "[汤尼]";
	mes "你的职业不是枪手,所以这里没有你需要的";
	mes "随便看看";
	mes "就走吧";
	close;
}

function	script	Bullet_Trade	{
	mes "[汤尼]";
	mes "请输入你要购买的数量.";
	next;
	mes "[汤尼]";
	mes "^0000FF1^E40CAA 个强化武器金属-级数一 和 ^0000FF1^E40CAA 个强化武器金属-级数二,还有 ^0000FF"+getarg(1)+"^E40CAA 个"+getitemname(getarg(0))+".";
	mes "可以和我交换 ^ff0000"+getitemname(getarg(2))+"^000000 30个 ";
	next;
	mes "[汤尼]";
	mes "最多一次可以交易500个.";
	mes "如果你要取消交易, 输入0.";
	next;
	input .@amount;
	mes "[汤尼]";
	if (.@amount < 1 || .@amount > 500) {
		mes "无效数字!";
		mes "重新输入~!";
		close;
	} else if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {
			mes "你物品栏里的东西满了,我不能交易给你. 把你的物品栏清理一下再回来找我吧.";
			close;
		} else {
			mes "噢~ 很好!";
			mes "交易数量确定!";
			mes "我会马上交易给你.";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			getitem getarg(2),.@amount * 30;
			close;
		}
	} else {
		mes "咳咳~";
		mes "你身上的物品数量不足";
		mes "所以,我无法";
		mes "将我的商品交易给你.";
		mes "准备好了再回来找我吧.";
		close;
	}
}
