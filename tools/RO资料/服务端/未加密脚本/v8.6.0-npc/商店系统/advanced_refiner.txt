//===== rAthena Script =======================================
//= Advanced Refiner
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4b
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= After a conversation with Doddler, it's been established that
//= the advanced refiner works similar the the "Bubble Gum" item.
//= The success percentage is not "increased" however, if it fails
//= You get a second try. This tries twice at the same time,
//= effectively giving you a re-roll on your attempt.
//= - Dialog is only partly official to iRO.
//= - Uses the iRO position for this NPC.
//===== Additional Comments: =================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed a weird carriage return. o_o [L0ne_W0lf]
//= 1.2 Optimizing refine method [Zephyrus]
//= 1.3 Typo fixes [Yommy]
//= 1.4 Removed unnecessary dialogs [Zephyrus]
//= 1.4a Added 'disable_items' command. [Euphy]
//= 1.4b Fixed coordinates. [Euphy]
//============================================================

payon,157,146,6	script	龙仙儒#cash_payon::LXRefiner	85,{
	disable_items;
	mes "[龙仙儒]";
	mes "我是专门锻治武器与防具的铁匠，";
	mes "我可以为你锻治装置专案中的物品。";
	mes "你想要锻治装置专案中的哪一项物品呢?";
	next;
	setarray .@position$[1], "头部","身体","左手","右手","披肩","鞋子","装饰品1","装饰品2","头中","头下";
	set .@menu$,"";
	for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
	{
		if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

		set .@menu$, .@menu$ + ":";
	}
	set .@part,select(.@menu$);

	if( !getequipisequiped(.@part) )
		close;

	if( !getequipisenableref(.@part) )
	{
		mes "[龙仙儒]";
		mes "你说的这个项目是无法锻治的项目喔!...";
		close;
	}
	if( getequiprefinerycnt(.@part) >= 10 )
	{
		mes "[龙仙儒]";
		mes "这项武器已经锻治到10级了，以我的能力，无法再锻治了..";
		close;
	}

	// Make sure you have the neccessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	switch( getequipweaponlv(.@part) )
	{
		case 1: callsub S_RefineValidate,1,7620,50,.@part; break;
		case 2: callsub S_RefineValidate,2,7620,200,.@part; break;
		case 3: callsub S_RefineValidate,3,7620,5000,.@part; break;
		case 4: callsub S_RefineValidate,4,7620,20000,.@part; break;
		default: callsub S_RefineValidate,0,7619,2000,.@part; break;
	}

	if( getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100) )
	{
		mes "[龙仙儒]";
		mes "锵! 锵! 锵!";
		SuccessRefItem .@part;
		next;
		Emotion e_no1;
		mes "[龙仙儒]";
		mes "拿去吧! 已经做好了。";
		mes "好久没锻治出那么优质的武器了，武器变强了你一定也很高兴吧!";
		close;
	}
	mes "[龙仙儒]";
	mes "锵! 锵! 锵!";
	FailedRefItem .@part;
	next;
	if (rand(5) == 1){
		Emotion e_cash;
	} else {
		Emotion e_omg;
	}
	mes "[龙仙儒]";
	mes "咳咳咳咳咳咳咳!!!!";
	next;
	mes "[龙仙儒]";
	mes "咳...咳咳...";
	mes "真...丢脸。";
	mes "武器在锻治的程序中损坏了。";
	mes "我...我...早就告诉过你会这样的嘛!";
	close;

S_RefineValidate:
	mes "[龙仙儒]";
	if (getarg(0))
		mes "你想要锻治武器等级" + getarg(0) + "的武器啊!";
	mes "要锻治的话需要 ^ff9999" + getitemname(getarg(1)) + "^000000";
	mes "以及费用 " + getarg(2) + " Zeny.";
	mes "你要继续吗?";
	next;
	if( select("是的:不要") == 1 )
	{
		if( getequippercentrefinery(getarg(3)) < 100 )
		{
			mes "[龙仙儒]";
			mes "呜哇!!";
			mes "看来这武器已经锻治很多次了的样子...";
			mes "要是再继续锻治的话，可能会造成损坏喔!";
			next;
			mes "要是损坏的话可就不能继续使用了!!";
			mes "插在里面的卡片和特征";
			mes "^ff0000全部都会消失了!!^000000";
			mes "再说，防具被消失了!!";
			mes "你确定还要继续锻治吗?";
			next;
			if( select("是:否") == 2 )
			{
				mes "[龙仙儒]";
				mes "很好。";
				mes "如果太过无理的锻治武器，而造成损坏，我心情也会跟着变坏的。";
				close;
			}
		}
		if( countitem(getarg(1)) > 0 && Zeny > getarg(2) )
		{
			delitem getarg(1), 1;
			set Zeny, Zeny - getarg(2);
			return;
		}
		mes "[龙仙儒]";
		mes "你看起来没有足够的金钱或者 "+getitemname(getarg(1))+"...";
		mes "回去拿一些来，我会在这里等你的";
		close;
	}
	mes "[龙仙儒]";
	mes "嗯啊... 没有必要急于现在就精炼~";
	mes "慢慢来~~";
	close;
}

morocc,183,94,4	duplicate(LXRefiner)	龙仙儒#cash_morocc	85