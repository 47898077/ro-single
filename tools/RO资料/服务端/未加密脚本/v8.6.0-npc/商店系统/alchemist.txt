//===== rAthena Script ======================================= 
//= Alchemist Shop
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: ================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//=     Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//= 1.7 Updated to match AEGIS script. [Masao]
//============================================================ 

alde_alche,31,186,3	script	制作书销售员#alde	883,{

	mes "[制作书销售员]";
	mes "欢迎光临.";
	mes "我专门贩售";
	mes "遗传学者的^0000FF特殊制作书籍^000000.";
	next;
	mes "[制作书销售员]";
	mes "遗传学者需要习得一些技能";
	mes "才能使用制作书.";
	next;
	mes "[制作书销售员]";
	mes "如果你有多种制作书籍,";
	mes "你能使用技能制做更多物品.";
	next;
	mes "[制作书销售员]";
	mes "请理解";
	mes "我只销售这些特殊制作书籍";
	mes "我 ^FF0000不卖其他东西^000000.";
	next;
	mes "[制作书销售员]";
	mes "那么, 以下是制作书清单.";
	next;
	switch(select("[苹果炸弹制作书] 100,000 zeny:[凤梨炸弹制作书] 100,000 zeny:[椰子炸弹制作书] 100,000 zeny:[哈密瓜炸弹制作书] 100,000 zeny:[香蕉炸弹制作书] 100,000 zeny:[植物基因栽培法] 100,000 zeny:[高级药水制作说明书] 100,000 zeny:[混合料理书] 100,000 zeny:[体力暴增研究本] 100,000 zeny:[活力饮料制作书] 100,000 zeny:取消")){
	case 1:
		callsub S_SellManual,6279,100000;
		break;
	case 2:
		callsub S_SellManual,6280,100000;
		break;
	case 3:
		callsub S_SellManual,6281,100000;
		break;
	case 4:
		callsub S_SellManual,6282,100000;
		break;
	case 5:
		callsub S_SellManual,6283,100000;
		break;
	case 6:
		callsub S_SellManual,6284,100000;
		break;
	case 7:
		callsub S_SellManual,6285,100000;
		break;
	case 8:
		callsub S_SellManual,11022,100000;
		break;
	case 9:
		callsub S_SellManual,11023,100000;
		break;
	case 10:
		callsub S_SellManual,11024,100000;
		break;
	case 11:
		mes "[制作书销售员]";
		mes "谢谢您的惠顾.";
		mes "欢迎再次光临.";
		close;
	}

S_SellManual:
	mes "[制作书销售员]";
	mes "你想要"+getitemname(getarg(0))+"?";
	mes "总共需要 "+getarg(1)+" zeny.";
	mes "想购买多少本?";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[制作书销售员]";
		mes "你想看其他书籍吗?";
		close;
	}
	if (.@amount < 1 || .@amount > 99 ){
		mes "[制作书销售员]";
		mes "你无法购买超过99本数量上限.";
		close;
	}
	mes "[制作书销售员]";
	mes "你输入了 "+.@amount+"本 "+getitemname(getarg(0))+".";
	mes "总共需要 "+getarg(1) * .@amount+" Zeny.";
	mes "要继续吗?";
	next;
	if (select("是的:不要") == 1) {
		if (Zeny < getarg(1) * .@amount) {
			mes "[制作书销售员]";
			mes "你没有";
			mes "足够的金钱.";
			mes "请确认你是否";
			mes "有足够的金钱.";
			close;
		}
		if (checkweight(getarg(0),.@amount) == 0){
			mes "[制作书销售员]";
			mes "你无法携带更多东西.";
			mes "请确认是否有足够的携带空间.";
			close;
		}
		mes "[制作书销售员]";
		mes "谢谢您的惠顾.";
		set Zeny,Zeny - getarg(1) * .@amount;
		getitem getarg(0),.@amount;
		close;
	}
	mes "[制作书销售员]";
	mes "请花点时间";
	mes "考虑一下在做出决定.";
	close;
}

alde_alche,24,188,3	script	综合贩卖员	740,{
	if (checkweight(1201,1) == 0) {
		mes "- 请等等! -";
		mes "- 你身上携带了太多物品了 -";
		mes "- 先把东西多余的东西 -";
		mes "- 寄放在卡普拉仓库之后 -";
		mes "- 再回来吧! -";
		close;
	}
	mes "[葛伯 纱普]";
	mes "欢迎来到炼金术师组织，";
	mes "需要什么服务吗?";
	next;
	switch (select("购买制药物品:购买制作说明书:取消")) {
	case 1:
		mes "[葛伯 纱普]";
		mes "请问需要什么物品";
		next;
		if (select("药钵 - 8 Zeny:停止交易") == 1) {
			mes "[葛伯 纱普]";
			mes "请问需要几个?";
			mes "如想要停止交易请写 '0' ";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[葛伯 纱普]";
					mes "交易已经取消，下次再说吧";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[葛伯 纱普]";
					mes "单次购买数量必须是1至2000个之间的数";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[葛伯 纱普]";
				mes "金额不足噢";
				mes "请确认一下所携带的金额";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[葛伯 纱普]";
				mes "看样子都不能带走啊，";
				mes "请确认一下所携带的物品量和负重";
				close;
			}
			set zeny,zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[葛伯 纱普]";
			mes "感谢你的购买，";
			mes "愿你做出实用的药";
			close;
		}
		mes "[葛伯 纱普]";
		mes "好吧~";
		mes "如果你需要制药相关的道具，可以随时找我~";
		close;
	case 2:
		mes "[葛伯 纱普]";
		mes "请问需要哪些物品?";
		mes "一般的制作说明书是100,000金币，";
		mes "比较特殊的几种制作说明书除外";
		next;
		switch(select("一般药水制作说明书:酒精制作说明书:火烟瓶制作说明书:盐酸制作说明书:植物瓶制作说明书:刺锤瓶制作说明书:护贝药制作说明书:纤细药水制作说明书:取消交易")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[葛伯 纱普]";
			mes "好吧~";
			mes "如果你需要制药相关说明书的话，可以随时找我~";
			close;
		}
	case 3:
		mes "[葛伯 纱普]";
		mes "好滴，祝你有个美好的一天~";
		close;
	}

S_SellManual:
	if (Zeny < getarg(1)) {
		mes "[葛伯 纱普]";
		mes "金额不足噢";
		mes "请确认一下所携带的金额";
		close;
	}
	mes "[葛伯 纱普]";
	mes ""+getitemname(getarg(0))+"?";
	mes "购买这本说明书需要 "+getarg(1)+" zeny.";
	next;
	if (select("确定购买:不买了") == 1) {
		set zeny,zeny-getarg(1);
		getitem getarg(0),1;
		mes "[葛伯 纱普]";
		mes "谢谢惠顾~";
		mes "愿你做出实用的药";
		close;
	}
	mes "[葛伯 纱普]";
	mes "好的，那么请下次再来吧！";
	close;
}
